;-------------------------------------------------
; 都市関連 시설
;-------------------------------------------------
@CITY_CENTER
#DIM L_FACI_ID ;시설ID
#DIM L_INPUT   ;入力内容
DRAWLINE
FOR L_FACI_ID, 1, 9
	CALL SHOW_FACILITY_LEVEL(L_FACI_ID)
NEXT
FOR L_FACI_ID, 50, 57
	CALL SHOW_FACILITY_LEVEL(L_FACI_ID)
NEXT
DRAWLINE
PRINTFORML
PRINTFORML [999] 돌아간다
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 999
		JUMP SYSTEM_MENU
	CASEELSE
		TRYCCALLFORM CITY_F_{L_INPUT}
			SIF FLAG:ターン終了フラグ == 1
				RETURN
		CATCH
			PRINTFORMW 미실장 시설입니다.
		ENDCATCH
		BREAK
	ENDSELECT
LOOP 1
RESTART


;시설レベル表示関数
@SHOW_FACILITY_LEVEL(L_FACI_ID)
#DIM  L_FACI_ID    ;시설ID
#DIM  L_FACI_LEVEL ;フラグ参照用
#DIM  L_FACI_POINT ;フラグ参照用
#DIM  L_color      ;色バックアップ
L_FACI_LEVEL = FLAG:(4000 + L_FACI_ID)
L_FACI_POINT = FLAG:(4100 + L_FACI_ID)
IF L_FACI_LEVEL <= 0
	L_color = GETCOLOR()
	SIF FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORML ？？？(미건설)({L_FACI_LEVEL})
	SIF FLAG:STATUS伏字モード
		SETCOLOR L_color
ELSE
	PRINTFORML [{L_FACI_ID, 2}]【%FLAGNAME:(4000 + L_FACI_ID), 20 ,LEFT%:레벨 {L_FACI_LEVEL, 3 ,LEFT}({L_FACI_POINT, 3}/{L_FACI_LEVEL * 10, 3})】
ENDIF



;시설のキャラ配属関数
@FACILITY_CHARA_SET(L_C_ID, L_FACI_ID)
#DIM L_C_ID                      ;任命対象キャラの登録番号
#DIM L_FACI_ID                   ;시설ID
#DIM CONST L_CFLAG_START = 10000 ;시설用CFLAGの開始値
#DIM CONST L_CFLAG_END   = 10100 ;시설用CFLAGの終了値（このID自体は含まれない）
#DIM L_CHARA_ORDER               ;対象キャラがすでに配属されている시설
L_CHARA_ORDER = FINDELEMENT(CFLAG:L_C_ID:0, 1, L_CFLAG_START, L_CFLAG_END)
IF L_CHARA_ORDER == L_FACI_ID + L_CFLAG_START
	PRINTFORMW 이미 %CFLAGNAME:L_CHARA_ORDER%에 임명되어 있습니다.
	RETURN
ELSEIF L_CHARA_ORDER > 0
	PRINTFORMW 이미 %CFLAGNAME:L_CHARA_ORDER%에 임명되어 있습니다.
	PRINTFORML 해임하고 재배치하시겠습니까?
	PRINTFORML [1] - 재배치한다
	PRINTFORML [0] - 그만둔다
	DO
		INPUT
		SIF RESULT == 1
			BREAK
		SIF RESULT == 0
			RETURN
	LOOP 1
	CALL FACILITY_CHARA_REMOVE(L_C_ID)
ENDIF
CFLAG:L_C_ID:(L_CFLAG_START + L_FACI_ID) = 1
PRINTFORMW %조사처리(CALLNAME:L_C_ID,"를")% %CFLAGNAME:(L_CFLAG_START + L_FACI_ID)%에 임명했습니다.



;시설のキャラ解任関数
;初期化範囲が10000～10098（10099が初期化漏れしてるように見える）だったので10099を初期化範囲に入れた
;終端まで使い切ってないので問題は表面化しないとは思う
@FACILITY_CHARA_REMOVE(L_C_ID)
#DIM L_C_ID                      ;任命対象キャラの登録番号
#DIM CONST L_CFLAG_START = 10000 ;시설用CFLAGの開始値
#DIM CONST L_CFLAG_END   = 10100 ;시설用CFLAGの終了値（このID自体は含まれない）
SIF TALENT:L_C_ID:부재 == 0
	PRINTFORMW %조사처리(CALLNAME:L_C_ID,"를")% 임무에서 제외했습니다.
VARSET CFLAG:L_C_ID:0, 0, L_CFLAG_START, L_CFLAG_END





;시설任命処理関数
@CITY_FACILITY_ORDER(L_FACI_ID)
#DIM L_FACI_ID                   ;시설ID
#DIM L_C_ID                      ;任命対象キャラの登録番号
#DIM L_ORDER_CNT                 ;시설の任命済みのキャラ数の取得
#DIM L_INPUT                     ;入力内容
CLEARLINE LINECOUNT
DRAWLINE
CALL SHOW_FACILITY_INTRODUCE(L_FACI_ID, "시설名")
DRAWLINE
CALL SHOW_FACILITY_INTRODUCE(L_FACI_ID, "参加者")
L_ORDER_CNT = FLAG:범용변수
DRAWLINE
CALL SHOW_FACILITY_INTRODUCE(L_FACI_ID, "説明文")
DRAWLINE
CALL SHOW_FACILITY_INTRODUCE(L_FACI_ID, "任命ボタン")
;ボタンは共通で
;2　任命
;1　任命解除
;0　もどる
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
	CASE 0
		RETURN
	CASE 2, 1
		$LOOP_INPUT2
		;入力ループ内なので配属人数の更新が必要
		SKIPDISP 1
		CALL SHOW_FACILITY_INTRODUCE(L_FACI_ID, "参加者")
		L_ORDER_CNT = FLAG:범용변수
		SKIPDISP 0

		CLEARLINE LINECOUNT
		DRAWLINE
		IF L_INPUT == 1
			PRINTFORML 등록을 해제할 거주민을 선택해주세요.
		ELSE
			PRINTFORML 임명할 거주민을 선택해주세요.
		ENDIF
		DRAWLINE
		CALL LIST_CHARA("시설配属")
		L_C_ID = RESULT
		IF L_C_ID == 1000
			RESTART
		ELSEIF !INRANGE(L_C_ID, 0, CHARANUM - 1)
			PRINTFORMW 입력된 번호는 캐릭터 범위 외입니다.
			GOTO LOOP_INPUT2
		ELSEIF L_C_ID == 0
			PRINTFORMW 당신은 선택할 수 없습니다.
			GOTO LOOP_INPUT2
		ENDIF
		
		IF L_INPUT == 1
			CALL FACILITY_CHARA_REMOVE(L_C_ID)
			GOTO LOOP_INPUT2
		ELSEIF L_INPUT == 2
			IF L_ORDER_CNT >= 5
				PRINTFORMW 5명까지 배속할 수 있습니다.
				GOTO LOOP_INPUT2
			ENDIF
			CALL FACILITY_CHARA_SET(L_C_ID, L_FACI_ID)
			GOTO LOOP_INPUT2
		ENDIF
		
	CASEELSE
ENDSELECT
RESTART



;任命処理の시설別の表示関数
@SHOW_FACILITY_INTRODUCE(L_FACI_ID, L_TYPE, OPT_MARKER = -1)
#DIM        L_FACI_ID         ;시설ID
#DIMS       L_TYPE            ;表示타입
#DIM        OPT_MARKER        ;強調表示するID
;可読性確保のための定数系
#DIM  CONST L_학원       = 1  
#DIM  CONST L_지원주택   = 7  
#DIM  CONST L_러브호텔 = 50
#DIM  CONST L_고급카지노 = 53 
#DIM  CONST L_훈련장     = 56 
SELECTCASE L_TYPE
CASE "시설名"
	SELECTCASE L_FACI_ID
	CASE L_학원
		CALL PRINT_LABEL("학원")
		PRINTL
	CASE L_지원주택
		CALL PRINT_LABEL("지원주택")
		PRINTL
	CASE L_러브호텔
		CALL PRINT_LABEL("러브호텔")
		PRINTL
	CASE L_고급카지노
		CALL PRINT_LABEL("고급카지노")
		PRINTL
	CASE L_훈련장
		CALL PRINT_LABEL("훈련장")
		PRINTL
	ENDSELECT
CASE "参加者"
	SELECTCASE L_FACI_ID
		;@MEMBER_LIST2はFLAG:범용변수を操作します
	CASE L_학원
		PRINTFORM 【학생：
		CALL MEMBER_LIST2(GETNUM(CFLAG, "학원生徒"), 1, OPT_MARKER)
		PRINTFORML 】
	CASE L_지원주택
		PRINTFORM 【점원:
		CALL MEMBER_LIST2(GETNUM(CFLAG, "支援자원봉사자"), 1, OPT_MARKER)
		PRINTFORML 】
	CASE L_러브호텔
		PRINTFORM 【점원:
		CALL MEMBER_LIST2(GETNUM(CFLAG, "러브호텔店員"), 1, OPT_MARKER)
		PRINTFORML 】
	CASE L_고급카지노
		PRINTFORM 【점원：
		CALL MEMBER_LIST2(GETNUM(CFLAG, "カジノ店員"), 1, OPT_MARKER)
		PRINTFORML 】
	CASE L_훈련장
		PRINTFORM 【훈련：
		CALL MEMBER_LIST2(GETNUM(CFLAG, "훈련장関係者"), 1, OPT_MARKER)
		PRINTFORML 】
	ENDSELECT
CASE "説明文"
	SELECTCASE L_FACI_ID
	CASE L_학원
		PRINTFORML 거주민을 재교육시킵니다.
		PRINTFORML 임명된 거주민은 모럴이 상승합니다. (정원: 5명)
	CASE L_지원주택
		PRINTFORML 거주민을 자원봉사에 참가시킵니다.
		PRINTFORML 자금을 얻을 수 없고, 카르마가 감소합니다. (정원: 5명)
	CASE L_러브호텔
		PRINTFORML %마스터는% 이곳을 창관으로 이용할 수가 있습니다.
		PRINTFORML 거주민을 임명함으로써, 턴 종료 시에 창부로 일하며 자금을 벌어들입니다. (정원: 5명)
	CASE L_고급카지노
		PRINTFORML 거주민을 임명함으로써, 턴 종료 시에 호객 행위로 자금을 벌어들입니다.
		PRINTFORML 수입은 거주민의 무자각소질의 질에 따라 변동합니다. (정원: 5명)
		SETCOLOR 250,50,50
		PRINTFORML 이 때, 모럴이 낮은 거주민, 또는 카르마가 높은 거주민은 주의할 것.
		RESETCOLOR
	CASE L_훈련장
		PRINTFORML 거주민에게 전투 훈련을 행합니다.
		PRINTFORML 소속된 거주민들은 능력이 매우 조금 상승합니다.
	ENDSELECT
	DRAWLINE
CASE "任命ボタン"
	SELECTCASE L_FACI_ID
	CASE L_학원
		PRINTFORML [2] 학생 임명
		PRINTFORML [1] 임명 해제
		PRINTFORML 
		PRINTFORML [0] 돌아간다
	CASE L_지원주택
		PRINTFORML [2] 점원 임명
		PRINTFORML [1] 임명 해제
		PRINTFORML 
		PRINTFORML [0] 돌아간다
	CASE L_러브호텔
		PRINTFORML [2] 점원 임명
		PRINTFORML [1] 임명 해제
		PRINTFORML 
		PRINTFORML [0] 돌아간다
	CASE L_고급카지노
		PRINTFORML [2] 점원 임명
		PRINTFORML [1] 임명 해제
		PRINTFORML 
		PRINTFORML [0] 돌아간다
	CASE L_훈련장
		PRINTFORML [2] 훈련 등록
		PRINTFORML [1] 등록 해제
		PRINTFORML 
		PRINTFORML [0] 돌아간다
	ENDSELECT
ENDSELECT




;キャラ配属関数
;能力表示から시설にキャラを配属する用
@SELECT_WOKER(L_C_ID)
#DIM         L_C_ID                     ;対象登録番号
#DIM         L_FACI_ID                  ;対象になった시설ID
#DIM         L_FACI_LIST = 1, 7, 50, 53 ;학원、지원주택、러브호텔、고급카지노
#DIM DYNAMIC L_CNT_WOKER, 4             ;各시설に配属済みのキャラ数
#DIM         L_FACI_LEVEL               ;フラグ参照用、시설レベル
#DIM CONST   L_CFLAG_START = 10000      ;시설用CFLAGの開始値
#DIM CONST   L_CFLAG_END   = 10100      ;시설用CFLAGの終了値（このID自体は含まれない）
#DIM         L_C_ID_REMOVE              ;解任対象登録番号
IF L_C_ID == 0
	PRINTFORMW ≪「플레이어」, 농담은 적당히 하세요≫
	RETURN
ENDIF

L_FACI_ID = FINDELEMENT(CFLAG:L_C_ID:0, 1, L_CFLAG_START, L_CFLAG_END)
IF L_FACI_ID < 0
	; L_FACI_IDを手抜きでつかいまわしてますごめんなさい
	PRINTFORML %조사처리(CALLNAME:L_C_ID,"를")% 배정할 시설을 선택해주세요.
ELSE
	PRINTFORML %CALLNAME:L_C_ID%（%CFLAGNAME:L_FACI_ID%）%조사만처리(CALLNAME:L_C_ID,"를")% 배정할 시설을 선택해주세요.
ENDIF
CALL SHOW_ALL_WORKERS(L_FACI_LIST, L_CNT_WOKER, , L_C_ID )
PRINTFORML [ 0] 취소
DO
	INPUT
	L_FACI_ID = RESULT
	L_FACI_LEVEL = FLAG:(4000 + L_FACI_ID)
	IF L_FACI_ID == 0
		RETURN
	ELSEIF FINDELEMENT(L_FACI_LIST, L_FACI_ID) < 0
		CONTINUE
	ELSEIF CFLAG:L_C_ID:(L_CFLAG_START + L_FACI_ID)
		PRINTFORMW %조사처리(CALLNAME:L_C_ID,"는")% 이미 %CFLAGNAME:(L_CFLAG_START + L_FACI_ID)%에 임명되어 있습니다.
		RESTART
	ELSEIF L_FACI_LEVEL <= 0
		PRINTFORMW 그 시설은 완성되지 않았습니다.
		RESTART
	ENDIF
	BREAK
LOOP 1
IF L_CNT_WOKER:(FINDELEMENT(L_FACI_LIST, L_FACI_ID)) >= 5
	PRINTFORML %CFLAGNAME:(L_CFLAG_START + L_FACI_ID)%의 인원 수가 많습니다.
	PRINTFORML 해임할 캐릭터를 선택해주세요.
	FOR LOCAL, 1, CHARANUM
		SIF TALENT:LOCAL:부재
			CONTINUE
		SIF CFLAG:LOCAL:(L_CFLAG_START + L_FACI_ID) != 1
			CONTINUE
		PRINTFORML [{LOCAL, 3}] - %CALLNAME:LOCAL%
	NEXT
	PRINTFORML [999] - 시설 재선택
	DO
		INPUT
		L_C_ID_REMOVE = RESULT
		SIF L_C_ID_REMOVE == 999
			RESTART
		IF !INRANGE(L_C_ID_REMOVE, 1, CHARANUM - 1)
		ELSEIF TALENT:L_C_ID_REMOVE:부재
		ELSEIF CFLAG:L_C_ID_REMOVE:(L_CFLAG_START + L_FACI_ID) != 1
		ELSE
			BREAK
		ENDIF
	LOOP 1
	CALL FACILITY_CHARA_REMOVE(L_C_ID_REMOVE)
ENDIF
CALL FACILITY_CHARA_SET(L_C_ID, L_FACI_ID)



@SHOW_ALL_WORKERS(L_FACI_LIST, L_CNT_WOKER, OP = "", MARKER_CID = -1)
#DIM  REF  L_FACI_LIST        ;시설IDリスト
#DIM  REF  L_CNT_WOKER        ;各시설の店員の人数、配列を直接書き換える
#DIM       L_LIST_ID          ;参照中のリスト位置
#DIM       L_MEMORY           ;범용변수の書き換え防止措置
#DIM       L_FACI_LEVEL       ;フラグ参照用、시설レベル
#DIMS      OP                 ;実行オプション
#DIM       MARKER_CID         ;強調表示するキャラID
#DIM       L_color
L_MEMORY = FLAG:범용변수
VARSET L_CNT_WOKER

IF STRCOUNT(OP, "「ボタン無し」")
	FOR L_LIST_ID, 0, VARSIZE("L_FACI_LIST")
		L_FACI_LEVEL = FLAG:(4000 + L_FACI_LIST:L_LIST_ID)
		IF L_FACI_LEVEL <= 0
			L_color = GETCOLOR()
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORML ？？？(미건설)
			SIF FLAG:STATUS伏字モード
				SETCOLOR L_color
		ELSE
			CALL SHOW_FACILITY_INTRODUCE(L_FACI_LIST:L_LIST_ID, "시설名")
			PRINTFORM 　　
			CALL SHOW_FACILITY_INTRODUCE(L_FACI_LIST:L_LIST_ID, "参加者", MARKER_CID )
			L_CNT_WOKER:L_LIST_ID = FLAG:범용변수
		ENDIF
	NEXT
ELSE
	FOR L_LIST_ID, 0, VARSIZE("L_FACI_LIST")
		L_FACI_LEVEL = FLAG:(4000 + L_FACI_LIST:L_LIST_ID)
		IF L_FACI_LEVEL <= 0
			L_color = GETCOLOR()
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORML ？？？(미건설)
			SIF FLAG:STATUS伏字モード
				SETCOLOR L_color
		ELSE
			PRINTFORM [{L_FACI_LIST:L_LIST_ID, 2}]
			CALL SHOW_FACILITY_INTRODUCE(L_FACI_LIST:L_LIST_ID, "시설名")
			PRINTFORM 　　
			CALL SHOW_FACILITY_INTRODUCE(L_FACI_LIST:L_LIST_ID, "参加者", MARKER_CID)
			L_CNT_WOKER:L_LIST_ID = FLAG:범용변수
		ENDIF
	NEXT
ENDIF

FLAG:범용변수 = L_MEMORY
;広域変数はどこでバグるかわからないので念の為
