
;-------------------------------------------------
;시설　연구시설
;-------------------------------------------------
@CITY_F_52
CALL MANAGE_CHARA_SELECT("MNG_特殊_身体強化")




@LABORATORY_BREEDING_SELECT_MOTHER
#DIM L_MOTHER
CLEARLINE LINECOUNT
DRAWLINE
CALL PRINT_LABEL("차세대창조")
PRINTL
PRINTFORML 최첨단 기술을 이용해, 새로운 세대를 창조합니다.
PRINTFORML 새로운 세대는 이전 세대에서 ＋ 보정치를 받습니다.
DRAWLINE
PRINTFORML 계승의 주체가 될 주민을 선택해주세요.
CALL PRINT_LABEL("조건")
PRINTFORML 출산횟수 ＞계승횟수 (주민등록 이전의 출산횟수는 포함되지 않습니다.)
CALL LIST_CHARA("차세대창조")
L_MOTHER = RESULT
SIF L_MOTHER == 1000
	RETURN
IF L_MOTHER == 0
	PRINTFORMW %마스터는% 선택할 수 없습니다.
ELSEIF !INRANGE(L_MOTHER, 0, CHARANUM - 1)
	PRINTFORMW 입력된 번호는 캐릭터 범위 외입니다.
ELSEIF CFLAG:L_MOTHER:주민_회수済み数  <= CFLAG:L_MOTHER:주민_継承
	PRINTFORMW 조건을 만족시키지 못했습니다.
ELSE
	JUMP LABORATORY_BREEDING_BABY_DESIGNING(L_MOTHER)
ENDIF
RESTART



@LABORATORY_BREEDING_BABY_DESIGNING(L_MOTHER)
#DIM L_MOTHER
#DIM L_BABY
#DIM DYNAMIC L_CREATE_BABY = 1
#DIM DYNAMIC L_DESIGN_FLAG = 0
#DIM L_INPUT
#DIM L_I
IF L_CREATE_BABY == 1
	L_CREATE_BABY = 0
	CALL LABORATORY_BREEDING_CREATE_BABY(L_MOTHER, L_BABY, L_DESIGN_FLAG); 역주) 새로운 캐릭터 작성
	CLEARLINE LINECOUNT
ENDIF
;リロードできるように？
SIF !TALENT:L_BABY:선조회귀
	CALL CHARACTER_SCOUT_COSTUME(L_BABY)
DRAWLINE
CALL C_PICTURE_S(L_BABY)
PRINTFORML ■------------------------------------ 　소질　 ------------------------------------■
CALL INFO_STATUS_TALENT(L_BABY)
DRAWLINE
PRINTFORML 창조할 주민의 세부사항을 설정해주세요.
DRAWLINE
PRINTFORML [0] 외모 설정 (무작위 선출)
PRINTFORML [1] 유전요소 선택
;PRINTLC [2] 
PRINTFORML 
;PRINTLC [3] 종족의 설정 (유전자 필요)
PRINTFORML 
PRINTFORML [100] 차세대창조
PRINTFORML 
PRINTFORML [999] 돌아간다
INPUT

L_INPUT = RESULT

SELECTCASE L_INPUT
	CASE 0
		IF L_BABY > 0
			CALL CANCEL_SCOUT(L_BABY)
			L_CREATE_BABY = 1
		ENDIF
	CASE 1
		CLEARLINE LINECOUNT
		CALL LABORATORY_BREEDING_SELECT_DESIGN(L_BABY, L_DESIGN_FLAG)
	CASE 2
	CASE 3
	CASE 100
		TARGET = L_BABY
		;모체측의 계승횟수 갱신이 있으므로 결정시의 이 타이밍이 아니면 안됨
		CALL LABORATORY_BREEDING_BABY_HEREDITY_CFLAG(L_MOTHER, L_BABY) ; 역주) c플래그유전 호출
		RETURN
	CASE 999
		SIF L_BABY > 0
			CALL CANCEL_SCOUT(L_BABY) ;새로운 캐릭터 작성 취소
		FLAG:継承종족 = 0
		RETURN
	CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
;차세대의 가입 처리
;-------------------------------------------------
@LABORATORY_BREEDING_CREATE_BABY(L_MOTHER, L_BABY, L_DESIGN_FLAG)
#DIM     L_MOTHER
#DIM REF L_BABY
#DIM     L_DESIGN_FLAG
;여기만큼은 아무래도 사용하지 않을 수 없고
FLAG:継承종족 = (CFLAG:L_MOTHER:주민_종족) + 1

FLAG:범용변수 = 52
CALL CHARACTER_SCOUT1
L_BABY = CHARANUM - 1
;차세대 소질 유전
CALL LABORATORY_BREEDING_BABY_HEREDITY_TALENT(L_MOTHER, L_BABY)
CALL LABORATORY_BREEDING_FORMING_DESIGN(L_MOTHER, L_BABY, L_DESIGN_FLAG)
SIF TALENT:L_BABY:선조회귀
	CALL LABORATORY_BREEDING_REVERSION(L_MOTHER, L_BABY)
FLAG:범용변수 = 0


;-------------------------------------------------
; 遺伝の継承　범위축소適用
;-------------------------------------------------
@LABORATORY_BREEDING_FORMING_DESIGN(L_MOTHER, L_BABY, L_DESIGN_FLAG)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_DESIGN_FLAG
#DIM  CONST L_BIT_머리색 = 0
#DIM  CONST L_BIT_눈동자색 = 1
#DIM  CONST L_BIT_胸   = 2
#DIM  CONST L_BIT_尻   = 3
#DIM  CONST L_BIT_체격 = 4
SIF GETBIT(L_DESIGN_FLAG, L_BIT_머리색)
	CFLAG:L_BABY:머리색 = CFLAG:L_MOTHER:머리색
SIF GETBIT(L_DESIGN_FLAG, L_BIT_눈동자색)
	CFLAG:L_BABY:눈동자색 = CFLAG:L_MOTHER:눈동자색
IF GETBIT(L_DESIGN_FLAG, L_BIT_胸)
	TALENT:L_BABY:폭유 = TALENT:L_MOTHER:폭유
	TALENT:L_BABY:거유 = TALENT:L_MOTHER:거유
	TALENT:L_BABY:빈유 = TALENT:L_MOTHER:빈유
	TALENT:L_BABY:절벽 = TALENT:L_MOTHER:절벽
	CFLAG:L_BABY:가슴사이즈 = CHARACTER_BUST_RETURN(L_BABY)
ENDIF
IF GETBIT(L_DESIGN_FLAG, L_BIT_尻)
	TALENT:L_BABY:거대엉덩이 = TALENT:L_MOTHER:거대엉덩이
	TALENT:L_BABY:큰엉덩이 = TALENT:L_MOTHER:큰엉덩이
	CFLAG:L_BABY:힙사이즈 = CHARACTER_HIP_RETURN(L_BABY)
ENDIF
IF GETBIT(L_DESIGN_FLAG, L_BIT_체격)
	TALENT:L_BABY:소인체형 = TALENT:L_MOTHER:소인체형
	TALENT:L_BABY:작은체형 = TALENT:L_MOTHER:작은체형
	TALENT:L_BABY:장신 = TALENT:L_MOTHER:장신
	TALENT:L_BABY:덩치 = TALENT:L_MOTHER:덩치
	TALENT:L_BABY:거구 = TALENT:L_MOTHER:거구	
	CFlag:L_BABY:주민_체격 = CFlag:L_MOTHER:주민_체격
ENDIF
;-------------------------------------------------
;継承素質の選択
;-------------------------------------------------
@LABORATORY_BREEDING_SELECT_DESIGN(L_BABY, L_DESIGN_FLAG)
#DIM        L_BABY
#DIM  REF   L_DESIGN_FLAG
#DIM        L_INPUT
#DIMS CONST L_LABEL = "머리색 유전", "눈동자색 유전", "가슴 유전", "엉덩이 유전", "체격 유전"
#DIM  CONST L_BIT_머리색 = 0
#DIM  CONST L_BIT_눈동자색 = 1
#DIM  CONST L_BIT_胸   = 2
#DIM  CONST L_BIT_尻   = 3
#DIM  CONST L_BIT_체격 = 4
#DIM        L_FLAG_BIT
DRAWLINE
FOR L_FLAG_BIT, 0, 5
	PRINTFORML [{L_FLAG_BIT}] %L_LABEL:L_FLAG_BIT, 10% %\@GETBIT(L_DESIGN_FLAG, L_FLAG_BIT) ? ○ # ×\@%
NEXT
PRINTFORML 
PRINTFORML 
PRINTFORML [999] 돌아간다
INPUT
L_INPUT = RESULT
SIF L_INPUT == 999
	RETURN
SIF INRANGE(L_INPUT, 0, 4)
	INVERTBIT L_DESIGN_FLAG, L_INPUT
RESTART



;-------------------------------------------------
;차세대창조
;주로 CFLAG 계열 상태 계승
;-------------------------------------------------
@LABORATORY_BREEDING_BABY_HEREDITY_CFLAG(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_FIRST
#DIM L_CFLAG_ID
#DIM L_WOMB_GRADE
#DIM L_GENE_GRADE

L_WOMB_GRADE = 0
L_GENE_GRADE = 0

;유전인자 계승
FOR L_CFLAG_ID, 1800, 1850
	SIF CFLAG:L_MOTHER:L_CFLAG_ID != 1
		CONTINUE
	CFLAG:L_BABY:L_CFLAG_ID = 1
	L_GENE_GRADE ++
NEXT
IF L_GENE_GRADE >= 19
	TALENT:L_BABY:3008 = 1
ELSE
	TALENT:L_BABY:(3000 + (L_GENE_GRADE / 2)) = 1
ENDIF

;d3+x/2+d(x/2) = > x + (2d4) - 5
CALL LABORATORY_BREEDING_BABY_HEREDITY_PHEROMONE(L_MOTHER, L_BABY)
;CFlag:모체레벨 = 1+(CFlag:L_I:모체레벨/2)+RAND:(CFlag:L_I:모체레벨/2+1)+RAND:3
;CFlag:난자레벨 = 1+(CFlag:L_I:난자레벨/2)+RAND:(CFlag:L_I:난자레벨/2+1)+RAND:3
; 역주) 모체와 난자레벨 모두 다른 함수로 이미 작동중

IF CSTR:L_MOTHER:시조 == ""
	CSTR:L_BABY:시조 = %CALLNAME:L_MOTHER%
	CFLAG:L_BABY:주민_시조 = CFLAG:L_MOTHER:개별번호
	CFLAG:L_MOTHER:차세대창조자손수 ++
ELSE
	CSTR:L_BABY:시조 = %CSTR:L_MOTHER:시조%
	CFLAG:L_BABY:주민_시조 = CFLAG:L_MOTHER:주민_시조
	;次善の策、記録されてないならわかるところから記録しはじめましょう
	SIF !CFLAG:L_BABY:주민_시조
		CFLAG:L_BABY:주민_시조 = CFLAG:L_MOTHER:개별번호
	L_FIRST = FINDCHARA(CFLAG:0:개별번호, CFLAG:L_BABY:주민_시조)
	SIF L_FIRST >= 1
		CFLAG:L_FIRST:차세대창조자손수 ++
ENDIF
CSTR:L_BABY:모친 = %CALLNAME:L_MOTHER%
CFLAG:L_BABY:주민_모친 = CFLAG:L_MOTHER:개별번호

CFLAG:L_BABY:주민_세대 = MAX(CFLAG:L_MOTHER:주민_세대 + 1, 2)
SIF CFLAG:L_BABY:주민_세대 >= 10
	TALENT:L_BABY:명가 = 1

SIF TALENT:L_BABY:명가 && !TALENT:L_MOTHER:명가
	CALL LABORATORY_BREEDING_NOBLE_FAMILY(L_MOTHER, L_BABY)


L_WOMB_GRADE = L_GENE_GRADE + CFLAG:L_MOTHER:모체레벨 + CFLAG:L_MOTHER:난자레벨 + CFLAG:L_BABY:주민_세대

SIF TALENT:L_MOTHER:성녀의자궁 == 1
	L_WOMB_GRADE += 50
SIF TALENT:L_MOTHER:《대성여우공신》 == 1
	L_WOMB_GRADE += EXP:L_MOTHER:출산경험*5


CFlag:L_BABY:주민_최대체력 = CFlag:L_MOTHER:주민_최대체력 + SQRT(MAX(1, CFlag:L_MOTHER:주민_최대체력 + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_최대기력 = CFlag:L_MOTHER:주민_최대기력 + SQRT(MAX(1, CFlag:L_MOTHER:주민_최대기력 + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_최대BRE  = CFlag:L_MOTHER:주민_최대BRE  + SQRT(MAX(1, CFlag:L_MOTHER:주민_최대BRE  + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_현재체력 = CFlag:L_BABY:주민_최대체력
CFlag:L_BABY:주민_현재기력 = CFlag:L_BABY:주민_최대기력
CFlag:L_BABY:주민_현재BRE  = CFlag:L_BABY:주민_최대BRE

CFlag:L_BABY:주민_ATK = CFlag:L_MOTHER:주민_ATK + SQRT(MAX(1, CFlag:L_MOTHER:주민_ATK + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_DEF = CFlag:L_MOTHER:주민_DEF + SQRT(MAX(1, CFlag:L_MOTHER:주민_DEF + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_HIT = CFlag:L_MOTHER:주민_HIT + SQRT(MAX(1, CFlag:L_MOTHER:주민_HIT + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:주민_SPD = CFlag:L_MOTHER:주민_SPD + SQRT(MAX(1, CFlag:L_MOTHER:주민_SPD + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)

CFLAG:L_BABY:주민_기능포인트 += L_GENE_GRADE * 5

CSTR:L_BABY:캐릭터해설03 = 【기밀 레벨 2:채취】창조 이후로부터 기록 전까지 성경험은 없다. 채취 역시 되어있지 않다.
CSTR:L_BABY:캐릭터해설04 = 【기밀 레벨 3:권리(재산)】당신의 신청에 의해, %CSTR:L_BABY:모친%의 유전자를 계승하여 창조・조정된 주민.

CSTR:L_BABY:캐릭터해설15 = 【중요 기밀:일족】시조「%CSTR:L_BABY:시조%」%조사만처리(CSTR:L_BABY:시조,"를")% 시작으로서, 어미「%CSTR:L_BABY:모친%」%조사만처리(CSTR:L_BABY:모친,"로")%부터 태어난 {CFLAG:L_BABY:주민_세대}세대 주민.

MONEY -= 200000

PRINTFORMW 要求された条件をもとに、素材を用いて차세대창조を行いました。
FLAG:継承종족 = 0
CFLAG:L_MOTHER:주민_継承 ++



;-------------------------------------------------
;차세대소질유전
;어머니의 소질을 참조하여 확률로 유전
;보다 어머니를 닮도록
;-------------------------------------------------
@LABORATORY_BREEDING_BABY_HEREDITY_TALENT(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
;플래그 그 １






;상반된 소질이 존재하는 경우
;모체의 소질 판정, 모자로 상반된 소질을 부정하거나 가지지 못한 소질을 발현
;판정
;IF TALENT:L_I:OOOO == 1 || TALENT:L_I:XXXX == 1
;	
;	;상반된 소질을 지운다
;	IF TALENT:L_I2:OOOO == 1 && TALENT:L_I:XXXX == 1 
;		SIF !RAND:2
;			TALENT:L_I2:OOOO = 0
;	ELSEIF TALENT:L_I2:XXXX == 1 && TALENT:L_I:OOOO == 1
;		SIF !RAND:2
;			TALENT:L_I2:XXXX = 0
;	
;	;소질을 발현
;	ELSEIF TALENT:L_I:OOOO == 1
;		SIF !RAND:2
;			TALENT:L_I2:OOOO = 1
;	ELSEIF TALENT:L_I:XXXX == 1
;		SIF !RAND:2
;			TALENT:L_I2:XXXX = 1
;	ENDIF
;ENDIF


;플래그 그 ２

;단독 소질의 경우
;판정
;IF TALENT:L_I:OOOO == 1
;어머니의 소질을 발현
;	SIF !RAND:2
;		TALENT:L_I2:OOOO = 1
;ELSE
;없는경우 아이의 소질을 지운다
;	SIF !RAND:2
;		TALENT:L_I2:OOOO = 0
;ENDIF

SELECTCASE RAND:5 ;역주) 직업 랜덤결정
CASE 0
	Talent:L_BABY:학생 = 1
CASE 1
	Talent:L_BABY:아이돌 = 1
CASE 2
	Talent:L_BABY:사회인 = 1
CASE 3
	Talent:L_BABY:창녀 = 1
CASE 4
	Talent:L_BABY:치안유지국원 = 1
ENDSELECT

IF Talent:L_BABY:학생 ;역주) 학생일 경우 체격과 복장을 고정
	CALL CHARACTER_SCOUT_STUDENT(L_BABY)
	IF Talent:L_BABY:학생 >= 5
		IF RAND:10 >= 8
			CFlag:L_BABY:주민_체격 = 1
			CFlag:L_BABY:복장 = (RAND:5)
			CFlag:L_BABY:신발 = RAND:3
		ENDIF
	ENDIF
ENDIF


;감각 [000-009]
;C~B감각
CALL HEREDITY_CONFLICT_TALENT(0, 8, L_MOTHER, L_BABY)

;스테이터스 [010-029]
CALL HEREDITY_NORMAL_TALENT(10, 30, L_MOTHER, L_BABY)

;○○っ気 [030-034]
;何もなければスルー
IF ALLSAMES(0, TALENT:L_MOTHER:사디, TALENT:L_MOTHER:마조)
;1/2でスルー
ELSEIF !RAND:2
	IF ALLSAMES(1, TALENT:L_MOTHER:사디, TALENT:L_BABY:마조) || ALLSAMES(1, TALENT:L_MOTHER:마조, TALENT:L_BABY:사디)
		TALENT:L_BABY:사디 = 0
		TALENT:L_BABY:마조 = 0
	ELSE
		TALENT:L_BABY:사디 = TALENT:L_MOTHER:사디
		TALENT:L_BABY:마조 = TALENT:L_MOTHER:마조
	ENDIF
ENDIF

;中毒 [035-039]
CALL HEREDITY_NORMAL_TALENT(35, 40, L_MOTHER, L_BABY)
;技能 [040-049]
CALL HEREDITY_NORMAL_TALENT(40, 45, L_MOTHER, L_BABY)

;体質 [060-084]	 신장の書き換えが要るため体格の素質は保留
CALL HEREDITY_CONFLICT_TALENT(70, 78, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:고양이혀 = TALENT:L_MOTHER:고양이혀

;性格 [100-129]
;겁쟁이～낙관적
CALL HEREDITY_CONFLICT_TALENT(100, 114, L_MOTHER, L_BABY)

CALL HEREDITY_NORMAL_TALENT(114, 120, L_MOTHER, L_BABY)

;性格(社交面) [130-149]
;정직～目立ち
CALL HEREDITY_CONFLICT_TALENT(130, 136, L_MOTHER, L_BABY)

CALL HEREDITY_NORMAL_TALENT(136, 139, L_MOTHER, L_BABY)

;性格(性関連) [150-179]
SIF !RAND:2
	TALENT:L_BABY:자제심 = TALENT:L_MOTHER:자제심
;습득～정조관념
CALL HEREDITY_CONFLICT_TALENT(151, 157, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:선을넘지않음 = TALENT:L_MOTHER:선을넘지않음

;쾌감부정～開放
CALL HEREDITY_CONFLICT_TALENT(158, 162, L_MOTHER, L_BABY)
CALL HEREDITY_NORMAL_TALENT(165, 167, L_MOTHER, L_BABY)


;血統はかならず継承、기억처리완료は不自然なので消去
TALENT:L_BABY:명가 = TALENT:L_MOTHER:명가
TALENT:L_BABY:푸른혈족 = TALENT:L_MOTHER:푸른혈족
TALENT:L_BABY:기억처리완료 = 0

IF TALENT:L_MOTHER:즉각타락 == 1
	IF TALENT:L_BABY:금사빠
		IF !RAND:4
			TALENT:L_BABY:금사빠 = 0
			TALENT:L_BABY:즉각타락 = 1
		ENDIF
	ELSEIF TALENT:L_BABY:경계심
		SIF !RAND:3
			TALENT:L_BABY:경계심 = 0
	ELSEIF TALENT:L_BABY:인간불신
		IF !RAND:2
			TALENT:L_BABY:인간불신 = 0
			TALENT:L_BABY:경계심 = 1
		ENDIF
	ELSE
		SIF !RAND:4
			TALENT:L_BABY:즉각타락 = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:금사빠
	IF TALENT:L_BABY:즉각타락
		IF !RAND:4
			TALENT:L_BABY:즉각타락 = 0
			TALENT:L_BABY:금사빠 = 1
		ENDIF
	ELSEIF TALENT:L_BABY:경계심
		SIF !RAND:3
			TALENT:L_BABY:경계심 = 0
	ELSEIF TALENT:L_BABY:인간불신
		IF !RAND:2
			TALENT:L_BABY:인간불신 = 0
			TALENT:L_BABY:경계심 = 1
		ENDIF
	ELSE
		SIF !RAND:3
			TALENT:L_BABY:금사빠 = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:경계심
	IF TALENT:L_BABY:즉각타락
		IF !RAND:2
			TALENT:L_BABY:즉각타락 = 0
			TALENT:L_BABY:금사빠 = 1
		ENDIF
	ELSEIF TALENT:L_BABY:금사빠
		SIF !RAND:3
			TALENT:L_BABY:금사빠 = 0
	ELSEIF TALENT:L_BABY:인간불신
		IF !RAND:4
			TALENT:L_BABY:인간불신 = 0
			TALENT:L_BABY:경계심 = 1
		ENDIF
	ELSE
		SIF !RAND:3
			TALENT:L_BABY:경계심 = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:인간불신
	IF TALENT:L_BABY:즉각타락
		IF !RAND:2
			TALENT:L_BABY:즉각타락 = 0
			TALENT:L_BABY:금사빠 = 1
		ENDIF
	ELSEIF TALENT:L_BABY:금사빠
		SIF !RAND:3
			TALENT:L_BABY:금사빠 = 0
	ELSEIF TALENT:L_BABY:경계심
		IF !RAND:4
			TALENT:L_BABY:경계심 = 0
			TALENT:L_BABY:인간불신 = 1
		ENDIF
	ELSE
		SIF !RAND:4
			TALENT:L_BABY:인간불신 = 1
	ENDIF
ENDIF
;その他 [180-199]
;습득～천재
CALL HEREDITY_CONFLICT_TALENT(180, 184, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:의존 = TALENT:L_MOTHER:의존

RETURN



;통상 소질 범위 지정 유전 처리 함수
@HEREDITY_NORMAL_TALENT(L_TALENT_START, L_TALENT_END, L_MOTHER, L_BABY)
#DIM L_TALENT_ID    ;상속대상의 TALENT 번호
#DIM L_TALENT_START ;상속대상의 TALENT 범위
#DIM L_TALENT_END   ;상속대상의 TALENT 범위、이 번호 자체는 포함되지 않음
#DIM L_MOTHER       ;모친
#DIM L_BABY         ;아이
FOR L_TALENT_ID, L_TALENT_START, L_TALENT_END
	;1/2 확률로 통과 역주) 유전되거나 유전되지 않는다?
	SIF !RAND:2
		CONTINUE
	TALENT:L_BABY:L_TALENT_ID = TALENT:L_MOTHER:L_TALENT_ID
NEXT



;상반되는 소질 범위 지정 유전처리 함수
;읽어보시면 알겠지만 상반된 소질끼리의 TALENT 번호가 인접해 있을 경우에만 잘 움직입니다.
@HEREDITY_CONFLICT_TALENT(L_TALENT_START, L_TALENT_END, L_MOTHER, L_BABY)
#DIM L_TALENT_ID    ;상속대상의 TALENT 번호
#DIM L_TALENT_START ;상속대상의 TALENT 범위
#DIM L_TALENT_END   ;상속대상의 TALENT 범위、이 번호 자체는 포함되지 않음
#DIM L_MOTHER       ;모친
#DIM L_BABY         ;아이
FOR L_TALENT_ID, L_TALENT_START, L_TALENT_END, 2
	;아무것도 없으면 통과
	SIF ALLSAMES(0, TALENT:L_MOTHER:L_TALENT_ID, TALENT:L_MOTHER:(L_TALENT_ID + 1))
		CONTINUE
	;1/2확룰로 통과
	SIF !RAND:2
		CONTINUE
	;상쇄
	;母：둔감＋娘：민감、또는 母：민감＋娘：둔감 이라면 역주) 두 소질 모두 미획득처리
	IF ALLSAMES(1, TALENT:L_MOTHER:(L_TALENT_ID + 1), TALENT:L_BABY:L_TALENT_ID) || ALLSAMES(1, TALENT:L_MOTHER:L_TALENT_ID, TALENT:L_BABY:(L_TALENT_ID + 1))
		TALENT:L_BABY:L_TALENT_ID       = 0
		TALENT:L_BABY:(L_TALENT_ID + 1) = 0
	;유전 역주) 상쇄되지 않는다면 모친의 소질과 동일하게 가져간다.
	ELSE
		TALENT:L_BABY:L_TALENT_ID       = TALENT:L_MOTHER:L_TALENT_ID
		TALENT:L_BABY:(L_TALENT_ID + 1) = TALENT:L_MOTHER:(L_TALENT_ID + 1)
	ENDIF
NEXT


;모체 계승 성장용 다이스
;성장기대치2、-1～5
@HEREDITY_PHEROMONE_DICE(BASE_VALUE)
#FUNCTION
#DIM BASE_VALUE
RETURNF BASE_VALUE + RAND:3 + RAND:3 + RAND:3 - 1



;페로몬용의 계승처리
@LABORATORY_BREEDING_BABY_HEREDITY_PHEROMONE(L_MOTHER, L_BABY)
#DIM  L_MOTHER
#DIM  L_BABY
#DIM  L_DICE
#DIMS L_RACE
#DIM  L_DICE_NUM

L_RACE = INFO_STR_SYUZOKU(CFLAG:L_BABY:주민_종족)
CFLAG:L_BABY:무자각한암컷 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:무자각한암컷)
L_DICE_NUM = 1
;소질에 의한 보너스 성장
SIF TALENT:L_BABY:매혹
	L_DICE_NUM ++
SIF TALENT:L_BABY:인기
	L_DICE_NUM ++
SIF TALENT:L_BABY:카리스마
	L_DICE_NUM ++
SIF TALENT:L_BABY:색마
	L_DICE_NUM ++
SIF TALENT:L_BABY:소악마
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:쾌감솔직
	L_DICE_NUM ++
SIF TALENT:L_BABY:해방
	L_DICE_NUM ++
SIF TALENT:L_BABY:남미새
	L_DICE_NUM ++
SIF TALENT:L_BABY:금사빠
	L_DICE_NUM ++
SIF TALENT:L_BABY:즉각타락
	L_DICE_NUM ++
SIF TALENT:L_BABY:의존
	L_DICE_NUM ++

FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:무자각한암컷 += RAND:3
NEXT

;다른 수치에 대한 보너스 씨앗
L_DICE = LIMIT(CFLAG:L_BABY:무자각한암컷/ 5, 2, 10)
SIF TALENT:L_BABY:극상의암컷
	L_DICE += 3



CFLAG:L_BABY:페로몬 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:페로몬)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:마돈나
	L_DICE_NUM += 3

SIF TALENT:L_BABY:매혹
	L_DICE_NUM ++
SIF TALENT:L_BABY:매력
	L_DICE_NUM ++
SIF TALENT:L_BABY:미모
	L_DICE_NUM ++
SIF TALENT:L_BABY:인기
	L_DICE_NUM ++
SIF TALENT:L_BABY:소악마
	L_DICE_NUM ++
SIF TALENT:L_BABY:카리스마
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:페로몬 += RAND:3
NEXT



CFLAG:L_BABY:암컷의본능 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:암컷의본능)
L_DICE_NUM = L_DICE
SIF GROUPMATCH(L_RACE, "아인", "오니", "조인", "야마")
	L_DICE_NUM += 3
SIF TALENT:L_BABY:색마
	L_DICE_NUM ++
SIF TALENT:L_BABY:중독되기쉬움
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:쾌감솔직
	L_DICE_NUM ++
SIF TALENT:L_BABY:해방
	L_DICE_NUM ++
SIF TALENT:L_BABY:의존
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:암컷의본능 += RAND:3
NEXT



CFLAG:L_BABY:성적인흥미 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:성적인흥미)
L_DICE_NUM = L_DICE
;호기심이 왕성한 듯한 종족
SIF GROUPMATCH(L_RACE, "인간", "이스")
	L_DICE_NUM += 4

SIF TALENT:L_BABY:중독되기쉬움
	L_DICE_NUM ++
SIF TALENT:L_BABY:호기심
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:자위하기쉬움
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:성적인흥미 += RAND:3
NEXT



CFLAG:L_BABY:수컷에관심 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:수컷에관심)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:즉각타락
	L_DICE_NUM += 5
SIF TALENT:L_BABY:매혹
	L_DICE_NUM ++
SIF TALENT:L_BABY:중독되기쉬움
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:쾌감솔직
	L_DICE_NUM ++
SIF TALENT:L_BABY:해방
	L_DICE_NUM ++
SIF TALENT:L_BABY:남미새
	L_DICE_NUM ++
SIF TALENT:L_BABY:금사빠
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:수컷에관심 += RAND:3
NEXT



CFLAG:L_BABY:성교원망 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:성교원망)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:강간원망
	L_DICE_NUM += 5
SIF TALENT:L_BABY:색마
	L_DICE_NUM ++
SIF TALENT:L_BABY:중독되기쉬움
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:쾌감솔직
	L_DICE_NUM ++
SIF TALENT:L_BABY:해방
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:성교원망 += RAND:3
NEXT



CFLAG:L_BABY:임신원망 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:임신원망)
L_DICE_NUM = L_DICE
;번식력이 약한 무리들은 절실하다
SIF GROUPMATCH(L_RACE, "천사", "엘프", "정령", "오토마타", "해인")
	L_DICE_NUM += 5
SIF TALENT:L_BABY:성녀의자궁
	L_DICE += 3

SIF TALENT:L_BABY:색마
	L_DICE_NUM ++
SIF TALENT:L_BABY:중독되기쉬움
	L_DICE_NUM ++
SIF TALENT:L_BABY:정조관념없음
	L_DICE_NUM ++
SIF TALENT:L_BABY:얀데레
	L_DICE_NUM ++
SIF TALENT:L_BABY:해방
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:임신원망 += RAND:3
NEXT



CFLAG:L_BABY:무자각한암컷 = LIMIT(CFLAG:L_BABY:무자각한암컷, 0, 99)
CFLAG:L_BABY:페로몬 = LIMIT(CFLAG:L_BABY:페로몬, 0, 99)
CFLAG:L_BABY:암컷의본능   = LIMIT(CFLAG:L_BABY:암컷의본능,   0, 99)
CFLAG:L_BABY:성적인흥미 = LIMIT(CFLAG:L_BABY:성적인흥미, 0, 99)
CFLAG:L_BABY:수컷에관심 = LIMIT(CFLAG:L_BABY:수컷에관심, 0, 99)
CFLAG:L_BABY:성교원망   = LIMIT(CFLAG:L_BABY:성교원망,   0, 99)
CFLAG:L_BABY:임신원망   = LIMIT(CFLAG:L_BABY:임신원망,   0, 99)

CFLAG:L_BABY:모체레벨 = LIMIT(HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:모체레벨), 0, 99)
CFLAG:L_BABY:난자레벨 = LIMIT(HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:난자레벨), 0, 99)
CFLAG:L_BABY:機関評価   = CFLAG:L_BABY:모체레벨 + CFLAG:L_BABY:난자레벨



;시조를 공유하는 캐릭터를 모두 명가화한다
;그리고 가장 최근의 어머니와 시조만 푸른혈족을 부여（BLOOD_LINE을 다듬는 것은 귀찮으니까 용서해줘）
@LABORATORY_BREEDING_NOBLE_FAMILY(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_FIRST
#DIM L_C_ID
TALENT:L_BABY:푸른혈족 = 1
TALENT:L_MOTHER:푸른혈족 = 1
TALENT:L_MOTHER:명가 = 1
SIF CFLAG:L_BABY:주민_시조 <= 0
	RETURN
;시조에게 부여
L_FIRST = FINDCHARA(CFLAG:0:개별번호, CFLAG:L_BABY:주민_시조)
IF L_FIRST >= 1
	TALENT:L_FIRST:푸른혈족 = 1
	TALENT:L_FIRST:명가 = 1
ENDIF
FOR L_C_ID, 1, CHARANUM
	SIF CFLAG:L_C_ID:주민_시조 == CFLAG:L_BABY:주민_시조
		TALENT:L_C_ID:명가 = 1
NEXT



;시조의 모습을 복사한다
@LABORATORY_BREEDING_REVERSION(L_MOTHER, L_BABY)
#DIM L_BABY
#DIM L_MOTHER
#DIM L_FIRST
IF CFLAG:L_BABY:주민_시조 <= 0
	L_FIRST = 0
ELSE
	L_FIRST = FINDCHARA(CFLAG:0:개별번호, CFLAG:L_BABY:주민_시조)
ENDIF
;시조가 발견되지 않으면 억지로 어머니를 모방한다
SIF L_FIRST <= 0
	L_FIRST = L_MOTHER
FOR LOCAL, 0, 53
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL, 58, 85
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL, 100, 197
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL,1400, 1499
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
;아마 NPC번호는 필요없을거라고 생각한다
FOR LOCAL,300, 490
	CFLAG:L_BABY:LOCAL = CFLAG:L_FIRST:LOCAL
NEXT
;역주) 종족특징을 부여함 조인에게 날개부여라던가
CFLAG:L_BABY:주민_종족      = CFLAG:L_FIRST:주민_종족
CFLAG:L_BABY:주민_종족특징1 = CFLAG:L_FIRST:주민_종족특징1
CFLAG:L_BABY:주민_종족특징2 = CFLAG:L_FIRST:주민_종족특징2
CFLAG:L_BABY:주민_종족특징3 = CFLAG:L_FIRST:주민_종족특징3
CFLAG:L_BABY:주민_체격      = CFLAG:L_FIRST:주민_체격




;최종조정
SIF TALENT:L_MOTHER:명가 ;명가라면 기본적으로 합의상태로 태어난다
	TALENT:L_BABY:합의 = 1

;-------------------------------------------------
;신체강화
;-------------------------------------------------
@LABORATORY_BOOST_ST_SELECT
#DIM L_CHARA
CLEARLINE LINECOUNT
DRAWLINE
CALL PRINT_LABEL("신체강화")
PRINTL
PRINTFORML 최첨단 기술로, 주민을 강화합니다.
PRINTFORML 강화치는 일정.
DRAWLINE
PRINTFORML 강화할 주민을 선택해주세요.
CALL LIST_CHARA
L_CHARA = RESULT
SIF L_CHARA == 1000
	RETURN
CALL LABORATORY_BOOST_ST(L_CHARA)
SIF RESULT < 0
	RESTART



@LABORATORY_BOOST_ST(L_CHARA)
#DIM L_CHARA
#DIM L_COST
#DIM L_BONUS
#DIM L_INPUT
L_COST = 1000 * (CFLAG:L_CHARA:주민_신체강화횟수 + 1)
IF L_CHARA == MASTER
	PRINTFORMW 당신은 선택할 수 없습니다.
	RETURN -1
ELSEIF !INRANGE(L_CHARA, 1, CHARANUM - 1)
	PRINTFORMW 존재하지 않는 캐릭터입니다.
	RETURN -1
ELSEIF TALENT:L_CHARA:NPC
	PRINTFORMW NPC는 선택할 수 없습니다.
	RETURN -1
ELSEIF FLAG:도시_공적 < L_COST
	PRINTFORMW 공적 포인트가 부족합니다.
	RETURN
ELSEIF MONEY < 200000
	PRINTFORMW 자금이 부족합니다.
	RETURN
ENDIF

DO
	CLEARLINE LINECOUNT
	CALL INFO_STATUS_HEADER(L_CHARA)
	CALL C_PICTURE_S(L_CHARA)
	DRAWLINE
	PRINTFORML 강화에는 자금 200000, 공적 포인트 {L_COST} 만큼 필요합니다.
	PRINTFORML 강화하시겠습니까？
	CALL PRINT_LABEL("자금",,,TOSTR(MONEY),,,,,,,, 1)
	CALL PRINT_LABEL("공적포인트",,,TOSTR(FLAG:도시_공적),,,,,,,, 1)
	PRINTL
	PRINTFORML
	PRINTFORML [0] 강화한다
	PRINTFORML
	PRINTFORML [999] 돌아간다
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
		CASE 0
			BREAK
		CASE 999
			RETURN
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 1

MONEY -= 200000
FLAG:도시_공적 -= L_COST

IF GROUPMATCH(CFLAG:L_CHARA:주민_종족, 6, 8, 10)
	L_BONUS = 1
ELSE
	L_BONUS = 0
ENDIF

CFLAG:L_CHARA:주민_최대체력 += 5 + L_BONUS
CFLAG:L_CHARA:주민_최대기력 += 5 + L_BONUS
CFLAG:L_CHARA:주민_최대BRE += 10 + L_BONUS
CFLAG:L_CHARA:주민_ATK += 2 + L_BONUS
CFLAG:L_CHARA:주민_DEF += 2 + L_BONUS
CFLAG:L_CHARA:주민_HIT += 2 + L_BONUS
CFLAG:L_CHARA:주민_SPD += 2 + L_BONUS

CFLAG:L_CHARA:주민_현재체력 = CFLAG:L_CHARA:주민_최대체력
CFLAG:L_CHARA:주민_현재기력 = CFLAG:L_CHARA:주민_최대기력
CFLAG:L_CHARA:주민_현재BRE  = CFLAG:L_CHARA:주민_최대BRE

CFLAG:L_CHARA:주민_기능포인트 += 5 + L_BONUS

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_CHARA)
CALL C_PICTURE_S(L_CHARA)
DRAWLINE
PRINTFORML HP :{CFLAG:L_CHARA:주민_최대체력 - (5 + L_BONUS)}→{CFLAG:L_CHARA:주민_최대체력}
PRINTFORML SP :{CFLAG:L_CHARA:주민_최대기력 - (5 + L_BONUS)}→{CFLAG:L_CHARA:주민_최대기력}
PRINTFORML ATK:{CFLAG:L_CHARA:주민_ATK - (2 + L_BONUS)}→{CFLAG:L_CHARA:주민_ATK}
PRINTFORML DEF:{CFLAG:L_CHARA:주민_DEF - (2 + L_BONUS)}→{CFLAG:L_CHARA:주민_DEF}
PRINTFORML HIT:{CFLAG:L_CHARA:주민_HIT - (2 + L_BONUS)}→{CFLAG:L_CHARA:주민_HIT}
PRINTFORML SPD:{CFLAG:L_CHARA:주민_SPD - (2 + L_BONUS)}→{CFLAG:L_CHARA:주민_SPD}
SIF CFLAG:L_CHARA:주민_최대BRE && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML BRE:{CFLAG:L_CHARA:주민_최대BRE - (10 + L_BONUS)}→{CFLAG:L_CHARA:주민_최대BRE}
RESETCOLOR
SIF CFLAG:L_CHARA:주민_기능포인트 && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML 기능 포인트:{CFLAG:L_CHARA:주민_기능포인트-(5 + L_BONUS)}→{CFLAG:L_CHARA:주민_기능포인트}
RESETCOLOR
PRINTFORML 
PRINTFORML 
PRINTFORML 
PRINTFORMW 강화 완료했습니다.
