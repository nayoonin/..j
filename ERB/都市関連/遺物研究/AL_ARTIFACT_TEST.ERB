;-------------------------------------------------
; 遺物をテストする関数
; ステータスやプロフィール、成功／失敗時のテキストを確認する
;-------------------------------------------------
@遺物研究テスト
FLAG:유연_연구중 = 1
FLAG:유연_サポーター総数 = 3
CALL 遺物研究テスト_スカウト
JUMP 遺物研究テスト_メインループ


; サンプル자원봉사자を生成する
@遺物研究テスト_スカウト
Flag:喫茶店スカウト = 1

CLEARLINE LINECOUNT
CALL CHARACTER_SCOUT1
TARGET = CHARANUM - 1
CALL CAFE_SCOUT_OPTION2(TARGET)
CALL 遺物研究テスト_자원봉사자のプロフィールを表示する

PRINTL * 유물을 테스트할 자원봉사자를 선택해 주세요.
PRINTL
PRINT [1] 결정 　　　
PRINT [2] 다른 거주민
PRINT [3] 범위 축소　

$INPUT_LOOP
INPUT
SELECTCASE RESULT
    CASE 1
        RETURN
    CASE 2
        CALL CANCEL_SCOUT(TARGET)
    CASE 3
        CALL CANCEL_SCOUT(TARGET)
        CALL CAFE_SCOUT_OPTION
    CASEELSE
        CALL C_INPUT_ERROR("무효한 입력입니다")
        GOTO INPUT_LOOP
ENDSELECT
RESTART


; 자원봉사자のプロフィール
@遺物研究テスト_자원봉사자のプロフィールを表示する
DRAWLINE
PRINTFORML 名前:%CALLNAME%
DRAWLINE
CALL C_PICTURE_S(TARGET)
PRINTFORML ■------------------------------------ 　소질　 ------------------------------------■
CALL INFO_STATUS_TALENT(TARGET)
CALL CHARACTER_SCOUT_PROFILE(TARGET)
DRAWLINE

;-------------------------------------------------
; 遺物をリストから選択してテストする
;-------------------------------------------------
@遺物研究テスト_メインループ
#DIM CONST 全情報を開示する = 1

CLEARLINE LINECOUNT
CALL AL_ARTIFACT_LIST(全情報を開示する)

SIF FLAG:유연_연구대상 > 0
    CALL 遺物研究テスト_対象遺物をテストする

RESTART

;-------------------------------------------------
; 対象遺物をテストする
;-------------------------------------------------
@遺物研究テスト_対象遺物をテストする(遺物ID)
#DIM 遺物ID

遺物ID = FLAG:유연_연구대상

; ステータス
DRAWLINE
CALLFORM AL_Status_{遺物ID}
DRAWLINE

; LPや비용効率などの詳細情報
CALL 遺物研究テスト_解析表を表示する

; プロフィールをLV1～3まで表示するため、一時的に研究回数を100回にする
FLAG:(16000 + FLAG:유연_연구대상) = 100
CALLFORM AL_Profile_{遺物ID}
FLAG:(16000 + FLAG:유연_연구대상) = 0

; 成功／失敗のテキスト
WHILE 1
    PRINTLC [0] 돌아간다  [1] 성공  [2] 실패  [3] 판정
    INPUT
    SIF RESULT == 0
        BREAK
    SIF RESULT < 0 || RESULT > 3
        CONTINUE

    PRINTL
    PRINTFORMDL {FLAG:(16000+FLAG:유연_연구대상)+1}회째...
    SELECTCASE RESULT
        CASE 1
            CALL 遺物研究テスト_成功(遺物ID)
        CASE 2
            CALL 遺物研究テスト_失敗(遺物ID)
        CASE 3
            CALL 遺物研究テスト_ランダム(遺物ID)
    ENDSELECT
    FLAG:(16000+FLAG:유연_연구대상) ++
    PRINTL
WEND

; 研究回数をリセット
FLAG:(16000 + FLAG:유연_연구대상) = 0

;-------------------------------------------------
; 各遺物の解析表
;-------------------------------------------------
@遺物研究テスト_解析表を表示する
#DIMS 項目
#DIMS 数値
#DIMS 説明

SETCOLOR 50,250,250

項目 = 성공률
数値 = {AL_성공률()}\%
説明 = 100\%(基本) +15\%(サポーター3人) -{FLAG:유연_실패율}\%(실패율) -{FLAG:유연_위험성 * 2}\%(위험성) %遺物研究テスト_독자판정()%
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

項目 = 獲得LP
数値 = 1 ～ {AL_최대LP()}
説明 = {FLAG:유연_연구이익}(연구이익) + {(FLAG:유연_위험성 / 2)}(위험성÷2)
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

項目 = LP期待値
数値 = %AL_小数表記に変換(AL_LP期待値())%
説明 = 1回の研究で得られるLPの期待値
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

項目 = 비용効率
数値 = {AL_비용効率()}\%
説明 = 1LPにかかる비용を50000とした場合の効率
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

項目 = 육체데미지
数値 = {AL_육체데미지()}
説明 = {FLAG:유연_육체데미지}(육체데미지) + {FLAG:유연_위험성}(위험성)
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

項目 = 정신데미지
数値 = {AL_정신데미지()}
説明 = {FLAG:유연_정신데미지}(정신데미지) + {FLAG:유연_위험성}(위험성)
CALL 遺物研究テスト_解析表の項目(項目, 数値, 説明)

RESETCOLOR


@遺物研究テスト_解析表の項目(項目, 数値, 説明)
#DIMS 項目
#DIMS 数値
#DIMS 説明
PRINTFORML %項目, 20, LEFT% | %数値, 10, LEFT% | %説明%

;-------------------------------------------------
; 研究の成功／失敗テキストを表示する
;-------------------------------------------------
@遺物研究テスト_成功(遺物ID)
#DIM 遺物ID

SETCOLOR 150,250,150
DRAWLINE
PRINTFORML 【유물연구:성공】
DRAWLINE
CALLFORM AL_Clear_{遺物ID}
RESETCOLOR


@遺物研究テスト_失敗(遺物ID)
#DIM 遺物ID

SETCOLOR 250,150,150
DRAWLINE
PRINTFORML 【유물연구:실패】
DRAWLINE
CALLFORM AL_Out_{遺物ID}
RESETCOLOR


@遺物研究テスト_ランダム(遺物ID)
#DIM 遺物ID

IF RAND:100 < AL_성공률()
    CALL 遺物研究テスト_成功(遺物ID)
ELSE
    CALL 遺物研究テスト_失敗(遺物ID)
ENDIF

;-------------------------------------------------
; 독자판정の文字列を返す
; 解析表で성공률の説明に使う
;-------------------------------------------------
@遺物研究テスト_독자판정
#FUNCTIONS
#DIM 독자판정
#DIMS 表記

독자판정 = FLAG:유연_독자판정

SELECTCASE 독자판정
    CASE 0
        RETURNF
    CASE 10
        表記 = +100\%
    CASE -10
        表記 = -100\%
    CASE IS > 0
        表記 = +{독자판정 * 5}\%
    CASE IS < 0
        表記 = {독자판정 * 5}\%
ENDSELECT
RETURNF 表記 + "(독자판정)"
