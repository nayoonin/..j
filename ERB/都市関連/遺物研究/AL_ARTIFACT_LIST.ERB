;-------------------------------------------------
; 遺物のリストを表示して選択する
;
; 引数の「全情報を開示するか」はデバッグ用。1を指定すると研究回数に制限されずすべての情報を表示する
; (例)
; CALL AL_ARTIFACT_LIST(1)
;-------------------------------------------------
@AL_ARTIFACT_LIST(全情報を開示するか = 0)
#DIM 全情報を開示するか

#DIM CONST 遺物の최대数 = 100
#DIM DYNAMIC 遺物の配列, 100    ; 毎回初期化するためにDYNAMIC指定する
#DIM 遺物の数
#DIM 遺物番号

#DIM CONST ページの行数 = 20    ; 1ページの行数を変えたい時はここを変更する
#DIM ページ上限
#DIM 현재のページ
#DIM 表示開始する番号
#DIM 表示終了する番号

#DIM 元の研究対象
#DIM 元の色
#DIM L_INPUT

元の研究対象 = FLAG:유연_연구대상

CLEARLINE LINECOUNT
PRINTFORML 연구할 유물을 선택해주세요. 연구가 진행되면 정보가 개시됩니다.
元の色 = GETCOLOR()
SETCOLORBYNAME YELLOW
PRINTL ※ ＬＰ를 획득하고 싶다 → LP 기대치가 높은 유물
PRINTL ※ 비용을 절약하고 싶다 → 비용 효율이 높은 유물
SETCOLOR 元の色

; ヘッダー
DRAWLINE
PRINTFORML       %"이름", 20, LEFT% %" ", 12% | %"비용", 6% | %"성공률", 6% | %"육Ｄ", 3% | %"정Ｄ", 3% | %"획득LP", 7% | %"LP기대치", 8% | %"비용효율", 8%
DRAWLINE

; 1～99番の間で定義されている遺物を抽出する
CALL AL_遺物を配列に抽出する(遺物の配列)
遺物の数 = RESULT

; ページ数と表示範囲を計算する
ページ上限 = (遺物の数 - 1) / ページの行数
表示開始する番号 = ページの行数 * 현재のページ
表示終了する番号 = 表示開始する番号 + ページの行数

; リストを表示する
FOR LOCAL, 表示開始する番号, 表示終了する番号
    遺物番号 = 遺物の配列:LOCAL

    ; 2ページ以降のリストの空きは空行で埋める
    IF LOCAL >= 遺物の数
        SIF 현재のページ > 0
            PRINTL
        CONTINUE
    ENDIF

    ; 遺物のステータスを読み込む
    CALLFORM AL_Check_{遺物番号}
    CALLFORM AL_Set_{遺物番号}
    CALLFORM AL_Branch_{遺物番号}

    ; 현재の対象は色を変えて目立たせる
    IF 遺物番号 == 元の研究対象
        SETCOLOR 50,250,250
    ENDIF
    CALL AL_遺物の情報を１行で表示する(全情報を開示するか)
    SETCOLOR 元の色
NEXT
DRAWLINE

; リスト表示のために変更した研究対象を元に戻す
IF 元の研究対象 > 0
    CALLFORM AL_Check_{元の研究対象}
    CALLFORM AL_Set_{元の研究対象}
    CALLFORM AL_Branch_{元の研究対象}
ELSE
    CALL AL_研究対象を解除する
ENDIF

; ページ移動ボタンを表示する
IF 현재のページ > 0
    PRINTFORMLC [998] 이전 페이지
ELSE
    PRINTFORMLC  
ENDIF
PRINTFORMLC [999] 돌아간다
IF 현재のページ < ページ上限
    PRINTFORMLC [1000] 다음 페이지
ENDIF

INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
    ; 前ページ
    CASE 998
        SIF 현재のページ > 0
            현재のページ --

    ; 戻る
    CASE 999
        RETURN

    ; 次ページ
    CASE 1000
        SIF 현재のページ < ページ上限
            현재のページ ++

    ; 遺物を選択
    CASEELSE
        ; 연구중の対象を再選択したら解除する
        IF L_INPUT == 元の研究対象
            CALL AL_研究対象を解除する
            PRINTFORMW 연구대상을 해제했습니다.
            RETURN
        ENDIF

        ; 番号の遺物が存在するなら対象に設定する
        RESULT = 0
        TRYCALLFORM AL_Check_{L_INPUT}
        IF RESULT
            CALLFORM AL_Set_{L_INPUT}
            CALLFORM AL_Branch_{L_INPUT}
            PRINTFORMW 연구대상을 설정했습니다.
            RETURN
        ENDIF
ENDSELECT
RESTART

;-------------------------------------------------
; 遺物の情報を１行で表示する
; 研究が進むと開示される情報も増える
;-------------------------------------------------
@AL_遺物の情報を１行で表示する(全情報を開示するか = 0)
#DIM 全情報を開示するか
#DIM CONST 情報開示LV1 = 5      ; 성공률と데미지情報を開示する回数
#DIM CONST 情報開示LV2 = 10     ; 獲得LPと비용効率を開示する回数
#DIM 研究回数
#DIMS 番号
#DIMS 名前
#DIMS 進捗
#DIMS 비용
#DIMS 성공률
#DIMS 육체데미지
#DIMS 정신데미지
#DIMS 獲得LP
#DIMS LP期待値
#DIMS 비용効率

研究回数 = FLAG:(16000 + FLAG:유연_연구대상)
番号 = {FLAG:유연_연구대상}
名前 = %SaveStr:500%
進捗 = %BARSTR(研究回数, 10, 10)%
비용 = {FLAG:유연_비용}
성공률 = ???
육체데미지 = ???
정신데미지 = ???
獲得LP = ???
LP期待値 = ???
비용効率 = ???

IF 研究回数 >= 情報開示LV1 || 全情報を開示するか
    성공률 = {AL_성공률()}\%
    육체데미지 = {AL_육체데미지()}
    정신데미지 = {AL_정신데미지()}
ENDIF

IF 研究回数 >= 情報開示LV2 || 全情報を開示するか
    獲得LP = 1 ～ {AL_최대LP()}
    LP期待値 = %AL_小数表記に変換(AL_LP期待値())%
    비용効率 = {AL_비용効率()}\%
ENDIF

PRINTFORM [%番号, 2%] %名前, 20, LEFT% %進捗% | %비용, 6% | %성공률, 6% | %육체데미지, 3%
PRINTFORML  | %정신데미지, 3% | %獲得LP, 7% | %LP期待値, 8% | %비용効率, 8%

;-------------------------------------------------
; 研究の성공률(0～100)を返す
;-------------------------------------------------
@AL_성공률
#FUNCTION
#DIM DYNAMIC C_P
#DIM DYNAMIC U_P
#DIM 成功補整
#DIM 失敗補整
#DIM 성공률

; 強制成功
SIF FLAG:유연_독자판정 == 10
    RETURNF 100

; 強制失敗
SIF FLAG:유연_독자판정 == -10
    RETURNF 0

SIF FLAG:유연_독자판정 >= 1
    C_P = FLAG:유연_독자판정 * 5

SIF FLAG:유연_독자판정 <= -1
    U_P = FLAG:유연_독자판정 * 5

成功補整 = (FLAG:유연_サポーター総数 * 5) + C_P + U_P
失敗補整 = FLAG:유연_실패율 + (FLAG:유연_위험성 * 2)
성공률 = 100 + 成功補整 - 失敗補整

RETURNF LIMIT(성공률, 0, 100)

;-------------------------------------------------
; 各値の計算
;-------------------------------------------------
@AL_최대LP
#FUNCTION
RETURNF FLAG:유연_연구이익 + (FLAG:유연_위험성 / 2)


; 小数を扱うため、1.72なら172を返す。計算で使う時は100分の1にする
@AL_LP期待値
#FUNCTION
#DIM 平均LP
平均LP = (1 + AL_최대LP()) / 2
RETURNF 平均LP * AL_성공률()


@AL_비용効率
#FUNCTION
#DIM CONST 基本研究費 = 50000   ; 1LPあたりにかかる비용（仮）
#DIM LPあたりの비용
SIF AL_LP期待値() == 0  ; 0除算を避けるために先に判定しておく
    RETURNF 0
LPあたりの비용 = FLAG:유연_비용 * 100 / AL_LP期待値()
SIF LPあたりの비용 == 0
    RETURNF 0
RETURNF 基本研究費 * 100 / LPあたりの비용


@AL_육체데미지
#FUNCTION
RETURNF FLAG:유연_육체데미지 + FLAG:유연_위험성


@AL_정신데미지
#FUNCTION
RETURNF FLAG:유연_정신데미지 + FLAG:유연_위험성

;-------------------------------------------------
; 小数表記に変換する
; ARGに172を渡すと、"1.72"という文字列を返す
;
; (例)
; 8    -> "0.08"
; 56   -> "0.56"
; 3456 -> "34.56"
;-------------------------------------------------
@AL_小数表記に変換(ARG)
#FUNCTIONS
#DIMS 文字列
#DIM 小数点
#DIMS 整数
#DIMS 少数

文字列 '= TOSTR(ARG)

SIF STRLENS(文字列) == 1
    文字列 '= "00" + 文字列
SIF STRLENS(文字列) == 2
    文字列 '= "0" + 文字列

小数点 = STRLENS(文字列) - 2
整数 '= SUBSTRING(文字列, 0, 小数点)
少数 '= SUBSTRING(文字列, 小数点)
RETURNF 整数 + "." + 少数

;-------------------------------------------------
; 遺物1～99から、定義されている遺物の番号を配列に抽出する
;-------------------------------------------------
@AL_遺物を配列に抽出する(配列)
#DIM CONST 遺物の최대数 = 100
#DIM REF 配列
#DIM 遺物の数
#DIMS 元の名前

元の名前 = %SaveStr:500%

遺物の数 = 0
FOR LOCAL, 1, 遺物の최대数
    RESULT = 0
    TRYCALLFORM AL_Check_{LOCAL}
    IF RESULT
        配列:遺物の数 = LOCAL
        遺物の数 ++
    ENDIF
NEXT

; AL_Checkを呼ぶと研究対象の名前が変更されるので元に戻しておく
SaveStr:500 = %元の名前%

RETURN 遺物の数

;-------------------------------------------------
; 研究対象を解除する
;-------------------------------------------------
@AL_研究対象を解除する
FLAG:유연_연구대상 = 0
SaveStr:500 = 
FLAG:유연_위험성 = 0
FLAG:유연_연구이익 = 0
FLAG:유연_육체데미지 = 0
FLAG:유연_정신데미지 = 0
FLAG:유연_실패율 = 0
FLAG:유연_비용 = 0


