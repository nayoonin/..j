
;-------------------------------------------------
; 喫茶店　会話
;-------------------------------------------------

@CAFE_TALK(L_I, ARG:1)
#DIM L_I
#DIM L_INPUT
#DIM L_color

CLEARLINE LINECOUNT

CALL NAYAMI_PAGE(L_I)
CALL C_PICTURE_T(L_I)
CALL INFO_CITY_BASE
DRAWLINE

REDRAW 0
CALL PRINT_LABEL("―――――――― 이 장소에서의 행동 ――――――――")
PRINTL
L_color = GETCOLOR()
SIF Flag:도시_현재TP < 1 && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORM [1] 대화한다(TP-1)　　
PRINTFORM [2] 고민해결(TP-1)　　
SETCOLOR L_color
IF CFlag:L_I:관계단계 >= 2 && TALENT:L_I:합의 == 0
	SIF Flag:도시_현재TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [5] 구애한다(TP-5)　　
	SETCOLOR L_color
ENDIF
PRINTFORML
IF CFlag:L_I:관계단계 >= 2
	CALL PRINT_LABEL("―――――― 사적인 행동 ――――――")
	PRINTL
	PRINTFORM [10] 경향없음　
	SIF Flag:도시_현재TP < 3 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [11] 충성경향(TP-3)　
	PRINTFORM [12] 애정경향(TP-3)　
	PRINTFORM [13] 음란경향(TP-3)　
	SETCOLOR L_color
	PRINTFORML 
	CALL PRINT_LABEL("―――――――――― 관계 구축 ――――――――――")
	PRINTL
	SIF Flag:도시_현재TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [21] 복종√(TP-5)　
	PRINTFORM [22] 연애√(TP-5)　
	PRINTFORM [23] 음란√(TP-5)　
	SETCOLOR L_color
ENDIF
PRINTFORML
PRINTFORML 
SIF !FLAG:도시_현재BP && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML [50] BP섭취
SETCOLOR L_color
PRINTFORML 
PRINTFORML [999] 그만둔다
DO
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		IF Flag:도시_현재TP <= 0
			$LESS_TP
			CALL C_INPUT_ERROR("TP가 부족합니다.")
			CONTINUE
		ELSE
			Flag:도시_현재TP -= 1
			CALL CAFE_TALK2(L_I)
		ENDIF
	CASE 2
		SIF Flag:도시_현재TP <= 0
			GOTO LESS_TP
		IF CFlag:L_I:주민_悩み解決判定回数 != 0 || CFlag:L_I:주민_悩み解決中 != 0
			CALL C_INPUT_ERROR("현재 고민 해결 중입니다. 결과가 나올 때까지 재선택 할 수 없습니다.")
			CONTINUE
		ELSE
			CALL NAYAMI_CHALLENGE(L_I)
		ENDIF
	CASE 5
		SIF Flag:도시_현재TP < 5
			GOTO LESS_TP
		Flag:도시_현재TP -= 5
		CALL CAFE_TALK_KDK(L_I)
	CASE 10
		CFlag:L_I:値傾向 = 0
	CASE 11
		SIF Flag:도시_현재TP < 3
			GOTO LESS_TP
		Flag:도시_현재TP -= 3
		CFlag:L_I:値傾向 = 1
	CASE 12
		SIF Flag:도시_현재TP < 3
			GOTO LESS_TP
		Flag:도시_현재TP -= 3
		CFlag:L_I:値傾向 = 2
	CASE 13
		SIF Flag:도시_현재TP < 3
			GOTO LESS_TP
		Flag:도시_현재TP -= 3
		CFlag:L_I:値傾向 = 3
	CASE 21
		SIF Flag:도시_현재TP < 5
			GOTO LESS_TP
		Flag:도시_현재TP -= 5
		CALL CAFE_TALK_R_J(L_I)
	CASE 22
		SIF Flag:도시_현재TP < 5
			GOTO LESS_TP
		Flag:도시_현재TP -= 5
		CALL CAFE_TALK_R_R(L_I)
	CASE 23
		SIF Flag:도시_현재TP < 5
			GOTO LESS_TP
		Flag:도시_현재TP -= 5
		CALL CAFE_TALK_R_I(L_I)
	CASE 50
		CALL SYSTEM_BP
	CASE 999
		RETURN
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("무효한 입력입니다")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART












;-------------------------------------------------
; 会話する
;-------------------------------------------------
@CAFE_TALK2(L_I)
#DIM L_I     ;対象登録番号
#DIM L_K_U   ;호감도上昇値
#DIM L_K_U2  ;호감도上昇値
#DIM L_N_K   ;悩み解明値
#DIM L_N_K2  ;悩み解明値
#DIM I_LOOP

L_K_U2 = (CFLAG:L_I:호감도 / 100) + 1
SIF L_K_U2 >= 100
	L_K_U2 = 100

L_K_U = (RAND:1) + 1
L_K_U +=(L_K_U2 / 2)
L_K_U += RAND:((L_K_U2 / 2) + 2)
IF TALENT:L_I:즉각타락 == 1
	L_K_U = L_K_U * 5
ELSEIF TALENT:L_I:금사빠 == 1
	L_K_U = L_K_U * 2
ELSEIF TALENT:L_I:경계심 == 1
	L_K_U -= L_K_U / 5
ELSEIF TALENT:L_I:인간불신 == 1
	L_K_U -= L_K_U / 2
ENDIF

SIF TALENT:L_I:고원의꽃 == 1
	L_K_U -= L_K_U /3






CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML %마스터는% %조사처리(CALLNAME:L_I,"과")% 가볍게 이야기했다…….


PRINTFORM 「――
IF CFlag:L_I:관계단계 >= 2 && RAND:100 > 50
	PRINTDATA
		DATAFORM そう、以前の話はなんとかな%CSTR:L_I:어미ました%
		DATAFORM %CSTR:L_I:어미微笑%、%CSTR:L_I:二人称%は頼もしい%CSTR:L_I:어미です2%
		DATAFORM %CSTR:L_I:一人称%も出来れば力に
		DATAFORM わか%CSTR:L_I:어미ました%、いつも通りに
		DATAFORM そう%CSTR:L_I:어미肯定%、これでや%CSTR:L_I:어미促進%
	ENDDATA
ELSEIF CFlag:L_I:관계단계 >= 1 && RAND:100 > 50
	PRINTDATA
		DATAFORM %CSTR:L_I:어미微笑%、そう%CSTR:L_I:어미肯定%
		DATAFORM あれはそうすれば良い%CSTR:L_I:어미行為%
		DATAFORM そう、あの時は%CSTR:L_I:어미丁寧%世話にな%CSTR:L_I:어미ました%
		DATAFORM そう%CSTR:L_I:어미逆説%、あのままで
		DATAFORM %CSTR:L_I:어미反応%、そういえばこの前の
	ENDDATA
ELSE
	PRINTDATA
		DATAFORM どう%CSTR:L_I:어미疑問%？
		DATAFORM 実はこんなことが
		DATAFORM そう%CSTR:L_I:어미同意求む%
		DATAFORM %CSTR:L_I:어미逆説%、これは
		DATAFORM それはいい%CSTR:L_I:어미褒める%っ
	ENDDATA
ENDIF
IF RAND:100 > 50
	PRINTDATA
		DATAFORM ―――それと
		DATAFORM ―――%CSTR:L_I:어미逆説%
		DATAFORM ―――そうなる%CSTR:L_I:어미ので%
		DATAFORM ―――これも
		DATAFORM ―――それに%CSTR:L_I:一人称%も
	ENDDATA
ENDIF
IF RAND:100 > 50
	PRINTDATA
		DATAFORM ――%CSTR:L_I:어미YES%、お願い%CSTR:L_I:어미お願い%
		DATAFORM ――%CSTR:L_I:어미No%、大丈夫%CSTR:L_I:어미です%
		DATAFORM ――%CSTR:L_I:어미謝罪%、助か%CSTR:L_I:어미ました%
		DATAFORM ――大丈夫そう%CSTR:L_I:어미肯定%
		DATAFORM ――%CSTR:L_I:어미相槌%、後は――
	ENDDATA
ENDIF

PRINTFORML 」

DRAWLINE
PRINTFORML %조사처리(CALLNAME:L_I,"과")%의 관계가 약간 깊어진 느낌이 든다…….
PRINTFORML 호감도 {CFLAG:L_I:호감도}　→　{(CFLAG:L_I:호감도)+L_K_U}
PRINTFORML 
PRINTFORML 
PRINTFORMW 



L_N_K2 = (CFLAG:L_I:호감도 / 200) + 1
SIF L_N_K2 >= 20
	L_N_K2 = 20

L_N_K = (RAND:5) + 1
L_N_K += L_N_K2

I_LOOP = 1
FOR I_LOOP, 1, 5
	SIF CFLAG:L_I:(4000 + I_LOOP) <= 0
		CONTINUE
	CFLAG:L_I:(4100 + I_LOOP) += (RAND:L_N_K) + 1
	SIF CFLAG:L_I:(4100 + I_LOOP) >= 100
		CFLAG:L_I:(4100 + I_LOOP)  = 100
NEXT
FOR I_LOOP, 11, 15
	SIF CFLAG:L_I:(4000 + I_LOOP) <= 0
		CONTINUE
	CFLAG:L_I:(4100 + I_LOOP) += (RAND:L_N_K) + 2
	SIF CFLAG:L_I:(4100 + I_LOOP) >= 100
		CFLAG:L_I:(4100 + I_LOOP)  = 100
NEXT
CFLAG:L_I:호감도 += L_K_U


;-------------------------------------------------
; 悩み解決
;-------------------------------------------------
@NAYAMI_CHALLENGE(L_I)
#DIM L_I
#DIM I_LOOP
#DIM L_INPUT
CLEARLINE LINECOUNT
CALL INFO_CITY_BASE
PRINTFORML 
CALL INFO_STATUS_NAYAMI_LIST(L_I, "「ボタン」")
PRINTFORML
PRINTFORML [999] 그만둔다
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 999
		RETURN
	CASE L_INPUT
		IF !INRANGE(L_INPUT, 1, 9) && !INRANGE(L_INPUT, 11, 19)
		;そんな悩みはない
			CALL C_INPUT_ERROR("무효한 입력입니다.")
			CONTINUE
		ENDIF
		IF CFLAG:L_I:(4000 + L_INPUT) <= 0
		;これまたそんな悩みはない
			CALL C_INPUT_ERROR("그 고민은 존재하지 않습니다.")
			CONTINUE
		ENDIF
		IF CFLAG:L_I:(4100 + L_INPUT) < 100 
		;内容がわからない
			CALL C_INPUT_ERROR("아직 고민이 판명되지 않았습니다.")
			CONTINUE
		ENDIF
		IF L_INPUT <= 10 && CFLAG:L_I:관계단계 == 0
			CALL C_INPUT_ERROR("해결할 수 있을 만큼의 신뢰를 얻지 못하고 있습니다.")
			CONTINUE
		ENDIF
		CFLAG:L_I:주민_悩み解決選択 = L_INPUT
		CALL NAYAMI_CHALLENGE2(L_I, L_INPUT)
		SIF RESULT
			RETURN RESULT
		CFLAG:L_I:주민_悩み解決選択 = 0
		RESTART
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("무효한 입력입니다")
		CONTINUE
	ENDSELECT
LOOP 1



;-------------------------------------------------
; 悩み解決
;-------------------------------------------------
;戻り値：いずれかの解決法の提示に成功したら1
@NAYAMI_CHALLENGE2(C_ID, N_ID)
#DIM  C_ID           ;登録番号
#DIM  N_ID           ;悩みのID
#DIMS PLAN_NAME,  11 ;解決方法の名前
#DIM  SUC_PLUS,   11 ;解決方法による固定補正
#DIM  HAVE_PLAN      ;初回限定で更新する用フラグ
#DIM  COST_MONEY, 11 ;このキャラでかかる各プランの必要資金
#DIM  COST_AP,    11 ;このキャラでかかる各プランの必要AP
#DIM  L_S            ;성공률
#DIM  L_K            ;호감도補正値
#DIM  L_INPUT        ;入力の一時保持用

IF !HAVE_PLAN
	CALLF DIM_TABLE_NAYAMI_PLAN(PLAN_NAME, SUC_PLUS)
	HAVE_PLAN = 1
ENDIF
PRINTFORML 
PRINTFORML 해결방법을 선택해 주세요.
PRINTFORML ※한 번 선택할 시 후일 결과가 나오기 전까지 재선택 할 수 없습니다.
;PRINTFORML {L_P}{ARG:0}
CALL NAYAMI_PLAN_BUTTON_LIST(C_ID, N_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
PRINTFORML 
PRINTFORML
PRINTFORML [0] - 그만둔다
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0
		RETURN
	CASE L_INPUT
		IF !INRANGE(L_INPUT, 1, 10)
			GOTO RETRY
		ENDIF
		IF MONEY < COST_MONEY:L_INPUT
			CALL C_INPUT_ERROR("자금이 부족합니다.")
			CONTINUE
		ENDIF
		IF FLAG:도시_현재AP < COST_AP:L_INPUT
			CALL C_INPUT_ERROR("AP가 부족합니다.")
			CONTINUE
		ENDIF
		
		MONEY -= COST_MONEY:L_INPUT
		FLAG:도시_현재AP -= COST_AP:L_INPUT
		
		;成功確率の設定
		L_S = 30 + SUC_PLUS:L_INPUT
		;最案だった場合補正＋
		IF L_INPUT == CFLAG:C_ID:(4400 + N_ID)
			L_S += 50
		;愚案だった場合補正―
		ELSEIF L_INPUT == CFLAG:C_ID:(4500 + N_ID)
			L_S -= 50
		ENDIF
		
		ABL:MASTER:해결기능 += 1
		L_S += L_S * (1 + (ABL:MASTER:해결기능 / 100))
		L_K = (CFLAG:C_ID:호감도) / 50
		SIF L_K  > 200
			L_K  = 200
		
		CFLAG:C_ID:주민_悩み解決成功確立 = (L_K + L_S) - (CFLAG:C_ID:(4300 + N_ID) * 2)
		SIF CFLAG:C_ID:주민_悩み解決成功確立 >= 100
			CFLAG:C_ID:주민_悩み解決成功確立 = 100
		CFLAG:C_ID:주민_悩み解決成功確立 -= CFLAG:C_ID:(4300 + N_ID)

		CFLAG:C_ID:주민_悩み解決判定回数 = (RAND:1) + 1
		CFLAG:C_ID:주민_悩み解決合計加算 = 0

		;店に行くのは金が要る
		IF L_INPUT == 5 && (CFLAG:C_ID:주민_소지금 >= (CFLAG:C_ID:(4300 + N_ID) * 5000) * CFLAG:C_ID:주민_난이도)
			CFLAG:C_ID:주민_悩み解決成功確立 += 20
		ELSE
			CFLAG:C_ID:주민_悩み解決成功確立 -= 20
		ENDIF
		
		IF CFLAG:C_ID:관계단계 >= 2 && RAND:100 > 50
			PRINTDATAL
				DATAFORM 「もう%CSTR:C_ID:一人称%が悩んでいること、全部知られているみたい%CSTR:C_ID:어미肯定2%……%CSTR:C_ID:어미微笑%」
				DATAFORM 「%CSTR:C_ID:어미YES%、%CSTR:C_ID:二人称%がいてくれれば解決できそう%CSTR:C_ID:어미です%」
				DATAFORM 「%CSTR:C_ID:어미感謝%、本当に助かってい%CSTR:C_ID:어미ます%」
			ENDDATA
		ELSEIF CFLAG:C_ID:관계단계 >= 1 && RAND:100 > 50
			PRINTDATAL
				DATAFORM 「いつも助けてもらって悪い%CSTR:C_ID:어미応答2%、お願い%CSTR:C_ID:어미問う2%」
				DATAFORM 「%CSTR:C_ID:어미YES%、%CSTR:C_ID:二人称%の%CSTR:C_ID:어미丁寧%力があれば解決できそう%CSTR:C_ID:어미です%」
				DATAFORM 「丁度、%CSTR:C_ID:二人称%の%CSTR:C_ID:어미丁寧%力を借りようと思ってた%CSTR:C_ID:어미ので%…%CSTR:C_ID:어미感謝%」
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 「%CSTR:C_ID:어미感謝%、最近この問題で苦労してた%CSTR:C_ID:어미繋ぎ%%CSTR:C_ID:어미です%…」
				DATAFORM 「ずっと解決策が浮かばなった%CSTR:C_ID:어미ので%……%CSTR:C_ID:어미謝罪%、感謝%CSTR:C_ID:어미問う2%」
				DATAFORM 「迷惑でないなら%CSTR:C_ID:二人称%の力を%CSTR:C_ID:어미丁寧%借り%CSTR:C_ID:어미ます%」
			ENDDATA
		ENDIF
		PRINTFORMW 해결 결과는 {(CFLAG:C_ID:주민_悩み解決判定回数) + 1}일 후 확인할 수 있습니다.
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("무효한 입력입니다.")
		CONTINUE
	ENDSELECT
	CFlag:C_ID:주민_悩み前回協力 = RESULT	;前回結果表示用
	BREAK
LOOP 1
FLAG:도시_현재TP -= 1
RETURN 1



;-------------------------------------------------
;悩み解決ボタンのリスト表示関数
;-------------------------------------------------
;ボタンリストを表示するとともに各種コストを計算する
@NAYAMI_PLAN_BUTTON_LIST(C_ID, N_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
#DIM  C_ID
#DIM  N_ID
#DIMS REF PLAN_NAME
#DIM  REF SUC_PLUS
#DIM  REF COST_AP
#DIM  REF COST_MONEY
CALLF VAR_NAYAMI_PLAN_COST(C_ID, N_ID, COST_AP, COST_MONEY)
FOR LOCAL, 1, 11
	CALL NAYAMI_PLAN_BUTTON(C_ID, LOCAL, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
NEXT



;-------------------------------------------------
;個々の悩み解決ボタン関数
;-------------------------------------------------
@NAYAMI_PLAN_BUTTON(C_ID, P_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
#DIM  C_ID
#DIM  P_ID
#DIMS REF PLAN_NAME
#DIM  REF SUC_PLUS
#DIM  REF COST_AP
#DIM  REF COST_MONEY
PRINTFORM [{P_ID, 2}] - %PLAN_NAME:P_ID%
IF SUC_PLUS:P_ID < 0
	PRINTFORM 【보정 -{ABS(SUC_PLUS:P_ID), 2}】
ELSE
	PRINTFORM 【보정 +{ABS(SUC_PLUS:P_ID), 2}】
ENDIF
IF COST_AP:P_ID == 0
	PRINTFORM %" " * 12%
ELSE
	PRINTFORM (필요 AP:{COST_AP:P_ID, 3})
ENDIF
SIF COST_MONEY:P_ID
	PRINTFORM (필요자금:{COST_MONEY:P_ID})
PRINTFORML 







;-------------------------------------------------
; 口説く
;-------------------------------------------------
@CAFE_TALK_KDK(L_I)
#DIM L_I
CFlag:L_I:설득도 += CAFE_TALK_KDK_CALC(L_I)
CFlag:L_I:설득도 = LIMIT(CFlag:L_I:설득도, 0, 100)

PRINTFORML 
DRAWLINE
PRINTFORML 회화 도중, %마스터는% %조사처리(CALLNAME:L_I,"를")% 살짝 끌어안으며 달콤한 말을 건냈다…….
IF CFlag:L_I:설득도 == 100
	PRINTFORML 
	PRINTDATAL
		DATAFORM 「………いい%CSTR:L_I:어미許可%…%CSTR:L_I:二人称%になら身体を任せられ%CSTR:L_I:어미ます%…」
		DATAFORM 「%CSTR:L_I:어미YES%……%CSTR:L_I:一人称%も%CSTR:L_I:二人称%ともっと深い関係になりたい%CSTR:L_I:어미です2%…」
		DATAFORM 「%CSTR:L_I:어미微笑%……そんなに焦らなくても拒絶はしない%CSTR:L_I:어미許可%……今度近いうちに…%UNICODE(0x2661)%」
		DATAFORM 「……%CSTR:L_I:어미YES%%UNICODE(0x2661)%………%CSTR:L_I:一人称%も……もっと…」
		DATAFORM 「…今度、空いてる時間に呼んで%CSTR:L_I:어미希望%…？………するなら長く愛し合いたい%CSTR:L_I:어미です2%…%UNICODE(0x2661)%」
		DATAFORM 「…す、するなら別の場所が……%CSTR:L_I:二人称%と%CSTR:L_I:一人称%……二人きりで…%UNICODE(0x2661)%」
		DATAFORM 「……%UNICODE(0x2661)%、…%CSTR:L_I:二人称%が%CSTR:L_I:一人称%を本気にさせた%CSTR:L_I:어미繋ぎ%%CSTR:L_I:어미です%……責任取って%CSTR:L_I:어미希望%…♪」
		DATAFORM 「%CSTR:L_I:二人称%にここまで求愛されたら、受け入れるしかない%CSTR:L_I:어미応答2%…%UNICODE(0x2661)%」
		DATAFORM 「…携帯番号教える%CSTR:L_I:어미ので%……時間があるとき呼んで%CSTR:L_I:어미希望%…%UNICODE(0x2661)%」
		DATAFORM 「…もう、仕方ない%CSTR:L_I:어미応答2%……優しくするって約束できるのなら…%UNICODE(0x2661)%」
	ENDDATA
	PRINTFORML ……어프로치 끝에, %조사처리(CALLNAME:L_I,"는")% 깊은 관계가 되는 것을 받아들인 것 같다.
	PRINTFORML %조사처리(CALLNAME:L_I,"로")%부터 전화번호를 입수하고, 【합의】를 얻었다!
	CFlag:L_I:携帯番号入手 = 1
	Talent:L_I:합의 = 1
	SIF MARK:L_I:함락각인 == 0
		MARK:L_I:함락각인 = 1
	Talent:L_I:비합의 = 0
ELSE
	PRINTFORML
	PRINTDATAL
		DATAFORM 「ま、まだダメ%CSTR:L_I:어미です%っ、……もっと친밀になったら…」
		DATAFORM 「こんなに密着して……でも悪い気しない%CSTR:L_I:어미応答2%…」
		DATAFORM 「女は무드に弱い……実際に経験すると反論の余地はない%CSTR:L_I:어미応答2%…」
		DATAFORM 「……♪、いずれはそういう関係になるのも悪くない%CSTR:L_I:어미応答2%…」
		DATAFORM 「こうして触れ合っていると…まるで恋人みたい%CSTR:L_I:어미肯定2%…」
		DATAFORM 「いろいろ助けてもらった%CSTR:L_I:어미繋ぎ%%CSTR:L_I:어미逆説%…これ以上の関係になるならもっと深く%CSTR:L_I:二人称%を知りたい%CSTR:L_I:어미応答2%」
		DATAFORM 「%CSTR:L_I:一人称%を抱きたいのなら%CSTR:L_I:二人称%をもっと教え%CSTR:L_I:어미命令強%？…体を預けられる人かよく知りたい%CSTR:L_I:어미繋ぎ%%CSTR:L_I:어미です%」
		DATAFORM 「…もしかして、%CSTR:L_I:一人称%を…抱きたいの%CSTR:L_I:어미問う%…？」
		DATAFORM 「…호의を持たれるのは嬉しい%CSTR:L_I:어미逆説%、…心の準備が…」
		DATAFORM 「っ…！、…い、いきなり言われても…困\@STRFIND("らりるれろ",SUBSTRINGU(CSTR:L_I:어미ます,0,1)) ?  # り\@%CSTR:L_I:어미ます%…」
	ENDDATA
	PRINTFORML ……와닿지 않았지만, %조사처리(CALLNAME:L_I,"는")% 살짝 가드를 늦춘 것 같다…….
ENDIF
PRINTFORMW



@CAFE_TALK_KDK_CALC(L_I)
#FUNCTION
#DIM L_I
#DIM KDK_POINT
KDK_POINT = (CFLAG:L_I:호감도 / 200) + RAND(1, 4)
SIF TALENT:L_I:창녀
	KDK_POINT += 5

KDK_POINT *= 100
SIF TALENT:L_I:즉각타락
	TIMES KDK_POINT, 5
SIF TALENT:L_I:금사빠 == 1
	TIMES KDK_POINT, 2
SIF TALENT:L_I:정조관념없음
	TIMES KDK_POINT, 1.2

SIF TALENT:L_I:정조관념
	TIMES KDK_POINT, 0.8
SIF TALENT:L_I:경계심 == 1
	TIMES KDK_POINT, 0.2
SIF TALENT:L_I:인간불신 == 1
	TIMES KDK_POINT, 0.5
SIF TALENT:L_I:고원의꽃 == 1
	TIMES KDK_POINT, 0.5
SIF TALENT:L_I:백합공주 == 1
	TIMES KDK_POINT, 0.1

RETURNF MAX(KDK_POINT / 100, 1)



;-------------------------------------------------
; 関係構築　従属
;-------------------------------------------------
@CAFE_TALK_R_J(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %마스터는% 이쪽의 지시를 받아들이도록 길들였다…….
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:충성도 >= 900 && MARK:L_I:예속각인 == 3 && TALENT:L_I:심취  == 0 && TALENT:L_I:정부  == 0 && TALENT:L_I:애인  == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 종속화가 극에 달해, %조사처리(CALLNAME:L_I,"는")% %CALLNAME:MASTER%에게 심취한 자가 된 것 같다…….
	PRINTFORML %조사처리(CALLNAME:L_I,"는")% 【심취】를 취득했다.
	TALENT:L_I:심취  = 1
	TALENT:L_I:바람 = 0
	Flag:陥落人数 += 1
ELSEIF CFlag:L_I:충성도 >= 600 && MARK:L_I:예속각인 == 2
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 종속화의 끝에, 마치 봉사하는 듯한 행동을 취하게 된 것 같다……. 끝이 얼마 남지 않은 것 같다…….
	PRINTFORML %CALLNAME:L_I%의 예속각인이 1 상승했다.
	MARK:L_I:예속각인 += 1
ELSEIF CFlag:L_I:충성도 >= 300 && MARK:L_I:예속각인 == 1
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 종속화가 진행되어, 어느 정도의 지시라면 어렵지 않게 받아들이게 된 것 같다…….
	PRINTFORML %CALLNAME:L_I%의 예속각인이 1 상승했다.
	MARK:L_I:예속각인 += 1
ELSEIF CFlag:L_I:충성도 >= 100 && MARK:L_I:예속각인 == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 어느정도 인상효과가 있었는지, %조사처리(CALLNAME:L_I,"는")% 이쪽을 존경하는 눈빛으로 보고 있다…….
	PRINTFORML %CALLNAME:L_I%의 예속각인이 1 상승했다.
	MARK:L_I:예속각인 += 1
ELSE
	CALL PRINT_LABEL("변화 없음")
	PRINTL
	PRINTFORML 시간을 써가며 이야기해 보았지만, 딱히 심경의 변화는 없었던 것 같다…….
ENDIF

DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 関係構築　恋愛
;-------------------------------------------------
@CAFE_TALK_R_R(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %마스터는% 과하지 않은 정도로 사랑해 주기로 했다…….
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:애정도 >= 900 && MARK:L_I:함락각인 == 3 && TALENT:L_I:심취  == 0 && TALENT:L_I:정부  == 0 && TALENT:L_I:애인  == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 조금이라도 %마스터와% 오래 있고 싶은지, 몸으로 말리는 것도 불사하는 것 같다…….
	PRINTFORML %조사처리(CALLNAME:L_I,"는")% 【애인】을 취득했다.
	TALENT:L_I:애인  = 1
	TALENT:L_I:바람 = 0
	Flag:陥落人数 += 1
ELSEIF CFlag:L_I:애정도 >= 600 && MARK:L_I:함락각인 == 2
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 일과성이였을 사랑은, 확실한 사랑이 되어 %조사처리(CALLNAME:L_I,"를")% 한 명의 여자로 만든 것 같다…….
	PRINTFORML %CALLNAME:L_I%의 함락각인이 1 상승했다.
	MARK:L_I:함락각인 += 1
ELSEIF CFlag:L_I:애정도 >= 300 && MARK:L_I:함락각인 == 1
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML %조사처리(CALLNAME:L_I,"는")% 품고 있던 연모를 자각하고, 이쪽에게 애정을 숨김없이 드러내고 있다…….
	PRINTFORML %CALLNAME:L_I%의 함락각인이 1 상승했다.
	MARK:L_I:함락각인 += 1
ELSEIF CFlag:L_I:애정도 >= 100 && MARK:L_I:함락각인 == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 교류의 성과로, %조사처리(CALLNAME:L_I,"는")% 이쪽에 대한 옅은 연모를 품고 있는 듯하다…….
	PRINTFORML %CALLNAME:L_I%의 함락각인이 1 상승했다.
	MARK:L_I:함락각인 += 1
ELSE
	CALL PRINT_LABEL("변화 없음")
	PRINTL
	PRINTFORML 시간을 써가며 이야기해 보았지만, 딱히 심경의 변화는 없었던 것 같다…….
ENDIF
DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 関係構築　淫乱
;-------------------------------------------------
@CAFE_TALK_R_I(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %마스터는% %CALLNAME:L_I%에게 잠들어 있는 "성"을 드러내도록 했다…….
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:음란도 >= 900 && MARK:L_I:음란각인 == 3 && TALENT:L_I:심취  == 0  && TALENT:L_I:애인  == 0 && TALENT:L_I:정부  == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 
	PRINTFORML %조사처리(CALLNAME:L_I,"는")% 【정부】를 취득했다.
	TALENT:L_I:정부  = 1
	TALENT:L_I:바람 = 0
	Flag:陥落人数 += 1
ELSEIF CFlag:L_I:음란도 >= 600 && MARK:L_I:음란각인 == 2
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 
	PRINTFORML %CALLNAME:L_I%의 음란각인이 1 상승했다.
	MARK:L_I:음란각인 += 1
ELSEIF CFlag:L_I:음란도 >= 300 && MARK:L_I:음란각인 == 1
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 조교의 성과인지, %CALLNAME:L_I%의 행동이 요염해졌다…….
	PRINTFORML %CALLNAME:L_I%의 음란각인이 1 상승했다.
	MARK:L_I:음란각인 += 1
ELSEIF CFlag:L_I:음란도 >= 100 && MARK:L_I:음란각인 == 0
	CALL PRINT_LABEL("성공")
	PRINTL
	PRINTFORML 얼굴을 붉히면서도, 은밀한 육욕을 제어할 수 없게 된 것 같다…….
	PRINTFORML %CALLNAME:L_I%의 음란각인이 1 상승했다.
	MARK:L_I:음란각인 += 1
ELSE
	CALL PRINT_LABEL("변화 없음")
	PRINTL
	PRINTFORML 시간을 써가며 이야기해 보았지만, 딱히 심경의 변화는 없었던 것 같다…….
ENDIF
DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 悩み一覧　簡易版
;-------------------------------------------------

@NAYAMI_PAGE(ARG)
#DIM L_I
#DIM I_LOOP
#DIM L_color

I_LOOP = 1
L_color = GETCOLOR()
L_I = ARG
DRAWLINE
PRINTFORML ◇-----------------큰　고민-----------------◇
REPEAT 5
	IF CFlag:L_I:(4000+I_LOOP) != 0
		IF CFlag:L_I:(4100+I_LOOP) >= 100
			IF CFlag:L_I:(4000+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("금전문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("연애문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("가정문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_생활")
				CALL PRINT_LABEL("생활문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("범죄문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("성적문제")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(미판명)
			CALL PRINT_LABEL("해석",,,TOSTR(CFlag:L_I:(4100+I_LOOP)),3,,"/","100")
		ENDIF
		PRINTFORM 난도:{CFlag:L_I:(4300+I_LOOP),2}　
		PRINTFORML 달성률:({CFlag:L_I:(4200+I_LOOP),3}/100)
		SETCOLOR L_color
	ENDIF
	I_LOOP += 1
REND
PRINTFORML ◇----------------작은　고민----------------◇
I_LOOP = 1
REPEAT 5
	IF CFlag:L_I:(4010+I_LOOP) != 0
		IF CFlag:L_I:(4110+I_LOOP) >= 100
			IF CFlag:L_I:(4010+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("금전문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("연애문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("가정문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_생활")
				CALL PRINT_LABEL("생활문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("범죄문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("성적문제")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(미판명)【해석:{CFlag:L_I:(4110+I_LOOP),3}/100】
		ENDIF
		PRINTFORM 난도:{CFlag:L_I:(4310+I_LOOP),2}　
		PRINTFORML 달성률:({CFlag:L_I:(4210+I_LOOP),3}/100)
		SETCOLOR L_color
	ENDIF
	I_LOOP += 1
REND
DRAWLINE



;-------------------------------------------------
;キャラ切替機能つき喫茶店会話関数
;-------------------------------------------------
@CAFE_TALK_GROUP(CHARA_LIST, CHARA_VIEW, CHARA_CNT, OP)
#DIM REF CHARA_LIST
#DIM     CHARA_VIEW
#DIM     CHARA_CNT
#DIMS    OP
#DIM     C_ID
#DIM     L_INPUT
#DIM     L_color
C_ID = CHARA_LIST:CHARA_VIEW
CALL INFO_STATUS_NAYAMI_LIST(C_ID)
CALL C_PICTURE_T(C_ID)
CALL INFO_CITY_BASE
L_color = GETCOLOR()
DRAWLINE
IF TALENT:C_ID:합의 || CFLAG:C_ID:관계단계 >= 2
	PRINTFORM 　
	PRINTBUTTON "[7] - 경향변경　　　", 7
ELSE
	PRINTFORM %"　" * 11%
ENDIF
IF STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 11%
ELSE
	PRINTFORM 　
	PRINTBUTTON "[8] - 이전 캐릭터 　", 8
ENDIF
IF TALENT:C_ID:합의 || CFLAG:C_ID:관계단계 >= 2
	PRINTFORM 　
	PRINTBUTTON "[9] - 관계구축　　　", 9
ENDIF
PRINTFORML 
PRINTFORM 　
SIF FLAG:도시_현재TP < 1 && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTBUTTON "[4] - 대화한다(TP:1)", 4
SETCOLOR L_color
IF VAR_NAYAMI_ASSIST_ABLE_LIST(C_ID) && CFLAG:C_ID:주민_悩み解決判定回数 == 0 ;!CFLAG:C_ID:주민_悩み협력상황
	PRINTFORM 　
	SIF FLAG:도시_현재TP < 1 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTBUTTON "[5] - 고민해결(TP:1)", 5
	SETCOLOR L_color
ELSE
	PRINTFORM %"　" * 11%
ENDIF
IF TALENT:C_ID:합의 == 0 && (CFLAG:C_ID:관계단계 >= 2 || TALENT:C_ID:창녀)
	PRINTFORM 　
	SIF FLAG:도시_현재TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTBUTTON "[6] - 구애한다(TP:5)", 6
	SETCOLOR L_color
ENDIF
PRINTFORML 
PRINTFORM 　
PRINTBUTTON "[1] - 능력표시　　　", 1

IF STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 11%
ELSE
	PRINTFORM 　
	PRINTBUTTON "[2] - 다음 캐릭터 　", 2
ENDIF
PRINTFORM 　
SIF !FLAG:도시_현재BP && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTBUTTON "[3] - BP섭취　　　　", 3
SETCOLOR L_color
PRINTFORML 
PRINTFORML 
PRINTBUTTON "[0] - 돌아간다　　　", 0
DO
	ONEINPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 7
		SIF !(TALENT:C_ID:합의 || CFLAG:C_ID:관계단계 >= 2)
			GOTO RETRY
		PRINTFORML 접촉 방법을 어떻게 변경하시겠습니까?
		PRINTFORM 　
		SIF FLAG:도시_현재TP < 3 && FLAG:STATUS伏字モード
			SETCOLOR COLOR("伏字")
		PRINTBUTTON "[1] - 충성경향(TP:3)", 1
		PRINTFORM 　
		PRINTBUTTON "[2] - 애정경향(TP:3)", 2
		PRINTFORM 　
		PRINTBUTTON "[3] - 음란경향(TP:3)", 3
		SETCOLOR L_color
		PRINTFORML 
		PRINTFORML 
		PRINTFORM 　
		PRINTBUTTON "[0] - 취소　　　　　", 0
		PRINTFORM %"　" * 11%
		PRINTFORM 　
		PRINTBUTTON "[9] - 경향 없음 　　", 9
		DO
			ONEINPUT
			SIF INRANGE(RESULT, 1, 3) && FLAG:도시_현재TP < 3
				GOTO LESS_TP
			SELECTCASE RESULT
			CASE 1
				FLAG:도시_현재TP -= 3
				CFLAG:C_ID:値傾向 = 1
			CASE 2
				FLAG:도시_현재TP -= 3
				CFLAG:C_ID:値傾向 = 2
			CASE 3
				FLAG:도시_현재TP -= 3
				CFLAG:C_ID:値傾向 = 3
			CASE 9
				CFLAG:C_ID:値傾向 = 0
			CASEELSE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 9
		SIF !(TALENT:C_ID:합의 || CFLAG:C_ID:관계단계 >= 2)
			GOTO RETRY
		PRINTFORML 어떤 관계를 구축하시겠습니까？
		PRINTFORM 　
		SIF FLAG:도시_현재TP < 5 && FLAG:STATUS伏字モード
			SETCOLOR COLOR("伏字")
		PRINTBUTTON "[1] - 종속√(TP:5)", 1
		PRINTFORM 　
		PRINTBUTTON "[2] - 연애√(TP:5)", 2
		PRINTFORM 　
		PRINTBUTTON "[3] - 음란√(TP:5)", 3
		SETCOLOR L_color
		PRINTFORML 
		PRINTFORML 
		PRINTFORM 　
		PRINTBUTTON "[0] - 취소　　　　", 0
		DO
			ONEINPUT
			SIF INRANGE(RESULT, 1, 3) && FLAG:도시_현재TP < 5
				GOTO LESS_TP
			SELECTCASE RESULT
			CASE 1
				FLAG:도시_현재TP -= 5
				CALL CAFE_TALK_R_J(C_ID)
			CASE 2
				FLAG:도시_현재TP -= 5
				CALL CAFE_TALK_R_R(C_ID)
			CASE 3
				FLAG:도시_현재TP -= 5
				CALL CAFE_TALK_R_I(C_ID)
			CASEELSE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 8
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT - 1) % CHARA_CNT
	CASE 2
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT + 1) % CHARA_CNT
	CASE 4
		IF FLAG:도시_현재TP < 1
			$LESS_TP
			CALL C_INPUT_ERROR("TP가 부족합니다")
			RESTART
		ENDIF
		FLAG:도시_현재TP --
		CALL CAFE_TALK2(C_ID)
	CASE 5
		SIF FLAG:도시_현재TP < 1
			GOTO LESS_TP
		;SIF CFLAG:C_ID:주민_悩み협력상황
			;GOTO RETRY
		SIF !VAR_NAYAMI_ASSIST_ABLE_LIST(C_ID)
			GOTO RETRY
		;FLAG:도시_현재TP --
		CALL CAFE_TALK_ASSIST_NAYAMI(C_ID)
		CALL NAYAMI_CHALLENGE(C_ID)
	CASE 6
		SIF FLAG:도시_현재TP < 5
			GOTO LESS_TP
		SIF TALENT:C_ID:합의 != 0
			GOTO RETRY
		SIF !(CFLAG:C_ID:관계단계 >= 2 || TALENT:C_ID:창녀)
			GOTO RETRY
		FLAG:도시_현재TP -= 5
		CALL CAFE_TALK_KDK(C_ID)
	CASE 1
		CALL INFO_STATUS_MAIN(C_ID)
	CASE 3
		CALL SYSTEM_BP
	CASE 0
		RETURN
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("무효한 입력입니다")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;-------------------------------------------------
;おなやみ協力関数
;-------------------------------------------------
;協力してあげるよ！っていうだけでここでは何もおこらない
@CAFE_TALK_ASSIST_NAYAMI(C_ID)
#DIM C_ID
CALL INFO_STATUS_NAYAMI_LIST(C_ID)
;PRINTFORML 
;PRINTFORML %CALLNAME:MASTER%は%CALLNAME:C_ID%の悩み解決への協力を申し出た
;PRINTFORMW 時間経過時に%CALLNAME:C_ID%が悩み相談にやってきます
CALL VAR_NAYAMI_SET_ASSIST(C_ID)



