;-------------------------------------------------
;キャラ変数参照関数
;-------------------------------------------------
;特定キャラ、特定変数、特定要素を参照する汎用関数
@C_GET_CVAR_VALUE(C_ID, VAR_NAME, VAR_ID)
#FUNCTION
#DIM  DYNAMIC C_ID     ;
#DIMS DYNAMIC VAR_NAME ;
#DIM  DYNAMIC VAR_ID   ;
#DIM  DYNAMIC TEMP_INT ;
#DIM  CONST EXTRA_수반     = 0;VAR_IDの分岐用定数
#DIM  CONST EXTRA_후방     = 1
#DIM  CONST EXTRA_회수     = 2
#DIM  CONST EXTRA_所属부대 = 3
#DIM  CONST EXTRA_配属시설 = 4
#DIM  CONST EXTRA_친분     = 5
#DIM  CONST EXTRA_탐색     = 6
#DIM  CONST EXTRA_태생     = 7
#DIM  CONST EXTRA_직업     = 8
#DIM  CONST EXTRA_관리指数 = 9
#DIM  CONST EXTRA_기밀     = 10
#DIM  CONST EXTRA_합의     = 11
#DIM  CONST EXTRA_호감도LV = 12
#DIM  CONST EXTRA_공략     = 13
#DIM  CONST EXTRA_계급     = 14
SELECTCASE VAR_NAME
CASE "ABL"
	RETURNF ABL:C_ID:VAR_ID
CASE "BASE"
	RETURNF BASE:C_ID:VAR_ID
CASE "MAXBASE"
	RETURNF MAXBASE:C_ID:VAR_ID
CASE "EXP"
	RETURNF EXP:C_ID:VAR_ID
CASE "CFLAG"
	RETURNF CFLAG:C_ID:VAR_ID
CASE "PALAM"
	RETURNF PALAM:C_ID:VAR_ID
CASE "TALENT"
	RETURNF TALENT:C_ID:VAR_ID
CASE "TCVAR"
	RETURNF TCVAR:C_ID:VAR_ID
CASE "MARK"
	RETURNF MARK:C_ID:VAR_ID
CASE "EXTRA"
	SELECTCASE VAR_ID
	CASE EXTRA_수반
		RETURNF CFLAG:C_ID:주민_ATK + CFLAG:C_ID:주민_DEF + CFLAG:C_ID:주민_SPD + CFLAG:C_ID:주민_HIT
	CASE EXTRA_후방
		RETURNF CFLAG:C_ID:주민_SPD + CFLAG:C_ID:주민_HIT
	CASE EXTRA_회수
		RETURNF CFLAG:C_ID:주민_SPD + CFLAG:C_ID:주민_LUK
	CASE EXTRA_탐색
		;複雑な評価値になっているが
		;これは扱いづらい이스が最上部に来るのを避けるために試行錯誤したせい
		{
		TEMP_INT = 
		CFLAG:C_ID:주민_ATK * 3
		 + CFLAG:C_ID:주민_DEF * 2
		 + CFLAG:C_ID:주민_SPD * 3
		 + CFLAG:C_ID:주민_HIT * 4
		 + CFLAG:C_ID:주민_현재체력 * 5
		 + CFLAG:C_ID:주민_현재기력 * 8
		 + CFLAG:C_ID:주민_현재BRE
		 + CFLAG:C_ID:주민_BRAVE * 5
		}
		RETURNF TEMP_INT / 10
	CASE EXTRA_所属부대
		;所属なし＝0、주민_支援_수반＝1、CFLAG側のIDは주민_支援_수반＝6800なので注意
		TEMP_INT = MAX(0, 1 + FINDELEMENT(CFLAG:C_ID:0, 1, 6800, 6804) - 6800)
		RETURNF TEMP_INT
	CASE EXTRA_配属시설
		;注意点に関しては所属부대と同様
		TEMP_INT = MAX(0, 1 + FINDELEMENT(CFLAG:C_ID:0, 1, 10000, 10100) - 10000)
		RETURNF TEMP_INT
	CASE EXTRA_친분
		;陥落と刻印だけ見る、反発は考慮しない
		IF TALENT:C_ID:심취
			TEMP_INT = 30
		ELSEIF TALENT:C_ID:애인
			TEMP_INT = 20
		ELSEIF TALENT:C_ID:정부
			TEMP_INT = 10
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT + SUMARRAY(MARK:C_ID:0, 0, 3)
	CASE EXTRA_태생
		IF TALENT:C_ID:주인있음 == 1
			TEMP_INT = 6
		ELSEIF TALENT:C_ID:구세대인 == 1
			TEMP_INT = 5
		ELSEIF TALENT:C_ID:前성노예 == 1
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:인권손괴 == 1
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:이레귤러 == 1
			TEMP_INT = 2
		ELSEIF TALENT:C_ID:푸른혈족 == 1
			TEMP_INT = 1
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT * 100 + CFLAG:C_ID:주민_종족
	CASE EXTRA_직업
		IF TALENT:C_ID:메이드 == 1
			TEMP_INT = 7
		ELSEIF TALENT:C_ID:성직자 == 1
			TEMP_INT = 6
		ELSEIF TALENT:C_ID:치안유지국원 == 1
			TEMP_INT = 5
		ELSEIF TALENT:C_ID:사회인 == 1
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:아이돌 == 1
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:학생
			TEMP_INT = 2
		ELSE
			TEMP_INT = 1
		ENDIF
		
		RETURNF TEMP_INT * 100 + TALENT:C_ID:창녀 * 10 + (TALENT:C_ID:학생 + 1)
	CASE EXTRA_관리指数
		TEMP_INT = CFLAG:C_ID:관계단계 + CFLAG:C_ID:レギュラー * 3 + CFLAG:C_ID:特別保護 * 2
		SIF CFLAG:C_ID:マンションID
			TEMP_INT += 2
		RETURNF TEMP_INT
	CASE EXTRA_기밀
		RETURNF MAX(FINDLASTELEMENT(TALENT:C_ID:0, 1, 3000, 3010) - 3000, 0)
	CASE EXTRA_합의
		IF TALENT:C_ID:조교완료
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:합의
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:범해짐
			TEMP_INT = 2
		ELSEIF TALENT:C_ID:약점
			TEMP_INT = 1
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT
	CASE EXTRA_호감도LV
		SIF CFLAG:C_ID:호감도 <= 0
			RETURNF 0
		RETURNF LIMIT(SQRT(CFLAG:C_ID:호감도), 1, 999)
	CASE EXTRA_공략
		IF CFLAG:C_ID:충성도 >= 900 && MARK:C_ID:예속각인 == 3 && TALENT:C_ID:심취  == 0 && TALENT:C_ID:정부  == 0 && TALENT:C_ID:애인  == 0
			RETURNF 1003
		ELSEIF CFLAG:C_ID:애정도 >= 900 && MARK:C_ID:함락각인 == 3 && TALENT:C_ID:심취  == 0 && TALENT:C_ID:정부  == 0 && TALENT:C_ID:애인  == 0
			RETURNF 1002
		ELSEIF CFLAG:C_ID:음란도 >= 900 && MARK:C_ID:음란각인 == 3 && TALENT:C_ID:심취  == 0  && TALENT:C_ID:애인  == 0 && TALENT:C_ID:정부  == 0
			RETURNF 1001
		ELSEIF CFLAG:C_ID:충성도 >= 600 && MARK:C_ID:예속각인 <= 2
			RETURNF 103
		ELSEIF CFLAG:C_ID:애정도 >= 600 && MARK:C_ID:함락각인 <= 2
			RETURNF 102
		ELSEIF CFLAG:C_ID:음란도 >= 600 && MARK:C_ID:음란각인 <= 2
			RETURNF 101
		ELSEIF CFLAG:C_ID:충성도 >= 300 && MARK:C_ID:예속각인 <= 1
			RETURNF 13
		ELSEIF CFLAG:C_ID:애정도 >= 300 && MARK:C_ID:함락각인 <= 1
			RETURNF 12
		ELSEIF CFLAG:C_ID:음란도 >= 300 && MARK:C_ID:음란각인 <= 1
			RETURNF 11
		ELSEIF CFLAG:C_ID:충성도 >= 100 && MARK:C_ID:예속각인 <= 0
			RETURNF 3
		ELSEIF CFLAG:C_ID:애정도 >= 100 && MARK:C_ID:함락각인 <= 0
			RETURNF 2
		ELSEIF CFLAG:C_ID:음란도 >= 100 && MARK:C_ID:음란각인 <= 0
			RETURNF 1
		ENDIF
	CASE EXTRA_계급
		TEMP_INT = MAX(0, FINDELEMENT(TALENT:C_ID:0, 1, 3101, 3108) - 3100)
		RETURNF TEMP_INT
	ENDSELECT
	RETURNF 0
CASEELSE
	THROW 불명なVAR_NAME %VAR_NAME%
ENDSELECT


