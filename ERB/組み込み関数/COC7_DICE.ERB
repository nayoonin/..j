;@license CC0

;CoC7版ライクな다이스
;注意点: -1 をそのままifで判定すると true 扱いになるのでちうい
;@param {SKILL_VALUE} 判定に使用する技能の値
;@param {BONUS} 보너스あるいはペナルティ다이스、ペナルティ다이스の場合は負の値を指定する。
;@return 成功値 4:critical, 3:extream, 2:hard, 1:regular, 0:miss, -1:fumble
@COC7_DICE(SKILL_VALUE, BONUS)
#FUNCTION
#DIM SKILL_VALUE
#DIM BONUS
#DIM DICE_VALUE

DICE_VALUE = _COC7_DICEROLL(BONUS)
IF DICE_VALUE == 100 || (SKILL_VALUE < 50 && DICE_VALUE > 95)
    RETURNF -1
ELSEIF DICE_VALUE == 1
    RETURNF 4
ELSEIF DICE_VALUE <= SKILL_VALUE / 5
    RETURNF 3
ELSEIF DICE_VALUE <= SKILL_VALUE / 2
    RETURNF 2
ELSEIF DICE_VALUE <= SKILL_VALUE
    RETURNF 1
ELSE
    RETURNF 0
ENDIF

;@private 外部からの利用は想定していません
;@param {BONUS} 보너스あるいはペナルティ다이스、ペナルティ다이스の場合は負の値を指定する。
;@return 出目、判定を簡便にするため範囲は1-100
@_COC7_DICEROLL(BONUS)
#FUNCTION
#DIM DYNAMIC BONUS
#DIM DYNAMIC DICE_VALUE

DICE_VALUE = RAND:100 + 1

DEBUGPRINTFORML 주사위눈 : {DICE_VALUE}, 잔여보너스 : {BONUS}

IF BONUS == 0
    RETURNF DICE_VALUE
ELSEIF BONUS > 0
    ;보너스다이스の判定
    RETURNF MIN(DICE_VALUE, _COC7_DICEROLL(BONUS - 1))
ELSEIF BONUS < 0
    ;ペナルティ다이스の判定
    RETURNF MAX(DICE_VALUE, _COC7_DICEROLL(BONUS + 1))
ENDIF

;成功値を文字表現したいとき用
;@param {VALUE} 成功値 4:critical, 3:extream, 2:hard, 1:regular, 0:miss, -1:fumble
@COC7_DICE_GET_RESULT_STR(VALUE)
#FUNCTIONS
#DIM VALUE

SELECTCASE VALUE
    CASE 4
        RETURNF "Critical!!!"
    CASE 3
        RETURNF "Extream!!"
    CASE 2
        RETURNF "Hard Success!"
    CASE 1
        RETURNF "Success!"
    CASE 0
        RETURNF "Miss..."
    CASE -1
        RETURNF "Fumble!"
    CASEELSE
ENDSELECT
