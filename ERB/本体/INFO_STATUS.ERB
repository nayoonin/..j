;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:INFO_STATUS.ERB
;	Facility	:情報表示(STATUS)。
;				 情報を表示する関数を纏める。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2014/09/28		なるはや				両親表示を追加
;	003		2017/02/28		morph					関数追加（INFO_STATUS_GROUP/INFO_STATUS_GET_BY_LABEL）
;	004		2018/11/13		いんどまん				@INFO_STATUS_GET_BY_LABELに수반/후방/회수부대で必要とされる能力値合計を表示するためのIF分岐追加
;	005		2018/11/14		morph					수반/후방/회수부대の能力値算出に使用する例外処理を@C_GET_CVAR_VALUEに移動、@INFO_STATUS_GET_BY_LABELの記述を差し戻し
;	006		2019/04/15		M						表示改修＆リファクタリングMOD: 表示桁幅指定、定型ラベル表示はPRINT_STATUS.ERBへ集約、0の値は伏字モードなど
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- 表示系
;
;	・ INFO_STATUS_LIST					表示:STATUS画面(LIST)
;
;	# STATUS系(画面)
;	・ INFO_STATUS_MAIN					表示:STATUS画面
;	・ INFO_STATUS_GROUP				表示:配列利用情報表示関数
;	・ INFO_STATUS_BASES_PALAM			表示:STATUS画面(基本PALAM)
;	・ INFO_STATUS_BASES_LOG			表示:STATUS画面(基本履歴)
;	・ INFO_STATUS_TRAIN_DATA			表示:STATUS画面(調教履歴)
;
;	# STATUS系 - 詳細
;	・ INFO_STATUS_HEADER				表示:STATUS(ヘッダ)
;	・ INFO_STATUS_MARK					表示:STATUS(刻印)
;	・ INFO_STATUS_TALENT				表示:STATUS(素質)
;	・ INFO_STATUS_ABL					表示:STATUS(能力)
;	・ INFO_STATUS_EXP					表示:STATUS(経験)
;	・ INFO_STATUS_JUEL					表示:STATUS(珠)
;	・ INFO_STATUS_GOTJUEL				表示:STATUS(取得珠)
;	・ INFO_STATUS_INTRODUCTION			表示:STATUS(紹介)
;	・ INFO_STATUS_OTHER				表示:STATUS(その他)
;	・ INFO_INTRODUCTION				表示:STATUS(紹介)
;
;	# その他
;	・ INFO_SIMPLE_STATUS_EXP			簡易表示:STATUS(経験)
;	・ INFO_STATUS_GET_BY_LABEL			式中関数:ラベル使用キャラステータス参照関数


;===============================================================================

;-------------------------------------------------------------------------------
;   STATUS系(画面)
;-------------------------------------------------------------------------------

;=================================================
;   sub INFO_STATUS_LIST
;=================================================
;   表示:STATUS画面(LIST)
;-------------------------------------------------
@INFO_STATUS_LIST
#DIM L_INPUT
#DIMS L_ERR_MSG

;-----------------------------
;- 初期処理
;-----------------------------
L_INPUT=0
L_ERR_MSG=

;-----------------------------
;- データリスト
;-----------------------------
RESULT:1=0
$DATA_LIST

;- LIST:キャラリスト
CALL LIST_CHARA(@"스테이터스를 표시할 캐릭터를 선택해주세요.　", RESULT:1, L_ERR_MSG)
L_INPUT = RESULT

;- 表示:ページ別
SELECTCASE L_INPUT
	;- 終了
	CASE 1000
		RETURN 0
	;- キャラ
	CASEELSE
		;表示:STATUS画面
		CALL INFO_STATUS_MAIN(L_INPUT)
		GOTO DATA_LIST
ENDSELECT
RESTART


;=================================================
;   sub INFO_STATUS_MAIN
;=================================================
;   表示:STATUS画面
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_MAIN(ARG)
#DIM L_TMP
#DIM L_PAGE_NOW
#DIM L_PAGE_下限
#DIM L_PAGE_上限
#DIM L_PAGE_前
#DIM L_PAGE_次

#DIM L_CHARA_前
#DIM L_CHARA_次

L_TMP=0
L_PAGE_下限=0
L_PAGE_上限=0

L_CHARA_前=0
L_CHARA_次=0

;-----------------------------
;- 定数設定
;-----------------------------
L_PAGE_下限 = 0
L_PAGE_上限 = 7


;-----------------------------
;- 処理開始
;-----------------------------
$PRINT_LOOP_1

L_CHARA_前 = VAR_GET_CL("PREV",ARG)
L_CHARA_次 = VAR_GET_CL("NEXT",ARG)

L_PAGE_前 = (L_PAGE_NOW > L_PAGE_下限) ? 1 # 0
L_PAGE_次 = (L_PAGE_NOW < L_PAGE_上限) ? 1 # 0

;- 表示:STATUS(ヘッダ)
		REDRAW 0
		CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(ARG)
PRINTL

;- 表示:ページ別
SELECTCASE L_PAGE_NOW
	CASE 0
		;- 表示:STATUS画面(基本PALAM)
		DRAWLINE
		CALL INFO_STATUS_BASES_PALAM(ARG)
	CASE 1
		;- 表示:STATUS画面(詳細情報)
		DRAWLINE
		CALL INFO_PERSONAL(ARG)
	CASE 2
		;- 表示:STATUS画面(RPG情報)
		DRAWLINE
		CALL INFO_RPG(ARG)
	CASE 3
		;- 表示:STATUS画面(隠し性情報)
		DRAWLINE
		CALL INFO_SEX(ARG)
	CASE 4
		;- 表示:STATUS画面(調教履歴)
		DRAWLINE
		CALL INFO_STATUS_TRAIN_DATA(ARG)
	CASE 5
		;- 表示:STATUS画面(素質＆初体験)
		DRAWLINE
		CALL INFO_STATUS_OTHER(ARG)
	CASE 6
		;- 表示:STATUS画面(基本履歴)
		DRAWLINE
		CALL INFO_STATUS_BASES_LOG(ARG)
	CASE 7
		;- 表示:STATUS画面(紹介)
		DRAWLINE
		CALL INFO_INTRODUCTION(ARG)
ENDSELECT

;- 表示:選択部
PRINTFORMLC [1007]이전 페이지
PRINTLC [1000]돌아간다
PRINTFORMLC [1009]다음 페이지
IF L_PAGE_NOW == 2
	;PRINTFORMLC [3000] 衣装変更
ENDIF
PRINTL 
IF FLAG:유연_연구중 ==  0
	PRINTFORMLC \@(L_CHARA_前 > -1) ? [1001]이전 캐릭터 # %" " * 16%\@
	PRINTFORMLC   %" " * 18%
	PRINTFORMLC \@(L_CHARA_次 > -1) ? [1003]다음 캐릭터 # %" " * 16%\@
ENDIF

$INPUT_LOOP_1
INPUT
SELECTCASE RESULT
	CASE 1000
		RETURN
	;衣装変更
	CASE 3000
		;- 衣装:変更画面
		CALL CLOTHES_CHG_MAIN(ARG)

	;前のページ
	CASE 1007
		IF L_PAGE_NOW == 0
			L_PAGE_NOW += 7
		ELSEIF L_PAGE_前
			L_PAGE_NOW -= 1
		ENDIF
	;次のページ
	CASE 1009
		IF L_PAGE_NOW ==7
			L_PAGE_NOW -= 7
		ELSEIF L_PAGE_次
			L_PAGE_NOW += 1
		ENDIF
	;前のキャラ
	CASE 1001
		SIF L_CHARA_前 > -1
			ARG = L_CHARA_前
	;次のキャラ
	CASE 1003
		SIF L_CHARA_次 > -1
			ARG = L_CHARA_次
	CASEELSE
		;- INPUTエラー対応
		CALL C_INPUT_ERROR
		GOTO INPUT_LOOP_1
ENDSELECT

GOTO PRINT_LOOP_1



;-------------------------------------------------
;配列利用情報表示関数
;-------------------------------------------------
;@INFO_STATUS_MAINからパクった
;配列を渡さないと機能しないので注意
;ページやキャラリストの端と端がつながってます
;渡された配列に含まれる登録番号のキャラの情報を閲覧することが出来る
;戻り値：OPに「最終確認」が含まれる場合、1で決定を行うとRESULTに表示キャラの登録番号が記録される
;
;OPに含まれる文字列によって機能を切り替える
;「最終確認」　　このオプションを指定して
;　　　　　　　　1で決定を行うとRESULTに表示キャラの登録番号が記録される
;「キャラ固定」　このオプションを指定するとキャラの切替ができない
@INFO_STATUS_GROUP(CHARA_LIST, CHARA_VIEW, CHARA_CNT, OP)
#DIM REF   CHARA_LIST      ;表示に使用するキャラリスト、必須
#DIM       CHARA_VIEW      ;현재閲覧中のキャラのリスト上の位置
#DIM       CHARA_CNT       ;キャラリストのサイズ
#DIMS      OP              ;実行オプション
#DIM       L_PAGE_NOW  = 0 ;閲覧中のページ数
#DIM CONST PAGE_SIZE   = 8 ;ページのサイズ
{
#DIMS CONST TYPE_STATUS, 8 = 
	"기본PALAM", 
	"상세정보", 
	"RPG정보", 
	"생식정보", 
	"조교이력", 
	"특수소질＆첫체험", 
	"기본이력", 
	"인물소개"
}

;- 表示:STATUS(ヘッダ)
REDRAW 0
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(CHARA_LIST:CHARA_VIEW)

SELECTCASE TYPE_STATUS:L_PAGE_NOW
CASE "기본PALAM", 
	;- 表示:STATUS画面(기본PALAM)
	DRAWLINE
	CALL INFO_STATUS_BASES_PALAM(CHARA_LIST:CHARA_VIEW)
CASE "상세정보", 
	;- 表示:STATUS画面(상세정보)
	DRAWLINE
	CALL INFO_PERSONAL(CHARA_LIST:CHARA_VIEW)
CASE "RPG정보", 
	;- 表示:STATUS画面(RPG정보)
	DRAWLINE
	CALL INFO_RPG(CHARA_LIST:CHARA_VIEW)
CASE "생식정보", 
	;- 表示:STATUS画面(隠し性情報)
	DRAWLINE
	CALL INFO_SEX(CHARA_LIST:CHARA_VIEW)
CASE "조교이력", 
	;- 表示:STATUS画面(조교이력)
	DRAWLINE
	CALL INFO_STATUS_TRAIN_DATA(CHARA_LIST:CHARA_VIEW)
CASE "특수소질＆첫체험", 
	;- 表示:STATUS画面(初体験)
	DRAWLINE
	CALL INFO_STATUS_OTHER(CHARA_LIST:CHARA_VIEW)
CASE "기본이력", 
	;- 表示:STATUS画面(기본이력)
	DRAWLINE
	CALL INFO_STATUS_BASES_LOG(CHARA_LIST:CHARA_VIEW)
CASE "인물소개"
	;- 表示:STATUS画面(紹介)
	DRAWLINE
	CALL INFO_INTRODUCTION(CHARA_LIST:CHARA_VIEW)
ENDSELECT

IF STRCOUNT(OP, "「最終確認」") || STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 8%
ELSE
	PRINTBUTTON "[7] - 　시설배속", 7
ENDIF
PRINTFORM %"　" * 1%
IF STRCOUNT(OP, "「キャラ固定」")
ELSE
	PRINTBUTTON "[8] -이전 캐릭터", 8
ENDIF
PRINTFORM %"　" * 1%
IF STRCOUNT(OP, "「最終確認」") || STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 8%
ELSE
	PRINTBUTTON "[9] - 　부대배속", 9
ENDIF
PRINTFORML 

PRINTBUTTON @"[4] - 　%TYPE_STATUS:((L_PAGE_NOW + PAGE_SIZE - 1) % PAGE_SIZE), 14*2,LEFT%", 4
PRINTBUTTON @"[6] - 　%TYPE_STATUS:((L_PAGE_NOW + PAGE_SIZE + 1) % PAGE_SIZE), 14, LEFT%", 6
PRINTFORML 
PRINTFORM %"　" * 8%
PRINTFORM %"　" * 1%
IF STRCOUNT(OP, "「キャラ固定」")
ELSE
	PRINTBUTTON "[2] -다음 캐릭터", 2
ENDIF
PRINTFORM %"　" * 1%
PRINTFORML 

IF STRCOUNT(OP, "「最終確認」")
	PRINTBUTTON "[1] - 　　　결정", 1
	PRINTFORM 　　　　　　　　　　
	PRINTBUTTON "[0] - 　　　취소", 0
	PRINTFORML 
ELSE
	PRINTBUTTON "[0] - 돌아간다", 0
ENDIF

DO
	ONEINPUT
	SELECTCASE RESULT
	CASE 1
		SIF !STRCOUNT(OP, "「最終確認」")
			GOTO RETRY
		RETURN CHARA_LIST:CHARA_VIEW
	CASE 0
		RETURN -1
	[SKIPSTART]
	;衣装変更
	CASE 3000
		;- 衣装:変更画面
		CALL CLOTHES_CHG_MAIN(ARG)
	[SKIPEND]
	;前のページ
	CASE 4
		L_PAGE_NOW = (L_PAGE_NOW + PAGE_SIZE - 1) % PAGE_SIZE
	;次のページ
	CASE 6
		L_PAGE_NOW = (L_PAGE_NOW + PAGE_SIZE + 1) % PAGE_SIZE
	;시설配属
	CASE 7
		SIF STRCOUNT(OP, "「最終確認」") || STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CALL SELECT_WOKER(CHARA_LIST:CHARA_VIEW)
	;前のキャラ
	CASE 8
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT - 1) % CHARA_CNT
	;부대配属
	CASE 9
		SIF STRCOUNT(OP, "「最終確認」") || STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CALL SELECT_SUPPORT(CHARA_LIST:CHARA_VIEW)
	;次のキャラ
	CASE 2
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT + 1) % CHARA_CNT
	CASEELSE
		$RETRY
		;- INPUTエラー対応
		CALL C_INPUT_ERROR("무효한 입력입니다")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;@INFO_STATUS_GROUPの呼び出し用関数
;キャラ番号を渡すことで単体の能力表示関数として動作する
@INFO_STATUS_SINGLE(L_C_ID, OP)
#DIM  L_C_ID
#DIMS OP
CALL INFO_STATUS_GROUP(L_C_ID, 0, 1, OP + "「キャラ固定」")



;=================================================
;   sub INFO_STATUS_BASES_PALAM
;=================================================
;   表示:STATUS画面(基本PALAM)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_BASES_PALAM(ARG)

;-- 刻印

;- ライン表示:見出し
CALL C_HEADLINE("각인(MARK)")
;- 表示:STATUS(刻印)
CALL INFO_STATUS_MARK(ARG)
PRINTL


;-- 素質

;- ライン表示:見出し
CALL C_HEADLINE("소질(TALENT)")
;- 表示:STATUS(素質)
CALL INFO_STATUS_TALENT(ARG)
PRINTL


;-- 能力
;- ライン表示:見出し
CALL C_HEADLINE("능력(ABL)")
;- 表示:STATUS(能力)
CALL INFO_STATUS_ABL(ARG)
PRINTL


;=================================================
;   sub INFO_STATUS_BASES_LOG
;=================================================
;   表示:STATUS画面(基本履歴)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_BASES_LOG(ARG)

;-- 経験

;- ライン表示:見出し
CALL C_HEADLINE("경험(EXP)")
;- 表示:STATUS(経験)
CALL INFO_STATUS_EXP(ARG)
PRINTL


;-- 珠

;- ライン表示:見出し
CALL C_HEADLINE("구슬(JUEL)")
;- 表示:STATUS(珠)
CALL INFO_STATUS_JUEL(ARG)
PRINTL

;=================================================
;   sub INFO_STATUS_TRAIN_DATA
;=================================================
;   表示:STATUS画面(調教履歴)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_TRAIN_DATA(ARG)
#DIM L_I

L_I = ARG
SETCOLOR 250,100,200
PRINTFORML ――――――――――――――――――――――――%CALLNAME:L_I%의 조교이력――――――――――――――――――――――――

IF  TALENT:L_I:남성거주자 == 1

ELSEIF  MASTER != L_I 
	IF TALENT:L_I:총애 == 1
		CALL PRINT_LABEL("총애　　　　　　　　")
		IF CFLAG:L_I:체형 == 2
			PRINTFORML 		%마스터는% 그 색기 있고 풍만한 신체에 반해, 총애를 쏟는다.
		ELSEIF CFLAG:L_I:체형 == 0
			PRINTFORML 		%마스터는% 그 아름답고 가냘픈 몸에 반해, 총애를 쏟는다.
		ELSE
			PRINTFORML 		%마스터는% 그 외모에 반해, 총애를 쏟는다.
		ENDIF
	ELSEIF Exp:L_I:주인조교경험 >= 500
		CALL PRINT_LABEL("마음에 듦 　　　　　")
		IF CFLAG:L_I:체형 == 2
			PRINTFORML 		%CALLNAME:MASTER%에게 그 색기 있고 풍만한 신체를 노려져, 몇번이고 안기고 있다.
		ELSEIF CFLAG:L_I:체형 == 0
			PRINTFORML 		%CALLNAME:MASTER%에게 그 아름답고 가냘픈 몸을 노려져, 몇번이고 안기고 있다.
		ELSE
			PRINTFORML 		%CALLNAME:MASTER%에게 그 외모를 눈독 들여, 몇번이고 안기고 있다.
		ENDIF
	ENDIF
	IF TALENT:L_I:심취 == 1
		CALL PRINT_LABEL("심취　　　　　　　　")
		PRINTFORML 		%조사처리(CALLNAME:L_I,"는")% %마스터를% 마음으로부터 숭배하고 있다.
	ELSEIF TALENT:L_I:애인 == 1
		CALL PRINT_LABEL("애인　　　　　　　　")
		PRINTFORML 		%조사처리(CALLNAME:L_I,"는")% %CALLNAME:MASTER%에게 깊은 애정을 품고 있다.
	ELSEIF TALENT:L_I:정부 == 1
		CALL PRINT_LABEL("정부　　　　　　　　")
		PRINTFORML 		%조사처리(CALLNAME:L_I,"는")% %마스터와%의 육체관계에 취해 있다.
	ENDIF

	IF TALENT:L_I:합의 == 1
		CALL PRINT_LABEL("합의　　　　　　　　")
		PRINTFORML 		%마스터와% 성행위를 하기로 합의한 바 있다.
	ENDIF
	IF TALENT:L_I:조교완료 == 1
		CALL PRINT_LABEL("조교됨　　　　　　　")
		PRINTFORML 		성적인 조교가 이루어졌으며, 신체가 항상 쾌락을 원한다.
	ENDIF
	IF Exp:L_I:파이즈리경험 >= 900
		CALL PRINT_LABEL("파이즈리 Ｓ 　　　　")
		PRINTFORML 		가슴으로 사정시키기 쉬울 정도의 기교를 지녔다.
	ELSEIF Exp:L_I:파이즈리경험 >= 700
		CALL PRINT_LABEL("파이즈리 Ａ 　　　　")
		PRINTFORML 		성행위와 동등한 쾌락을 주는 것이 가능해졌다.
	ELSEIF Exp:L_I:파이즈리경험 >= 500
		CALL PRINT_LABEL("파이즈리 Ｂ 　　　　")
		PRINTFORML 		어떤 페니스건 사정시킬 수 있다.
	ELSEIF Exp:L_I:파이즈리경험 >= 300
		CALL PRINT_LABEL("파이즈리 Ｃ 　　　　")
		PRINTFORML 		기교를 섞은 가슴을 써 사정을 이끌어 낼 수 있다.
	ELSEIF Exp:L_I:파이즈리경험 >= 100
		CALL PRINT_LABEL("파이즈리 Ｄ 　　　　")
		PRINTFORML 		가슴으로 사정시킬 수 있게 되었다.
	ENDIF

	IF Exp:L_I:펠라경험 >= 900
		CALL PRINT_LABEL("펠라치오 Ｓ 　　　　")
		PRINTFORML 		그녀의 펠라로 사정을 버티는 일은 없다.
	ELSEIF Exp:L_I:펠라경험 >= 700
		CALL PRINT_LABEL("펠라치오 Ａ 　　　　")
		PRINTFORML 		이제는 성기와 같은 쾌락을 안겨주는 입이다.
	ELSEIF Exp:L_I:펠라경험 >= 500
		CALL PRINT_LABEL("펠라치오 Ｂ 　　　　")
		PRINTFORML 		사정을 촉진하는 움직임이나 테크닉도 쓸 수 있다.
	ELSEIF Exp:L_I:펠라경험 >= 300
		CALL PRINT_LABEL("펠라치오 Ｃ 　　　　")
		PRINTFORML 		여유가 생겨 혀도 쓸 수 있게 되었다.
	ELSEIF Exp:L_I:펠라경험 >= 100
		CALL PRINT_LABEL("펠라치오 Ｄ 　　　　")
		PRINTFORML 		사정 받고 정액을 입에 머금는데에도 익숙해졌다.
	ENDIF

	IF Exp:L_I:성교경험 >= 900
		CALL PRINT_LABEL("성교기술 Ｓ 　　　　")
		PRINTFORML 		음탕한 허리로 사정을 촉진시키고, 정액을 전부 쥐어짜낼 때까지 놓아주지 않을 정도로 교미가 너무 좋아.
	ELSEIF Exp:L_I:성교경험 >= 700
		CALL PRINT_LABEL("성교기술 Ａ 　　　　")
		PRINTFORML 		성별 차이를 호의적으로 받아들일수록, 섹스에 열중한다.
	ELSEIF Exp:L_I:성교경험 >= 500
		CALL PRINT_LABEL("성교기술 Ｂ 　　　　")
		PRINTFORML 		질내에 페니스가 없으면 진정되지 않고, 질내는 항상 질척질척하다.
	ELSEIF Exp:L_I:성교경험 >= 300
		CALL PRINT_LABEL("성교기술 Ｃ 　　　　")
		PRINTFORML 		교미에 완전 순응해 페니스의 기능을 파악, 질내에서 그 상태을 알아차릴 수 있다.
	ELSEIF Exp:L_I:성교경험 >= 100
		CALL PRINT_LABEL("성교기술 Ｄ 　　　　")
		PRINTFORML 		수컷과의 교미에도 익숙해지고, 질내에 페니스가 들어오는 이물감에 적응했다.
	ENDIF
	IF TALENT:L_I:강간원망 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("당신의 노리개로… 　")
		PRINTFORML 		%CALLNAME:MASTER%에게 엉망진창 범해지고 싶다고 갈망하고 있다.
	ENDIF
	IF TALENT:L_I:즉각타락 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("운명의 만남 　　　　")
		PRINTFORML 		첫눈에 사랑에 빠지니, 그 속도는 찰나와 같도다.
	ENDIF
	IF TALENT:L_I:얀데레 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("사랑을 삼킨 광기　　")
		PRINTFORML 		%CALLNAME:MASTER%에게 사랑받기 위해서라면, 뭐든지…….
	ENDIF
	IF TALENT:L_I:츤데레 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("데레기　　　　　　　")
		PRINTFORML 		츤츤거리던 그 시절은 어디로 갔는지, 시선을 보내면 수줍어하는 모습이 보인다.
	ENDIF
	IF TALENT:L_I:마돈나 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("사랑하는 마돈나 　　")
		PRINTFORML 		사랑과 성을 알게 되어 더욱 매력적이 되었다. 그리고 안 될 것이라 생각하면서도 사로잡히는 남자도 급증 중.
	ENDIF
	IF TALENT:L_I:고원의꽃 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("연모의 꽃 　　　　　")
		PRINTFORML 		사랑하는 그 모습에 모두가 웅성인다, 누가 그녀의 마음을 사로잡았는지를…….
	ENDIF
	IF TALENT:L_I:인간불신 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("열린 마음 　　　　　")
		PRINTFORML 		타인을 거절하는 자물쇠를 깨고, 고독했던 그녀는 비로소 사람을 사랑한다.
	ENDIF
	IF TALENT:L_I:극상의암컷 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("지고의 애인 　　　　")
		PRINTFORML 		남자라면 누구나 범하고 싶은 그 지체는, %CALLNAME:MASTER%의 색으로 물들여졌다.
	ENDIF
	IF TALENT:L_I:성녀의혈족 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("성흔을 새기는 성녀　")
		PRINTFORML 		성스러운 성녀는 한 사람의 유전자만을 키울 것을 맹세했다.
	ELSEIF TALENT:L_I:성녀의자궁 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("성흔을 새기는 자궁　")
		PRINTFORML 		성스러운 자궁은 한 사람의 유전자만을 키울 것을 맹세했다.
	ENDIF
	IF TALENT:L_I:아이돌 == 1 &&  Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("아이돌의 비밀 　　　")
		PRINTFORML 		팬들은 모른다…… 그 미소는 단 한 사람에게로만 향한다는 것을.
	ENDIF
	IF TALENT:L_I:메이드 == 1 &&  Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("주종을 넘어서 　　　")
		PRINTFORML  	%CALLNAME:MASTER%에게 가장 가까운 여자로서, 누구보다도 사랑받고싶어……
	ENDIF
	IF TALENT:L_I:학생  &&  Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("반 제일의 여자　　　")
		PRINTFORML 		반 남자들이 제일 심취한 상대, 한 발 앞서 어른이 된 싱싱한 암컷.
	ENDIF
	IF TALENT:L_I:남혐 == 1 && TALENT:MASTER:오토코 == 0 &&  Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("…남자는 싫어 　　　")
		PRINTFORML 		그래서 그 사람에게는 몸을 허락했을지도 모르는, 사랑받고 싶은 한 송이 백합.
	ELSEIF TALENT:L_I:남혐 == 1 &&  Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("…남자는 싫어 　　　")
		PRINTFORML 		…하지만 그 남자에겐 몸을 허락하고 만다. 어떻게 해도 관계를 끊을 수 없다…….
	ENDIF
	IF TALENT:L_I:백합공주 == 1 && TALENT:MASTER:오토코 == 0 && (TALENT:L_I:심취 == 1|| TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("결실의 백합공주 　　")
		PRINTFORML 		백합공주는 같은 백합공주 왕자님과 맺어져, 결실을 맺었다.
	ELSEIF TALENT:L_I:백합공주 == 1 && (TALENT:L_I:심취 == 1|| TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("백탁의 백합공주 　　")
		PRINTFORML 		백합이더라도 수분의 정의는 피할 수 없다. 지금은 윤이 나는 한 송이 암꽃.
	ENDIF
	IF TALENT:L_I:동성애 == 1 &&  TALENT:MASTER:오토코 == 0 && Exp:L_I:주인조교경험 >= 100 && (TALENT:L_I:심취 == 1|| TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("레즈，함락　　　　　")
		PRINTFORML 		암컷은 같은 암컷인 동성 간의 사랑을 맺었다.
	ELSEIF TALENT:L_I:동성애 == 1 && Exp:L_I:주인조교경험 >= 100 && (TALENT:L_I:심취 == 1|| TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("레즈，타락　　　　　")
		PRINTFORML 		수컷이라는 존자에 굴복하여 이성간의 성행위, 새끼를 배고 낳는 암컷의 방식을 인정했다.
	ENDIF
	IF CFLAG:L_I:반려の子の出産数 && TALENT:L_I:ＮＴＬ == 0 && TALENT:L_I:백합따기 == 0
		CALL PRINT_LABEL("행복한 가족 　　　　")
		PRINTFORML 		지극히 흔하고 어디에나 있는, 그러나 무엇보다도 소중한 것.
	ENDIF
	IF TALENT:L_I:ＮＴＲ도 == 1
		CALL PRINT_LABEL("ＮＴＲ　　　　　　　")
		PRINTFORML 		%조사처리(CALLNAME:L_I,"는")% %마스터가% 아닌, 누군가에게 빼앗기고 말았다…….
	ENDIF
	IF TALENT:L_I:교제중 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("그이가 모르는 얼굴　")
		PRINTFORML 		그녀가 아양을 떠는 그 열띤 모습은 사랑하는 남자친구도 알 턱이 없다.
	ELSEIF TALENT:L_I:섹프있음 == 1 && Exp:L_I:주인조교경험 >= 100 && ( TALENT:L_I:심취 == 1 || TALENT:L_I:애인 == 1 || TALENT:L_I:정부 == 1)
		CALL PRINT_LABEL("신체 이상의 관계？　")
		PRINTFORML 		다른 섹프들보다 큰 집념, 정말 이 관계는 섹스 프렌드일까?
	ENDIF
	IF TALENT:L_I:ＮＴＬ == 1
		CALL PRINT_LABEL("ＮＴＬ　　　　　　　")
		PRINTFORML 		사랑하는 사람이 있을 %CALLNAME:L_I%의 몸과 마음은, 이미 %CALLNAME:MASTER%의 것이다.
	ENDIF
	IF TALENT:L_I:백합따기 == 1
		CALL PRINT_LABEL("백합꺾기　　　　　　")
		PRINTFORML 		사랑하는 여성이 있을 %CALLNAME:L_I%의 몸과 마음은, %CALLNAME:MASTER%의 것이다.
	ENDIF
	IF CFLAG:L_I:あなたの子の出産数 >= 10
		CALL PRINT_LABEL("현모양처　　　　　　")
		PRINTFORML 		%CALLNAME:MASTER%의 육욕과 정자를 수없이 받아들여 아이를 만든 인재.
	ELSEIF CFLAG:L_I:あなたの子の出産数 >= 5
		CALL PRINT_LABEL("당신 전용 좆집　　　")
		PRINTFORML 		%마스터와% 여러 번 관계를 맺으며 그 피를 이을 아이를 많이 만든 우수한 암컷.
	ELSEIF CFLAG:L_I:あなたの子の出産数
		CALL PRINT_LABEL("출산 완료 　　　　　")
		PRINTFORML 		%CALLNAME:MASTER%의 정자로 수정하여 수태, 그리고 %CALLNAME:MASTER%의 피를 계승한 아이를 출산했다.
	ENDIF
	IF TALENT:L_I:탁란완료 == 1
		CALL PRINT_LABEL("탁란됨　　　　　　　")
		PRINTFORML 		남편과 함께 키우는 아이는 그이의 피가 아니라, %CALLNAME:MASTER%의 피를 이은 사랑스러운 아이다.
	ELSEIF TALENT:L_I:탁란 == 1
		CALL PRINT_LABEL("탁란　　　　　　　　")
		PRINTFORML 		결혼한 상대가 아닌, %마스터와%의 아이를 배고 있다.
	ENDIF
	IF TALENT:L_I:피탁란완료 == 1
		CALL PRINT_LABEL("피탁란됨　　　　　　")
		PRINTFORML 		사랑해야 할 %마스터가% 아닌 아이를 스스로 원해서 낳았다…….
	ELSEIF TALENT:L_I:피탁란 == 1
		CALL PRINT_LABEL("피탁란　　　　　　　")
		PRINTFORML 		사랑해야 할 %CALLNAME:MASTER%의 것이 아닌 종자로 임신하고 있다…….
	ENDIF
	IF TALENT:L_I:바람 == 3
		CALL PRINT_LABEL("바람기　　　　　　　")
		PRINTFORML 		남편 외에도 「남편의 상사」와 은밀히 육체관계를 맺고 있다.
	ELSEIF TALENT:L_I:바람 == 2
		CALL PRINT_LABEL("바람기　　　　　　　")
		PRINTFORML 		남편 외에도 「소꿉친구」와 은밀히 육체관계를 맺고 있다.
	ELSEIF TALENT:L_I:바람 == 1
		CALL PRINT_LABEL("바람기　　　　　　　")
		PRINTFORML 		남편 외에도 「전 남자친구」와 은밀히 육체관계를 맺고 있다.
	ENDIF
	IF TALENT:L_I:성녀의자궁 && CFLAG:L_I:수정확률 <= 50
		CALL PRINT_LABEL("황폐한 성역 　　　　")
		PRINTFORML 		실험으로 인해 오염된 자궁은 아이를 품는 성역이 아니게 되었다…….
	ENDIF
	IF CFLAG:L_I:交配実験_出産回数 >= 45
		CALL PRINT_LABEL("인외의 사랑스런 아이")
		PRINTFORML 		교배실험 끝에, %조사처리(CALLNAME:L_I,"는")% 인간이 아닌 아이를 수없이 낳았다…….
	ENDIF
	IF CFLAG:L_I:犬_出産回数 >= 10
		CALL PRINT_LABEL("인간과 개의 사이에서")
		PRINTFORML 		사람과 개를 닮은 금지된 아이를 수없이 낳았다…….
		;人に似ず犬に非ず
	ENDIF



	IF CFLAG:L_I:子宮精液貯蔵量合計 > 0
		CALL PRINT_LABEL(@"%마스터와%의 아이 만들기")
		IF CFLAG:L_I:子宮精液貯蔵量 > 0
			;<!>謎の一行のせいで、残留がなくても表示されるようになってた...
		PRINTFORM 	
		PRINTFORM  %CALLNAME:MASTER%의 질내사정으로 {CFLAG:L_I:子宮精液貯蔵量/500}ml의 정액이 자궁에 남아있으며, 
		ENDIF
		PRINTFORML 	현재까지 {CFLAG:L_I:子宮精液貯蔵量合計/500}ml의 정액을 자궁에 보관하고 있다.
	ENDIF
ENDIF

PRINTFORML ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――
RESETCOLOR
;-------------------------------------------------------------------------------
;   STATUS系(詳細)
;-------------------------------------------------------------------------------

;=================================================
;   sub INFO_STATUS_HEADER
;=================================================
;   表示:STATUS(ヘッダ)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_HEADER(ARG)
;表示:ページヘッダ
CALL C_LINE
PRINTFORM NO.%TOSTR(CFLAG:ARG:개별번호) , 3, LEFT%:
PRINTFORM %CALLNAME:ARG,24,LEFT%
;체력
CALL VAR_BAR_BASE(ARG, 0, 1)
SIF ARG != MASTER
	CALL MNG_INFO_BASIC_DATA(ARG)
;- 情報:위험일
CALL INFO_위험일(ARG)
PRINTL

;=================================================
;   sub INFO_STATUS_MARK
;=================================================
;   表示:STATUS(刻印)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_MARK(ARG)
#DIM L_CNT

L_CNT=0

VARSET LOCAL
FOR LOCAL,0,VARSIZE("MARK")

	LOCALS = %MARKNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = MARK:ARG:LOCAL

	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM %LOCALS , 10%
	PRINTFORM :LV{LOCAL:1, 2 ,LEFT} 
	BAR LOCAL:1, 3, 3
	PRINT  
	RESETCOLOR

	L_CNT += 1
	IF L_CNT > 3
		PRINTL
		L_CNT = 0
	ENDIF
NEXT

SIF !LINEISEMPTY()
	PRINTL


;=================================================
;   sub INFO_STATUS_TALENT
;=================================================
;   表示:STATUS(素質)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				CSV優先
;-------------------------------------------------
@INFO_STATUS_TALENT(ARG, ARG:1=0)
#DIM L_CNT
#DIM L_COLOR
#DIM L_I
#DIMS L_STR
#DIM DYNAMIC L_LABELCOLOR
#DIM CONST L_LAYOUT_OFFSET = 14

L_CNT=0
L_COLOR = GETCOLOR()


FOR L_I,0,VARSIZE("TALENT")
	L_LABELCOLOR = -1

	SELECTCASE L_I
		CASE 0
			CALL PRINT_LABEL("ABL 소질")
			PRINTFORM %"：",L_LAYOUT_OFFSET-RESULT%
		CASE 50
			PRINTL
			CALL PRINT_LABEL("체질소질")
			PRINTFORM %"：",L_LAYOUT_OFFSET-RESULT%
			L_CNT = 0
		CASE 100
			PRINTL
			CALL PRINT_LABEL("개인소질")
			PRINTFORM %"：",L_LAYOUT_OFFSET-RESULT%
			L_CNT = 0
		CASE 200
			PRINTL
			CALL PRINT_LABEL("대인소질")
			PRINTFORM %"：",L_LAYOUT_OFFSET-RESULT%
			L_CNT = 0
		CASE 3000
			PRINTL
			CALL PRINT_LABEL("기밀소질")
			PRINTFORM %"：",L_LAYOUT_OFFSET-RESULT%
			L_CNT = 0
	ENDSELECT

	;- DB:TALENT名称
	L_STR = %DB_TALENT_NAME(ARG, TALENTNAME:L_I, ARG:1)%

	;-特殊変換
	SELECTCASE TALENTNAME:L_I
		CASE "시조"
			IF CFLAG:(ARG):차세대창조자손수
				L_STR = 시조:창조수{MIN(CFLAG:(ARG):차세대창조자손수, 999), 3, RIGHT}
			ELSE
				L_STR = 
			ENDIF
		CASE "머리색","머리모양","눈","눈동자색","唇","체형","유두","음모","페니스사이즈","모친"
		L_STR = 
	ENDSELECT


	;-特殊変換
	SELECTCASE TALENTNAME:L_I
		CASE "남자"
			IF VAR_SEX(ARG, ARG:1)
				L_STR = 여자
			ELSE
				L_STR = 남자
			ENDIF
	ENDSELECT

	SIF STRLENS(L_STR) <= 0
		CONTINUE

	IF L_CNT > 95;75
		PRINTL
		PRINTFORM %" ",L_LAYOUT_OFFSET%
		L_CNT = 0
	ENDIF

	L_CNT += STRLENS(L_STR)

	;-着色
	SELECTCASE L_STR
		CASE "처녀", "오토코노코"
			L_LABELCOLOR = 0xFF66CC
		CASE "동정", "남성주민"
			L_LABELCOLOR = COLOR("群青")
		CASE "비전투원","인외","저항","흠집","인간불신","추남","열등유전자","초식계","희박정액","담백","실좆"
			L_LABELCOLOR = COLOR("群青")
		CASE "배란유도제", "임신", "수정" ,"심취" ,"애인" ,"정부" ,"금사빠" ,"즉각함락","애석"
			L_LABELCOLOR = COLOR("ピンク")
		CASE "ＮＴＲ", "악마", "반마", "조마", "약점", "ＮＴＬ","탁란","탁란완","피탁란","피탁란완","외도"
			L_LABELCOLOR = COLOR("紫")
		CASE "비합의", "범해짐","부재","기억처리완료","강간원망","이레귤러","자원봉사","강화주민"
			L_LABELCOLOR = COLOR_FROMRGB(250,0,0)
		CASE "푸른혈족"
			L_LABELCOLOR = COLOR_FROMRGB(50,50,250)
		CASE "합의"
			L_LABELCOLOR = COLOR_FROMRGB(250,250,100)
		CASE "성녀의자궁", "극상의암컷" ,"마돈나","모유체질","성녀의혈족","자애"
			L_LABELCOLOR = COLOR_FROMRGB(250,250,200)
		CASE "학생", "대학생", "고등학생", "중학생", "초등학생"
			IF (TALENT:(ARG:0):아이돌 || TALENT:(ARG:0):창녀)
				L_LABELCOLOR = COLOR_FROMRGB(250,100,50)
			ELSE
				L_LABELCOLOR = COLOR_FROMRGB(50,250,50)
			ENDIF
		CASE "아이돌", "사회인", "창녀", "치안유지국", "메이드", "성직자","AI전속피험자"
			L_LABELCOLOR = COLOR_FROMRGB(50,250,50)
		CASE "기밀Lv1개체", "기밀Lv2개체", "기밀Lv3개체"
			L_LABELCOLOR = COLOR_FROMRGB(50,250,50)
		CASE "기밀Lv4개체", "기밀Lv5개체", "기밀Lv6개체"
			L_LABELCOLOR = COLOR_FROMRGB(250,250,50)
		CASE "기밀Lv7개체", "기밀Lv8개체", "기밀Lv9개체"
			L_LABELCOLOR = COLOR_FROMRGB(250,50,50)
		CASE "《리비도그로우업》", "《로스트머메이드》", "《브라콘》", "《대성여우천신》", "《대성여우공신》", "《아웃이터》"
			L_LABELCOLOR = COLOR_FROMRGB(150,0,0)
	ENDSELECT

	CALL PRINT_LABEL(L_STR,,L_LABELCOLOR,,,,,,,,1)

	;- 外枠分を加算
	L_CNT += 2

NEXT

SIF !LINEISEMPTY()
	PRINTL


;=================================================
;   sub INFO_STATUS_ABL
;=================================================
;   表示:STATUS(能力)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				空データ除外
;-------------------------------------------------
@INFO_STATUS_ABL(ARG, ARG:1=0)
#DIM L_CNT
#DIM L_COLOR

L_CNT=0
L_COLOR = GETCOLOR()

VARSET LOCAL
FOR LOCAL,0,VARSIZE("ABL")

	LOCALS = %ABLNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = ABL:ARG:LOCAL

	SIF ARG:1 && !LOCAL:1
		CONTINUE

	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM %LOCALS , 10%
	PRINTFORM :LV{LOCAL:1, 2, LEFT} 
	BAR (LOCAL:1/3), 3, 3
	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR L_COLOR
	PRINT  

	L_CNT += 1
	IF L_CNT > 3
		PRINTL
		L_CNT = 0
	ENDIF
NEXT

SIF !LINEISEMPTY()
	PRINTL

;=================================================
;   sub INFO_STATUS_EXP
;=================================================
;   表示:STATUS(経験)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				空データ除外
;-------------------------------------------------
@INFO_STATUS_EXP(ARG, ARG:1=0)
#DIM L_CNT
#DIM L_COLOR
#DIM CONST EXP_STATUS_SPAN = 29

L_CNT=0
L_COLOR = GETCOLOR()
VARSET LOCAL

FOR LOCAL,0,VARSIZE("EXP")

	;ヘッダー
	SELECTCASE LOCAL
		CASE 0
			PRINTFORM %"■======= 일반경험 =======■", EXP_STATUS_SPAN , LEFT% 
			L_CNT = 1
		CASE 50
			SIF !LINEISEMPTY()
				PRINTL
			PRINTFORM %"■======= 이상경험 =======■", EXP_STATUS_SPAN , LEFT% 
			L_CNT = 1
		CASE 80
			SIF !LINEISEMPTY()
				PRINTL
			PRINTFORM %"■======= 기타경험 =======■", EXP_STATUS_SPAN , LEFT% 
			L_CNT = 1
	ENDSELECT

	LOCALS = %EXPNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = EXP:ARG:LOCAL

	SIF ARG:1 && !LOCAL:1
		CONTINUE

	;CHK:数量(必須表示のみ素通り)
	SELECTCASE LOCAL
		CASE 0,50,60
		CASEELSE
;			SIF LOCAL:1 <= 0
;				CONTINUE
	ENDSELECT

	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM %@"%LOCALS , 16%:LV{GETEXPLV(LOCAL:1,9), 2, LEFT}(%TOSTR(LOCAL:1) , 4%회)", EXP_STATUS_SPAN , LEFT%
	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR L_COLOR
	PRINT  

	L_CNT += 1
	IF L_CNT > 2
		PRINTL
		L_CNT = 0
	ENDIF

NEXT

SIF !LINEISEMPTY()
	PRINTL

;=================================================
;   sub INFO_STATUS_JUEL
;=================================================
;   表示:STATUS(珠)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_JUEL(ARG)
#DIM L_HIT
#DIM L_CNT
#DIM L_COLOR

L_HIT=0
L_CNT=0
VARSET LOCAL
L_COLOR = GETCOLOR()

FOR LOCAL,0,VARSIZE("ABL")

	LOCALS = %PALAMNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = JUEL:ARG:LOCAL

	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	LOCALS += "의 구슬"
	PRINTFORM %LOCALS , 14%:
	PRINTFORM {LOCAL:1 , 5}
	SIF LOCAL:1 <= 0 && FLAG:STATUS伏字モード
		SETCOLOR L_COLOR
	PRINT  
	L_HIT = 1

	L_CNT += 1
	IF L_CNT > 3
		PRINTL
		L_CNT = 0
		L_HIT = 0
	ENDIF
NEXT

SIF L_HIT
	PRINTL

;=================================================
;   sub INFO_STATUS_GOTJUEL
;=================================================
;   表示:STATUS(取得珠)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_GOTJUEL(ARG)
#DIM L_HIT
#DIM L_CNT
#DIM L_COLOR

L_HIT=0
L_CNT=0
VARSET LOCAL
L_COLOR = GETCOLOR()

FOR LOCAL,0,VARSIZE("ABL")

	LOCALS = %PALAMNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = GOTJUEL:ARG:LOCAL
	LOCAL:2 = JUEL:ARG:LOCAL

	SIF LOCAL:1 <= 0
		CONTINUE
	LOCALS += "의 구슬"
	PRINTFORM %LOCALS , 10%:
	PRINTFORM {LOCAL:2 , 5}(+{LOCAL:1 , 5})
	PRINT  
	L_HIT = 1

	L_CNT += 1
	IF L_CNT > 3
		PRINTL
		L_CNT = 0
		L_HIT = 0
	ENDIF
NEXT
SIF L_HIT
	PRINTL
PRINTL

;=================================================
;   sub INFO_STATUS_INTRODUCTION
;=================================================
;   表示:STATUS(紹介)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				CSV優先
;-------------------------------------------------
@INFO_STATUS_INTRODUCTION(ARG, ARG:1=0)
#DIM L_TMP

IF ARG:1
	L_TMP = ARG
ELSE
	L_TMP = NO:ARG
ENDIF

REPEAT 4
	PRINTSL CSVCSTR(L_TMP, GETNUM(CSTR, "キャラ紹介" + TOSTR(COUNT)), 0)
REND

;=================================================
;   sub INFO_STATUS_OTHER
;=================================================
;   表示:STATUS(その他)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_STATUS_OTHER(ARG)
#DIM L_CNT
#DIM L_TMP
#DIMS L_STR
#DIM L_I

L_I = ARG

;- 主人の場合
IF ARG == MASTER
	PRINTFORML 함락 인원:{FLAG:陥落人数}명
	PRINTFORML 
	PRINTFORML 
ELSE
	;-----------------------------
	;- NTR
	;-----------------------------
	L_CNT=0
	L_TMP=CFLAG:ARG:ＮＴＲ再陥落回数
	IF L_TMP
		PRINTFORML ＮＴＲ재함락횟수：{L_TMP,2}회
		L_CNT ++
	ENDIF
	L_TMP=CFLAG:ARG:ＮＴＲ호감도
	IF L_TMP
		PRINTFORML ＮＴＲ호감도(%CSTR:ARG:ＮＴＲキャラ名%)：{L_TMP,3}/999
		L_CNT ++
	ENDIF
	WHILE 1
		SIF L_CNT >= 2
			BREAK
		PRINTL
		L_CNT ++
	WEND

ENDIF
TRYCALL 첫체험기록(ARG)


PRINTFORML ――――――――――――――――――――――――%CALLNAME:L_I%의 특수소질――――――――――――――――――――――――
SIF TALENT:L_I:생환자 == 1
	PRINTFORML 기밀 Lv 1:【생환자　　　】		「외적」의 위협을 받으면서, 도시 밖에서 살아 돌아온 자.
SIF TALENT:L_I:실험체 == 1
	PRINTFORML 기밀 Lv 2:【실험체　　　】		어떠한 것에 실험당한 자, 요관찰대상이기도 하다.
SIF TALENT:L_I:푸른혈족 == 1
	PRINTFORML 기밀 Lv 1:【푸른 혈족 　】		영주•왕족 등의 피를 이은 고귀한 출생으로, 현대에는 매우 드물다.
SIF TALENT:L_I:이계인 == 1
	PRINTFORML 기밀 Lv 1:【이방인　　　】		이곳이 아닌 곳에서 온, 운 없는 자들.
SIF TALENT:L_I:구세대인 == 1
	PRINTFORML 기밀 Lv 2:【구세대인　　】		「외적」에게 멸망당하기 전 살아 있던 사람, 구문명의 일에 매우 박식.
SIF TALENT:L_I:前성노예 == 1
	PRINTFORML 기밀 Lv 1:【성노예 출신 】		이전에는 성노예였던 자, 적어도 여기서는 아껴주자.
SIF TALENT:L_I:인권손괴 == 1
	PRINTFORML 기밀 Lv 2:【인권손괴　　】		인권이 현저히 손상되어 있는 자, 당신의 보호가 없으면 가축 이하의 대우.
SIF TALENT:L_I:돌연변이 == 1
	PRINTFORML 기밀 Lv 3:【돌연변이　　】		유익한 변이를 이룬 개체, 딱히 인외가 됐다거나 한건 아니다.
SIF TALENT:L_I:선조회귀 == 1
	PRINTFORML 기밀 Lv 3:【선조답례　　】		과거 세대의 특이성을 발현한 자, 외모도 다소 닮았다.
SIF TALENT:L_I:기억처리완료 == 1
	PRINTFORML 기밀 Lv 2:【기억처리　　】		아무것도 떠올리지 못한다.
IF TALENT:L_I:명가 == 1 && CFLAG:L_I:주민_세대 >=30
	PRINTFORML 기밀 Lv 0:【월경　　　　】		주민이라면 모르는 이가 없는 귀족 중의 귀족. 「플레이어」의 손발이 되어 도시에 적대하는 자를 섬멸한다.
ELSEIF TALENT:L_I:명가 == 1 && CFLAG:L_I:주민_세대 >= 20
	PRINTFORML 기밀 Lv 0:【호족　　　　】		다른 명문가보가 오래 「플레이어」를 섬기고 있는 일족. 계승으로 얻은 힘을 주인을 위해 휘두른다.
ELSEIF TALENT:L_I:명가 == 1 
	PRINTFORML 기밀 Lv 0:【명문가　　　】		수 대 째 「플레이어」를 섬기는 일족, 거주민들의 동경 대상이기도 하다.
ENDIF
SIF TALENT:L_I:극상의암컷 == 1
	PRINTFORML 기밀 Lv 1:【극상의암컷 】		남성에게는 성적으로, 여성에게는 질투의 시선을 받을 만한 외모를 하고 있다.
SIF TALENT:L_I:마돈나 == 1
	PRINTFORML 기밀 Lv 1:【마돈나　　　】		남자의 마음을 뺏어간 일이 부지기수, 그 몸과 마음이 노려지고 있다.
SIF TALENT:L_I:백합공주 == 1
	PRINTFORML 기밀 Lv 1:【백합공주　　】		동성을 사랑하고 사랑해, 이성에 거의 관심이 없다.
SIF TALENT:L_I:AI전속피험자 == 1
	PRINTFORML 기밀 Lv 2:【AI전속피험자】		ＡＩ박사의 「단골」인거야!, 우선적으로 실험해주는거야!
SIF TALENT:L_I:모유체질 == 1
	PRINTFORML 기밀 Lv 2:【모유체질　　】		임신 여부에 관계없이 모유가 나오는 특이체질, 자주 나오고 달콤하며 농후하다.
SIF TALENT:L_I:강화거주자 == 1
	PRINTFORML 기밀 Lv 6:【강화주민　　】		무엇을 해줄까 인거야—!!!
SIF TALENT:L_I:성녀의자궁 == 1
	PRINTFORML 기밀 Lv 4:【성녀의 자궁 】		어떤 정자라도 받아들여, 우수한 아이를 키우는 자궁을 갖는다.
SIF TALENT:L_I:성녀의혈족 == 1
	PRINTFORML 중요 기밀:【성녀의 혈족 】		최초기의 도시를 이끈 위대한 대성녀, 그 피를 짙게 물려받은 자. 교회에서 신앙 대상이 될 정도의 존귀한 피.
SIF TALENT:L_I:「시스터」 == 1
	PRINTFORML 기밀 Lv 5:【「시스터」　】		당신의 여동생이라는 하는 설정이 기입된 개체.
SIF TALENT:L_I:「예거」 == 1
	PRINTFORML 기밀 Lv 5:【「예거」　　】		수많은「외적」을 멸한 자에게 「시티」로부터 보내지는 칭호.
SIF TALENT:L_I:「마더」 == 1
	PRINTFORML 기밀 Lv 5:【「마더」　　】		많은 아이를 낳아 도시에 공헌한 자에게 「시티」로부터 보내지는 칭호.
SIF TALENT:L_I:「뮤추얼」 == 1
	PRINTFORML 중요 기밀:【「뮤츄얼」　】		「외적」의 숙주이며 공생하고 있다. 어째서 공생하고 있는지 해명이 되어 있지 않다.
SIF CFLAG:L_I:주민_종족 == 13
	PRINTFORML 중요 기밀:【니어　　　　】		「외적」해명의 일환으로 인공적으로 만들어진 「외적」의 피를 받은 종족.
PRINTFORML ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――

;=================================================
;   sub INFO_INTRODUCTION
;=================================================
;   表示:STATUS画面(詳細情報)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_PERSONAL(ARG)
#DIM L_I
#DIM L_TMP
#DIMS L_KEY
#DIM I_LOOP

I_LOOP = 1
L_TMP = NO:ARG
L_I = ARG:0


DRAWLINE
PRINTFORML ■------------------------------------ 상세정보 ------------------------------------■

PRINTFORML ◇----------------주민정보----------------◇
CALL PRINT_STATUS(L_I, "소지금")
CALL PRINT_STATUS(L_I, "수입", , , ,1000000)
CALL PRINT_STATUS(L_I, "지출", , , ,1000000)
SIF Talent:L_I:아이돌 != 0
	CALL PRINT_LABEL("팬 수",,(!CFLAG:L_I:팬수の数 && FLAG:STATUS伏字モード ? COLOR("伏字") # -1),TOSTR(CFLAG:L_I:팬수の数))
PRINTL 
CALL PRINT_STATUS(L_I, "인기"  , , , , 10000)
CALL PRINT_STATUS(L_I, "모럴", , , (L_I==MASTER ? "伏字"#""), 500 , , -500)
CALL PRINT_STATUS(L_I, "카르마", , , (L_I==MASTER ? "伏字"#""), 500 , , -500)
CALL PRINT_STATUS(L_I, "품성"  , , , (L_I==MASTER ? "伏字"#""), 500 , , -500)
PRINTL
CALL PRINT_STATUS(L_I, "육체데미지")
CALL PRINT_STATUS(L_I, "정신데미지")
PRINTL

IF TALENT:L_I:오토코 == 1 || TALENT:L_I:후타나리
PRINTFORM 【페니스사이즈:
	IF CFLAG:L_I:ペニスサイズ == 3
			PRINTFORM 대물
	ELSEIF CFLAG:L_I:ペニスサイズ == 2
			PRINTFORM 단소포경
	ELSEIF CFLAG:L_I:ペニスサイズ == 1
			PRINTFORM 포경
	ELSE
			PRINTFORM 평범
	ENDIF
PRINTFORML 】
ENDIF

PRINTFORML ◇----------------정신정보----------------◇
CALL PRINT_STATUS(L_I, "충성도")
CALL PRINT_STATUS(L_I, "애정도")
CALL PRINT_STATUS(L_I, "음란도")
PRINTL


PRINTFORML ■------------------------------------ 고민목록 ------------------------------------■
PRINTFORML ◇----------------- 큰 고민 -----------------◇
REPEAT 5
	IF CFlag:L_I:(4000+I_LOOP) != 0
		IF CFlag:L_I:(4100+I_LOOP) >= 100
			IF CFlag:L_I:(4000+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("금전문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("연애문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("가정문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_생활")
				CALL PRINT_LABEL("생활문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("범죄문제")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("성적문제")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(미판명)
			CALL PRINT_LABEL("해석",,,TOSTR(CFlag:L_I:(4100+I_LOOP)),3,,"/","100")
		ENDIF
		PRINTFORM 난도:{CFlag:L_I:(4300+I_LOOP),2}　
		PRINTFORML 달성률:({CFlag:L_I:(4200+I_LOOP),3}/100)
		RESETCOLOR
	ENDIF
	I_LOOP += 1
REND
PRINTFORML ◇---------------- 작은 고민 ----------------◇
I_LOOP = 1
REPEAT 5
	IF CFlag:L_I:(4010+I_LOOP) != 0
		IF CFlag:L_I:(4110+I_LOOP) >= 100
			IF CFlag:L_I:(4010+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("금전문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("연애문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("가정문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_생활")
				CALL PRINT_LABEL("생활문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("범죄문제")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("성적문제")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(미판명)
			CALL PRINT_LABEL("해석",,,TOSTR(CFlag:L_I:(4100+I_LOOP)),3,,"/","100")
		ENDIF
		PRINTFORM 난도:{CFlag:L_I:(4310+I_LOOP),2}　
		PRINTFORML 달성률:({CFlag:L_I:(4210+I_LOOP),3}/100)
		RESETCOLOR
	ENDIF
	I_LOOP += 1
REND
PRINTFORML ■------------------------------------ 신체정보 ------------------------------------■
SIF TALENT:L_I:오토코 == 0 
	CALL C_PICTURE_S(ARG:0)

SIF !LINEISEMPTY()
	PRINTL


;=================================================
;   表示:STATUS画面(隠し性情報)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_SEX(ARG)
#DIM L_I
#DIM P_S
#DIMS LSTR
L_I = ARG
P_S = 0
IF TALENT:L_I:오토코 == 1 || TALENT:L_I:남성거주자 == 1
	PRINTFORML 
	CALL PRINT_STATUS(L_I, "남체레벨")
	SIF !CFlag:L_I:男体경험치 && !CFlag:L_I:남체레벨 && Flag:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORML ({CFlag:L_I:男体경험치}/{CFlag:L_I:남체레벨*2})
	RESETCOLOR
	CALL PRINT_STATUS(L_I, "정자레벨")
	SIF !CFlag:L_I:精子경험치 && !CFlag:L_I:정자레벨 && Flag:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORML ({CFlag:L_I:精子경험치}/{((CFlag:L_I:정자레벨)*5)+CFlag:L_I:정자레벨})
	RESETCOLOR
	PRINTFORML 
ELSEIF TALENT:L_I:오토코 == 0 
	CALL C_PICTURE_S_S(ARG)
	PRINTFORML ■----------------%조사처리(CALLNAME:L_I,"가")% 지닌 무자각 소질 ----------------■
	IF TALENT:L_I:오토코 == 0 
		IF  MASTER == L_I
		ELSE
			IF CFlag:L_I:무자각한암컷 >= 80
				IF !RAND:5
					PRINTFORML 「……%UNICODE(0x2661)%」
				ELSEIF !RAND:4
					PRINTFORML 「%CSTR:L_I:二人称%의 아이는、아주 우수한 아이가 될 것 같아 %CSTR:L_I:어미同意求む%」
				ELSEIF !RAND:3
					PRINTFORML 「아이라던가 흥미 있어 %CSTR:L_I:어미대응%、%CSTR:L_I:二人称%は？」
				ELSEIF !RAND:2
					PRINTFORML (%CSTR:L_I:一人称%…어느샌가 몸을 끌어당겨 온다……)
				ELSE
					PRINTFORML 「옆에 와도 괜찮아 %CSTR:L_I:어미問う%？、이쪽이 듣기 편하고 좋아 %CSTR:L_I:어미繋ぎ%%CSTR:L_I:어미です%」
				ENDIF
			ELSEIF CFlag:L_I:무자각한암컷 >= 60
				IF !RAND:5
					PRINTFORML 「오늘도 이야기 많이 들어줘서 좋아%CSTR:L_I:어미問う%？」
				ELSEIF !RAND:4
					PRINTFORML 「%CSTR:L_I:二人称%는 여자한테 흥미있어%CSTR:L_I:어미聞く%？」
				ELSEIF !RAND:3
					PRINTFORML 「……♪」
				ELSEIF !RAND:2
					PRINTFORML 「%CSTR:L_I:어미何？%%UNICODE(0x2661)%」
				ELSE
					PRINTFORML 「몸이 뜨거워…%CSTR:L_I:어미逆説%…기분 좋아%CSTR:L_I:어미応答2%…」
				ENDIF
			ELSEIF CFlag:L_I:무자각한암컷 >= 40
				IF !RAND:5
					PRINTFORML 「%CSTR:L_I:二人称%의 이야기는 언제나 재미있네%CSTR:L_I:어미応答2%」
				ELSEIF !RAND:4
					PRINTFORML 「…읏」
				ELSEIF !RAND:3
					PRINTFORML 「…언제나%CSTR:L_I:어미感謝%」
				ELSEIF !RAND:2
					PRINTFORML 「몸이 달아올라…」
				ELSE
					PRINTFORML 「%CSTR:L_I:二人称%이라면 만져도 괜찮아%CSTR:L_I:어미許可%」
				ENDIF
			ELSEIF CFlag:L_I:무자각한암컷 >= 20
				IF !RAND:5
					PRINTFORML 「%CSTR:L_I:二人称%의 이야기를 들을 수 있어서 다행이야%CSTR:L_I:어미ます%」
				ELSEIF !RAND:4
					PRINTFORML 「좀 더 이야기 들어줘도 돼%CSTR:L_I:어미問う%？」
				ELSEIF !RAND:3
					PRINTFORML 「%CSTR:L_I:二人称%는 꽤 볼만하다고 생각해%CSTR:L_I:어미繋ぎ%%CSTR:L_I:어미です%」
				ELSEIF !RAND:2
					PRINTFORML 「더 가까이 와도 돼%CSTR:L_I:어미問う%？」
				ELSE
					PRINTFORML 「조금 덥네%CSTR:L_I:어미応答2%、왜일까%CSTR:L_I:어미疑問%…？」
				ENDIF
			ELSE 
				IF !RAND:5
					PRINTFORML 「%CSTR:L_I:一人称%의 얼굴에 뭔가 묻었어%CSTR:L_I:어미問う%？」
				ELSEIF !RAND:4
					PRINTFORML 「……？」
				ELSEIF !RAND:3
					PRINTFORML 「………왜일까%CSTR:L_I:어미問う4%？」
				ELSEIF !RAND:2
					PRINTFORML 「특별히 문제는 없는데%CSTR:L_I:어미許可%」
				ELSE
					PRINTFORML 「…？、%CSTR:L_I:어미何？%」
				ENDIF
			ENDIF
		ENDIF






		IF CFlag:L_I:페로몬 >= 1
		PRINTFORM ・【페로몬　　　　　】남성
			IF CFlag:L_I:페로몬 >= 90
				PRINTFORM 에 대한 영향력이 막강하여, 당신의 보호 없이는 제대로 생활하기 힘들 정도의 엄청난 페로몬을 뿜어내고 있습니다
				CALL CP_HEARTB_PINK, 10
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 80
				PRINTFORM 들의 이성을 압도해, 덮쳐서 임신 시켜버릴 정도의 페로몬을 발산하고 있습니다
				CALL CP_HEARTB_PINK(9)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 70
				PRINTFORM 들이 그녀들의 미색에 의해 발기를 억제할 수 없을 정도의 페로몬을 발산하고 있습니다
				CALL CP_HEARTB_PINK(8)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 60
				PRINTFORM 들이 한 번 본 것만으로 아이를 낳게 하고 싶을 정도의 페로몬을 발산하고 있습니다
				CALL CP_HEARTB_PINK(7)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 50
				PRINTFORM 들이 '어떻게든 관계를 맺을 수 없을까' 라며 매료되는 페로몬을 풍기고 있습니다
				CALL CP_HEARTB_PINK(6)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 40
				PRINTFORM 들로부터 성적인 시선을 피할 수 없을 정도의 페로몬을 풍기고 있습니다
				CALL CP_HEARTB_PINK(5)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 30
				PRINTFORM 이 아주 좋아할 만한 페로몬을 풍기고 있습니다
				CALL CP_HEARTB_PINK(4)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 20
				PRINTFORM 이 매력적인 여성이라고 생각되는 페로몬을 풍기고 있습니다
				CALL CP_HEARTB_PINK(3)
				PRINTL
			ELSEIF CFlag:L_I:페로몬 >= 10
				PRINTFORM 을 약간 흥분시키는 페로몬을 내고 있습니다
				CALL CP_HEARTB_PINK(2)
				PRINTL
			ELSE
				PRINTFORM 으로부터 관심을 끌 만한 페로몬을 내고 있습니다
				CALL CP_HEARTB_PINK()
				PRINTL
			ENDIF
		ENDIF

		PRINTFORM ・【암컷의 본능 　　】
		IF CFlag:L_I:암컷의본능 >= 90
			PRINTFORM 임신하지 않으면 항상 정서 불안정일 정도로 비정상적인 생식 본능입니다
			CALL CP_HEARTB_PINK(10)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 80
			PRINTFORM 우수한 수컷이 있으면 무의식적으로 유혹해 생식행위에 이를 정도의 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK(9)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 70
			PRINTFORM 피임을 하면 역으로 불쾌해 할 정도의 생식 본능이 있습니다
			CALL CP_HEARTB_PINK(8)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 60
			PRINTFORM 우수한 수컷이 있다면 아이를 낳고 싶어할 정도의 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK(7)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 50
			PRINTFORM 생식 본능이라 하기엔 약간 이상한 욕구도입니다
			CALL CP_HEARTB_PINK(6)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 40
			PRINTFORM 수컷이 가까이에 있으면 자궁이 쑤실 정도의 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK(5)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 30
			PRINTFORM 생식에 대하여 상당히 긍정적인 의사가 있습니다
			CALL CP_HEARTB_PINK(4)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 20
			PRINTFORM 암컷으로서는 다소 강한 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK(3)
			PRINTL
		ELSEIF CFlag:L_I:암컷의본능 >= 10
			PRINTFORM 암컷으로서 양호한 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK(2)
			PRINTL
		ELSE
			PRINTFORM 암컷으로서 일반적인 생식 본능을 가지고 있습니다
			CALL CP_HEARTB_PINK()
			PRINTL
		ENDIF

		PRINTFORM ・【성에 대한 흥미　】
		IF CFlag:L_I:성적인흥미 >= 90
			PRINTFORM 성경험을 추구하는 것은 인간으로서 당연하다고 주장하고 있습니다
			CALL CP_HEARTB_PINK(10)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 80
			PRINTFORM 성에 관해서는 잘 알고 있으며, 실제 체험에 빠져들고 싶어 할 정도입니다
			CALL CP_HEARTB_PINK(9)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 70
			PRINTFORM 성에 대한 관심이 많아, 항상 성적 망상을 하고 있을 정도입니다
			CALL CP_HEARTB_PINK(8)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 60
			PRINTFORM 성에 대한 관심이 크게 있으며, 밤마다 상상하고는 흥분하고 있습니다
			CALL CP_HEARTB_PINK(7)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 50
			PRINTFORM 조금이라도 성적 묘사가 있으면, 저도 모르게 확대망상을 해버립니다
			CALL CP_HEARTB_PINK(6)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 40
			PRINTFORM 성 탐구의 일환으로 여러 성적 도구들을 소지하고 있습니다
			CALL CP_HEARTB_PINK(5)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 30
			PRINTFORM 성적인 장르에 대한 거부감은 없습니다
			CALL CP_HEARTB_PINK(4)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 20
			PRINTFORM 남의 눈이 없으면 성에 대한 정보를 입수합니다
			CALL CP_HEARTB_PINK(3)
			PRINTL
		ELSEIF CFlag:L_I:성적인흥미 >= 10
			PRINTFORM 나름대로 성에 대한 관심이 있고, 관련된 것을 다소 알아보고 있습니다
			CALL CP_HEARTB_PINK(2)
			PRINTL
		ELSE
			PRINTFORM 일반적인 성에 대한 관심을 가지고 있습니다
			CALL CP_HEARTB_PINK()
			PRINTL
		ENDIF

		PRINTFORM ・【수컷에 대한 관심】
		IF CFlag:L_I:수컷에관심 >= 90
			PRINTFORM 의중의 상대를 데리고, 녹아내릴 듯이 아양을 떠는 암컷 중의 암컷입니다
			CALL CP_HEARTB_PINK(10)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 80
			PRINTFORM 취향인 남성에게는 바짝 다가서 암컷 어필 할 정도의 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(9)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 70
			PRINTFORM 이성에게는 무르고, 동성에게는 엄격한 태도를 취할 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(8)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 60
			PRINTFORM 생식기를 보면 성적으로 흥분할 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(7)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 50
			PRINTFORM 남성의 나체를 보고싶어할 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(6)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 40
			PRINTFORM 취향인 남성의 생식기는 어떻게 생겼을지 궁금해 할 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(5)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 30
			PRINTFORM 이성에 대한 편견을 전혀 가지고 있지 않습니다
			CALL CP_HEARTB_PINK(4)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 20
			PRINTFORM 자연스레 취향인 남성을 찾을 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(3)
			PRINTL
		ELSEIF CFlag:L_I:수컷에관심 >= 10
			PRINTFORM 거부감 없이 남성과 대화 할 수 있는 정도로 수컷에 대한 관심이 있습니다
			CALL CP_HEARTB_PINK(2)
			PRINTL
		ELSE
			PRINTFORM 남성에 대한 관심이 그리 크지 않습니다
			CALL CP_HEARTB_PINK()
			PRINTL
		ENDIF

		PRINTFORM ・【성교 욕망 　　　】
		IF CFlag:L_I:성교원망 >= 90
			PRINTFORM 서큐버스로 오인할 정도로 성행위에 대한 욕구를 가지고 있습니다
			CALL CP_HEARTB_PINK(10)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 80
			PRINTFORM 할 수만 있다면 계속 교미를 하고 싶을 정도로 성관계를 원하는 것 같습니다
			CALL CP_HEARTB_PINK(9)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 70
			PRINTFORM 몇 번을 절정해도 끝이 없는 성관계 욕구가 있습니다
			CALL CP_HEARTB_PINK(8)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 60
			PRINTFORM 짐승과도 같은 성행위를 원하는 것 같습니다
			CALL CP_HEARTB_PINK(7)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 50
			PRINTFORM 질내를 엉망진창 휘젓는 성행위를 원하는 것 같습니다
			CALL CP_HEARTB_PINK(6)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 40
			PRINTFORM 격렬한 성행위가 하고 싶을 정도로 성행위를 원하는 것 같습니다
			CALL CP_HEARTB_PINK(5)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 30
			PRINTFORM 여성들이 갖기엔 강한 성교 욕망을 가지고 있습니다
			CALL CP_HEARTB_PINK(4)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 20
			PRINTFORM 서로 만족할 수 있는 성행위를 원하는 것 같습니다
			CALL CP_HEARTB_PINK(3)
			PRINTL
		ELSEIF CFlag:L_I:성교원망 >= 10
			PRINTFORM 기분 좋아질 수 있다면 응할 정도의 성교 욕망이 있습니다
			CALL CP_HEARTB_PINK(2)
			PRINTL
		ELSE
			PRINTFORM 성관계에 대해서는 좋아하는 것 같진 않습니다
			CALL CP_HEARTB_PINK()
			PRINTL
		ENDIF

		PRINTFORM ・【임신 소망 　　　】
		IF CFlag:L_I:임신원망 >= 90
			PRINTFORM 뇌가 자궁에 있나 싶을 정도로 임신 소망이 있습니다
			CALL CP_HEARTB_PINK(10)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 80
			PRINTFORM 좋아하는 사람과의 교미라면 바로 배란이 시작 될 정도로 임신하고 싶은 것 같습니다
			CALL CP_HEARTB_PINK(9)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 70
			PRINTFORM 좋아하는 사람과의 아이라면 몇 명이라도 낳고 싶어하는 것 같습니다
			CALL CP_HEARTB_PINK(8)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 60
			PRINTFORM 자궁이 계속 쑤실 정도로 임신하고 싶은 것 같습니다
			CALL CP_HEARTB_PINK(7)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 50
			PRINTFORM 자식을 얻고 싶을 정도로 임신에 긍정적인 자세입니다
			CALL CP_HEARTB_PINK(6)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 40
			PRINTFORM 성행위를 한 후 잉태하고 싶을 정도로 임신을 원하는 것 같습니다
			CALL CP_HEARTB_PINK(5)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 30
			PRINTFORM 질내사정해도 임신에 대한 불안은 없는 것 같습니다
			CALL CP_HEARTB_PINK(4)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 20
			PRINTFORM 좋아하는 사람의 아이라면 낳고 싶어하는 모양입니다
			CALL CP_HEARTB_PINK(3)
			PRINTL
		ELSEIF CFlag:L_I:임신원망 >= 10
			PRINTFORM 단계를 찬찬히 밟아간다면 임신을 용인할 것 같습니다.
			CALL CP_HEARTB_PINK(2)
			PRINTL
		ELSE
			PRINTFORM 별로 임신 하고 싶지 않다고 생각하는 것 같습니다
			CALL CP_HEARTB_PINK()
			PRINTL
		ENDIF
	ENDIF
ENDIF
DRAWLINE



PRINTFORML
PRINTFORML

;=================================================
;   表示:STATUS画面(RPG情報)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_RPG(L_I)
#DIM L_I
PRINTFORML ■------------------------------------ 전투상세 ------------------------------------■
PRINTFORML ◇----------------신체정보----------------◇
CALL PRINT_RANGE_STATUS(L_I, "HP", "체력", , 3, "伏字", "％")
CALL PRINT_RANGE_STATUS(L_I, "SP", "기력", , 3, "伏字", "％")
PRINTL
CALL PRINT_STATUS(L_I, "GUTS" , , 3)
CALL PRINT_STATUS(L_I, "BRAVE", , 3)
CALL PRINT_STATUS(L_I, "SAN"  , , 3)
PRINTL
CALL PRINT_STATUS(L_I, "경험치")
PRINTL
CALL PRINT_STATUS(L_I, "ATK"  , , 3)
CALL PRINT_STATUS(L_I, "DEF"  , , 3)
PRINTL
CALL PRINT_STATUS(L_I, "HIT"  , , 3)
CALL PRINT_STATUS(L_I, "SPD"  , , 3)
CALL PRINT_STATUS(L_I, "LUK"  , , 3)
PRINTL
CALL PRINT_RANGE_STATUS(L_I, "BRE", , , 3, "伏字", "％")
PRINTL
CALL INFO_RPG_SKILL(L_I)
CALL INFO_RPG_WEAPON(L_I)
PRINTFORML 


@INFO_RPG_SKILL(L_I)
#DIM L_I
#DIM CONST SKILL_LABEL_SPAN = 8
PRINTFORML ◇----------------기능정보----------------◇
CALL PRINT_STATUS(L_I, "시각"    , SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "지력"    , SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "청각"    , SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "의술"    , SKILL_LABEL_SPAN, 3)
PRINTL
CALL PRINT_STATUS(L_I, "기계수리", SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "컴퓨터", SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "열쇠따기", SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "은신", SKILL_LABEL_SPAN, 3)
PRINTL
CALL PRINT_STATUS(L_I, "해체", SKILL_LABEL_SPAN, 3)
CALL PRINT_STATUS(L_I, "해독", SKILL_LABEL_SPAN, 3)
PRINTL
@INFO_RPG_WEAPON(L_I)
#DIM L_I
#DIM CONST _INFO_RPG_WEPON_SPAN = 23
PRINTFORML ◇----------------무장정보----------------◇
PRINTFORML □----- 메인장비 -----□□----- 서브장비 -----□
SIF L_I == MASTER && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
CALL PRINT_LABEL(CSTR:L_I:4000, -19)
PRINTFORM %" " * MAX(0, _INFO_RPG_WEPON_SPAN-RESULT)%
CALL PRINT_LABEL(CSTR:L_I:4010, -19)
PRINTL
CALL PRINT_LABEL("타　입",,,INFO_STR_HEISOU(CFlag:L_I:주민_M병기), -4)
PRINTFORM %" " * MAX(0, _INFO_RPG_WEPON_SPAN-RESULT)%
CALL PRINT_LABEL("타　입",,,INFO_STR_HEISOU(CFlag:L_I:주민_S병기), -4)
PRINTL
CALL PRINT_LABEL("숙련도",,,@"\@ CFlag:L_I:주민_M병기숙련도?{CFlag:L_I:주민_M병기숙련도}#\@", -3 )
PRINTFORM %" " * MAX(0, _INFO_RPG_WEPON_SPAN-RESULT)%
CALL PRINT_LABEL("숙련도",,,@"\@ CFlag:L_I:주민_S병기숙련도?{CFlag:L_I:주민_S병기숙련도}#\@", -3 )
PRINTL
CALL PRINT_LABEL("대미지",,,INFO_STR_MAIN_WEAPON_DAMAGE(L_I))
PRINTFORM %" " * MAX(0, _INFO_RPG_WEPON_SPAN-RESULT)%
CALL PRINT_LABEL("대미지",,,INFO_STR_SUB_WEAPON_DAMAGE(L_I))
PRINTL
RESETCOLOR




;=================================================
;   sub INFO_INTRODUCTION
;=================================================
;   表示:STATUS画面(紹介)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_INTRODUCTION(ARG)
#DIM L_I
#DIM L_LOOP

L_I = ARG
L_LOOP = 0
DRAWLINE
PRINTFORML ■------------------------------------ 초기 프로필 ------------------------------------■
DRAWLINE
IF TALENT:L_I:유니크 == 1
	PRINTFORML 【일반 정보:이름】%CSTR:L_I:UPF0%
	SETCOLOR 50,250,150
	PRINTFORML 【기밀 Lv 3:상세】%CSTR:L_I:UPF1%
	PRINTFORML 【기밀 Lv 3:상세】%CSTR:L_I:UPF2%
	PRINTFORML 【기밀 Lv 3:상세】%CSTR:L_I:UPF3%
	PRINTFORML 【기밀 Lv 3:상세】%CSTR:L_I:UPF4%
	PRINTFORML 【기밀 Lv 3:상세】%CSTR:L_I:UPF5%
	SETCOLOR 250,0,0
	PRINTFORML 【기밀 Lv 9:능력】%CSTR:L_I:UPF6%
	PRINTFORML 【기밀 Lv 9:능력】%CSTR:L_I:UPF7%
	PRINTFORML 【기밀 Lv 9:능력】%CSTR:L_I:UPF8%
	PRINTFORML 【기밀 Lv 9:능력】%CSTR:L_I:UPF9%
ELSE
	REPEAT 20
		SIF L_LOOP >= 2
			SETCOLOR 50,250,150
		SIF L_LOOP >= 5
			SETCOLOR 250,250,50
		SIF L_LOOP >= 9
			SETCOLOR 250,50,50
		SIF L_LOOP >= 13
			SETCOLOR 250,0,0
		SIF CSTR:L_I:(70+L_LOOP) != ""
			PRINTFORML %CSTR:L_I:(70+L_LOOP)%
		L_LOOP += 1
	REND
ENDIF
IF L_I == MASTER
	SETCOLOR 250,50,50
	PRINTFORML 【기밀 Lv 8:소속】당신은 도시를 발전・번영시키는「플레이어」중 한 명입니다.
	PRINTFORML 【기밀 Lv 9:운영】위 임무를 수행하는 동안 도시의 주민을 권한이 허락하는 한 이용할 수 있습니다.
	SETCOLOR 250,0,0
	PRINTFORML 【중요 기밀:권한】당신에게 부여된 권한은――――――――――－－－－□○종입니다.
	PRINTFORML 【중요 기밀:일족】４０４　Ｅｒｒｏｒ
	PRINTFORML 【중요 기밀:　　】４０３　Ｅｒｒｏｒ
	PRINTFORML 【중요 기밀:　　】４０３　Ｅｒｒｏｒ
	PRINTFORML 【중요 기밀:　　】４０３　Ｅｒｒｏｒ
	PRINTFORML 【중요 기밀:인권】당신에게는 제 1급 인권이 부여되어 있습니다,                     인권이 지켜지고 있습니다.
	PRINTFORML 【중요 기밀:　　】４０３　Ｅｒｒｏｒ
	PRINTFORML 【　　 기밀:　　】당신에게는 일체의 허가가 되어 있지 않습니다.
	PRINTFORML 【　　 기밀:　　】당신은          에서의    에 반드시    있어야 합니다.
ENDIF
RESETCOLOR
DRAWLINE





;=================================================
;   sub INFO_SIMPLE_STATUS_EXP
;=================================================
;   簡易表示:STATUS(経験)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@INFO_SIMPLE_STATUS_EXP(ARG)
#DIM L_HIT
#DIM L_CNT

L_HIT=0
L_CNT=0
VARSET LOCAL

FOR LOCAL,0,VARSIZE("EXP")

	LOCALS = %EXPNAME:LOCAL%
	SIF STRLENS(LOCALS) <= 0
		CONTINUE
	LOCAL:1 = EXP:ARG:LOCAL
	SIF !LOCAL:1
		CONTINUE

	PRINTFORM %LOCALS , 16%
	PRINTFORM :LV{GETEXPLV(LOCAL:1,9), 2, LEFT}
	PRINTFORM (%TOSTR(LOCAL:1) , 4%회)
	PRINT  
	L_HIT = 1

	L_CNT += 1
	IF L_CNT > 2
		PRINTL
		L_CNT = 0
		L_HIT = 0
	ENDIF

NEXT

SIF L_HIT
	PRINTL



;-------------------------------------------------
;ラベル使用キャラステータス参照関数
;-------------------------------------------------
;ラベル名から指定キャラのステータス値を参照する
;ラベル名が不正な場合は0
@INFO_STATUS_GET_BY_LABEL(C_ID, LABEL_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID           ;
#DIMS LABEL_NAME     ;
#DIMS LABEL_VAR_NAME ;
#DIM  LABEL_VAR_ID   ;
IF CHARA_SORT_FIND_LABEL(LABEL_NAME, LABEL_VAR_NAME, LABEL_VAR_ID)
	RETURNF C_GET_CVAR_VALUE(C_ID, LABEL_VAR_NAME, LABEL_VAR_ID)
ELSE
	DEBUGPRINTFORML INFO_STATUS_GET_BY_LABEL, 不正なLABEL_NAME %LABEL_NAME%
	RETURNF 0
ENDIF


;-------------------------------------------------
;個人の悩み一覧関数
;-------------------------------------------------
;仕様書いてないからよくわかんねーけど
;とりあえず大小各1～9までが悩みスロットと解釈したよ？
@INFO_STATUS_NAYAMI_LIST(C_ID, OP)
#DIM  C_ID   ;登録番号
#DIMS OP     ;実行オプション
#DIM  I_LOOP ;ループ用
DRAWLINE
PRINTFORML ◇----------------- 큰 고민 -----------------◇
IF STRCOUNT(OP, "「ボタン」")
	FOR I_LOOP, 1, 9
		SIF CFLAG:C_ID:(4000 + I_LOOP) <= 0
			CONTINUE
		IF CFLAG:C_ID:(4200 + I_LOOP) >= 100
			PRINTFORM 해결　
		ELSE
			PRINTFORM [{I_LOOP, 2}]
		ENDIF
		CALL INFO_STATUS_NAYAMI(C_ID, I_LOOP)
	NEXT
ELSE
	FOR I_LOOP, 1, 9
		SIF CFLAG:C_ID:(4000 + I_LOOP) <= 0
			CONTINUE
		CALL INFO_STATUS_NAYAMI(C_ID, I_LOOP)
	NEXT
ENDIF
PRINTFORML ◇---------------- 작은 고민 ----------------◇
IF STRCOUNT(OP, "「ボタン」")
	FOR I_LOOP, 11, 19
		SIF CFLAG:C_ID:(4000 + I_LOOP) <= 0
			CONTINUE
		IF CFLAG:C_ID:(4200 + I_LOOP) >= 100
			PRINTFORM 해결　
		ELSE
			PRINTFORM [{I_LOOP, 2}]
		ENDIF
		CALL INFO_STATUS_NAYAMI(C_ID, I_LOOP)
	NEXT
ELSE
	FOR I_LOOP, 11, 19
		SIF CFLAG:C_ID:(4000 + I_LOOP) <= 0
			CONTINUE
		CALL INFO_STATUS_NAYAMI(C_ID, I_LOOP)
	NEXT
ENDIF

;-------------------------------------------------
;悩み情報表示関数
;-------------------------------------------------
@INFO_STATUS_NAYAMI(C_ID, NAYAMI_ID)
#DIM C_ID
#DIM NAYAMI_ID
IF CFLAG:C_ID:(4100 + NAYAMI_ID) >= 100
	SELECTCASE CFLAG:C_ID:(4000 + NAYAMI_ID)
	CASE IS <= 199
		SETCOLOR COLOR("悩み_金銭")
		CALL PRINT_LABEL("금전고민")
	CASE IS <= 299
		SETCOLOR COLOR("悩み_恋愛")
		CALL PRINT_LABEL("연애고민")
	CASE IS <= 399
		SETCOLOR COLOR("悩み_家庭")
		CALL PRINT_LABEL("가정고민")
	CASE IS <= 499
		SETCOLOR COLOR("悩み_생활")
		CALL PRINT_LABEL("생활고민")
	CASE IS <= 599
		SETCOLOR COLOR("悩み_犯罪")
		CALL PRINT_LABEL("범죄고민")
	CASE IS <= 699
		SETCOLOR COLOR("悩み_性的")
		CALL PRINT_LABEL("성적고민")
	ENDSELECT
ELSE
	SIF FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM ？？？(미판명)
	CALL PRINT_LABEL("해석",,,TOSTR(CFLAG:C_ID:(4100 + NAYAMI_ID)),3,,"/","100")
ENDIF
PRINTFORM 　난도：{CFLAG:C_ID:(4300 + NAYAMI_ID),2}　
PRINTFORM 달성률：({CFLAG:C_ID:(4200 + NAYAMI_ID),3}/100)
RESETCOLOR
SIF NAYAMI_ID == CFLAG:C_ID:주민_悩み解決選択
	PRINTFORM 　현재 해결중
PRINTFORML 
