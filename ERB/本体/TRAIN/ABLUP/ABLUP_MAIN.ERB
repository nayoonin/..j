;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:ABLUP_MAIN.ERB
;	Facility	:ABLUP画面。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;   002     2019/04/14      M                       表示リファクタリング：表示桁幅固定
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- 表示系
;
;	・ SYSTEM_ABLUP_LIST				SYSTEM:ABLUP画面(LIST)
;
;	# ABLUP:リスト画面
;	・ ABLUP_MAIN						表示:ABLUP画面 主処理
;	・ _ABLUP_LIST_PRINT_SIGNAL			表示:ABLUP(LIST-結果)
;	・ _ABLUP_LIST_MAIN					表示:ABLUP(LIST-MAIN)
;
;	# ABLUP:詳細画面
;	・ _ABLUP_DETAIL_MAIN				表示:ABLUP詳細 主処理
;	・ _ABLUP_DETAIL					表示:ABLUP詳細
;
;===============================================================================

;=================================================
;   sub SYSTEM_ABLUP_LIST
;=================================================
;   SYSTEM:ABLUP画面(LIST)
;-------------------------------------------------
@SYSTEM_ABLUP_LIST
#DIM L_INPUT
#DIMS L_ERR_MSG

;-----------------------------
;- 初期処理
;-----------------------------
L_INPUT=0
L_ERR_MSG=

;-----------------------------
;- キャラリスト
;-----------------------------
RESULT:1=0
$CHARA_LIST

;- LIST:キャラリスト
CALL LIST_CHARA(@"능력을 상승시킬 캐릭터를 선택해주세요.　　", RESULT:1, L_ERR_MSG)
L_INPUT = RESULT

;- 表示:ページ別
SELECTCASE L_INPUT
	;- 終了
	CASE 1000
		RETURN 0
	;- キャラ
	CASEELSE
		;- 表示:ABLUP画面 主処理
		CALL ABLUP_MAIN(L_INPUT)
		GOTO CHARA_LIST
ENDSELECT
RESTART

;-------------------------------------------------------------------------------
;   ABLUP:リスト画面
;-------------------------------------------------------------------------------

;=================================================
;   sub ABLUP_MAIN
;=================================================
;   表示:ABLUP画面 主処理
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;-------------------------------------------------
@ABLUP_MAIN(ARG=-1,ARG:1)
#DIM L_INPUT
#DIM L_TMP
#DIM L_PAGE_NOW
#DIM L_PAGE_下限
#DIM L_PAGE_上限

#DIM L_CHARA_前
#DIM L_CHARA_次

L_INPUT=0
L_TMP=0
L_PAGE_NOW=0
L_PAGE_下限=0
L_PAGE_上限=0

L_CHARA_前=0
L_CHARA_次=0

;-----------------------------
;- 定数設定
;-----------------------------
L_PAGE_下限 = 0
L_PAGE_上限 = 0


;-----------------------------
;- 処理開始
;-----------------------------
$PRINT_LOOP_1
VARSET B

L_CHARA_前 = VAR_GET_CL("PREV",ARG)
L_CHARA_次 = VAR_GET_CL("NEXT",ARG)

;-----------------------------
;- 表示:ヘッダ
;-----------------------------

;表示:ページヘッダ
CALL C_LINE
PRINTFORML 어느 능력을 상승／각인을 소거하시겠습니까?
PRINTFORML （대상 캐릭터：%CALLNAME:ARG%）
PRINTL

;-----------------------------
;- 表示:内容部
;-----------------------------

;- 表示:ABLUP(LIST-MAIN)
CALL _ABLUP_LIST_MAIN(ARG)
PRINTL

;체력
B:200 = 1

;- 表示:選択部
PRINTFORMLC \@(L_CHARA_前 > -1) ? [1001]이전캐릭터 # %" " * 16%\@
PRINTLC [1000]돌아간다
PRINTFORMLC \@(L_CHARA_次 > -1) ? [1003]다음캐릭터 # %" " * 16%\@
PRINTL 
PRINTL 
PRINTFORM %" " * 52%
PRINTBUTTON @"[1010]전원의 체력을 상승시킨다", 1010
PRINTL 

$INPUT_LOOP_1
INPUT
L_INPUT = RESULT

SELECTCASE L_INPUT
	CASE 1000
		RETURN
	;前のキャラ
	CASE 1001
		SIF L_CHARA_前 > -1
			ARG = L_CHARA_前
	;次のキャラ
	CASE 1003
		SIF L_CHARA_次 > -1
			ARG = L_CHARA_次
	;全員の체력を上げる
	CASE 1010
		CALL MAXBASEUP_ALL
	CASEELSE
		;- CHK:NG
		IF B:L_INPUT == 0
			;念の為詰め替え
			RESULT = L_INPUT
			;INPUTエラー対応
			CALL C_INPUT_ERROR
			GOTO INPUT_LOOP_1
		ENDIF

		;-----------------------------
		;- パターン選択画面を表示
		;-----------------------------
		SELECTCASE L_INPUT
			;- 個人PALAM(ABL)を変更する場合
			CASE 0 to ( VARSIZE("ABL") - 1)

				;- 位置作成
				L_TMP = L_INPUT - 0

				IF ABL:ARG:L_TMP >= FLAG:최대数_ABLLV
					CLEARLINE 1
					REUSELASTLINE 이 이상 %조사처리(ABLNAME:L_TMP,"를")% 상승시킬 수 없습니다.
					GOTO INPUT_LOOP_1
				ENDIF

				;- 表示:ABLUP詳細 主処理
				CALL _ABLUP_DETAIL_MAIN(ARG,L_TMP,"ABL")

			;- 個人PALAM(MARK)を変更する場合
			CASE VARSIZE("ABL") to ( VARSIZE("ABL") + VARSIZE("MARK") - 1)

				;- 位置作成
				L_TMP = L_INPUT - VARSIZE("ABL")

				IF MARK:ARG:L_TMP <= 0
					CLEARLINE 1
					REUSELASTLINE %조사처리(MARKNAME:L_TMP,"는")% 없습니다.
					GOTO INPUT_LOOP_1
				ENDIF

				;- 表示:ABLUP詳細 主処理
				CALL _ABLUP_DETAIL_MAIN(ARG,L_TMP,"MARK")
			;- 個人PALAM(BASE)を変更する場合
			CASE 200
				$INPUT_LOOP_체력
				DRAWLINE
				PRINTFORML 체력을 상승시키겠습니까? 최대 {FLAG:최대値_체력}까지 상승시킬 수 있습니다.
				PRINTFORML （상승에 %조사처리(GVAR_MONEY_NAME,"를")% 300만큼 소비합니다. 현재 %GVAR_MONEY_NAME%:{MONEY}）
				PRINTL
				L_TMP = 50
				PRINTFORML 현재 체력：{MAXBASE:ARG:체력} → {MIN(MAXBASE:ARG:체력 + L_TMP, FLAG:최대値_체력)}
				PRINTL
				CALL INPUT_YN
				SELECTCASE RESULT
					CASE 0
						IF MAXBASE:ARG:체력 == FLAG:최대値_체력
							PRINTW 이미 최대치입니다.
							GOTO INPUT_LOOP_체력
						ELSE
							IF MONEY < 300
								PRINTFORMW %조사처리(GVAR_MONEY_NAME,"가")% 부족합니다.
								GOTO INPUT_LOOP_체력
							ENDIF
							MONEY -= 300
							MAXBASE:ARG:체력 = MIN(MAXBASE:ARG:체력 + L_TMP, FLAG:최대値_체력)

							PRINTFORML 체력이 상승되었습니다.
							PRINTFORML 현재 체력：{MAXBASE:ARG:체력}
							WAIT
						ENDIF
					CASE 1
						;スルー
				ENDSELECT

		ENDSELECT
		RESTART
ENDSELECT

GOTO PRINT_LOOP_1

;=================================================
;   sub MAXBASEUP_ALL
;=================================================
;人数多くなると全員の체력を上げるのが面倒なので、一括して上げる関数
;-------------------------------------------------
@MAXBASEUP_ALL
#DIM L_I
#DIM L_TMP
#DIM L_MONEY
#DIM L_TOTALMONEY

VARSET L_I
VARSET L_TOTALMONEY

L_TMP = 50
L_MONEY = 300

;必要資金の計算。今後최대値上限が増えた場合、改修の必要あり
;（최대値の指定はVER 0.52時点でERB\本体\VAR_PROCESS_1日終了.ERBにある）
FOR L_I, 0, CHARANUM
	;既に최대なら無視
	IF MAXBASE:L_I:체력 == FLAG:최대値_체력
		CONTINUE
	;자원봉사자も無視
	ELSEIF TALENT:L_I:자원봉사자
		CONTINUE
	;50毎に300円
	ELSE
		L_TOTALMONEY += (FLAG:최대値_체력 - MAXBASE:L_I:체력) / L_TMP * L_MONEY
	ENDIF
NEXT

DRAWLINE
DRAWLINE
PRINTFORML 체력이 최대치가 아닌 주민들의 체력을 {FLAG:최대値_체력}까지 상승시키겠습니까?（자원봉사자는 예외）
PRINTFORM （상승에 %조사처리(GVAR_MONEY_NAME,"를")% 
SETCOLOR 0xffff00
PRINTFORM {L_TOTALMONEY}
RESETCOLOR
PRINTFORML 만큼 소비합니다. 현재 %GVAR_MONEY_NAME%:{MONEY}）
PRINTL

CALL INPUT_YN
SELECTCASE RESULT
	;はい
	CASE 0
		IF MONEY < L_TOTALMONEY
			PRINTFORMW %조사처리(GVAR_MONEY_NAME,"가")% 부족합니다.
			RETURN
		ELSEIF L_TOTALMONEY == 0
			PRINTFORMW 이미 전원 최대치입니다.
			RETURN
		ENDIF
		FOR LOCAL, 0, CHARANUM
			;既に최대なら無視
			IF MAXBASE:LOCAL:체력 == FLAG:최대値_체력
				CONTINUE
			;자원봉사자も無視
			ELSEIF TALENT:LOCAL:자원봉사자
				CONTINUE
			;雑に全員최대値。
			ELSE
				MAXBASE:LOCAL:체력 = FLAG:최대値_체력
			ENDIF
		NEXT
		;総비용分お金の減少
		MONEY -= L_TOTALMONEY
		PRINTFORMW {L_TOTALMONEY}의 %조사처리(GVAR_MONEY_NAME,"를")% 소비해, 전원의 체력을 {FLAG:최대値_체력}까지 상승시켰습니다.

	;いいえ
	CASE 1
		;スルー
ENDSELECT

RETURN
;=================================================
;   sub _ABLUP_LIST_PRINT_SIGNAL
;=================================================
;   表示:ABLUP(LIST-結果)
;-------------------------------------------------
; Input:
;  ARG:0				結果(0:OK 1:NG)
;  ARG:1				현재値
;  ARG:2				최대値
;  ARG:9				計算方法(0:加算方式 1:減算方式)
;-------------------------------------------------
@_ABLUP_LIST_PRINT_SIGNAL(ARG,ARG:1=0,ARG:2=0,ARG:9=0)

;- 加算方式
IF (ARG:9 == 0) && (ARG:1 >= ARG:2)
	PRINT MAX
;- 減算方式
ELSEIF (ARG:9 == 1) && (ARG:1 <= ARG:2)
	PRINT MIN
ELSE
	;- 結果判定
	IF !ARG
		SETCOLOR COLOR("水色")
		PRINT  OK
		RESETCOLOR
	ELSE
		;灰色
		SETCOLOR COLOR("伏字")
		PRINT  NG
		RESETCOLOR
	ENDIF
ENDIF


;=================================================
;   sub _ABLUP_LIST_MAIN
;=================================================
;   表示:ABLUP(LIST-MAIN)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  B[0..x]				존재확인(0:NG 1:OK)
;-------------------------------------------------
@_ABLUP_LIST_MAIN(ARG)
#DIM L_I
#DIM L_MAX
#DIM L_TMP

L_I=0
L_MAX=0
L_TMP=0

;-----------------------------
;-- 表示:ABL
;-----------------------------
VARSET LOCAL

L_MAX = FLAG:최대数_ABLLV

;- ライン表示:見出し
CALL C_HEADLINE("능력(ABL)")

;- 全要素をチェック
FOR L_I,0,VARSIZE("ABL")

	LOCALS	= %ABLNAME:L_I%
	LOCAL:1	= ABL:ARG:L_I

	;- CHK:読み飛ばし
	SIF STRLENS(LOCALS) <= 0
		CONTINUE

	;- ABLUP可能かを表示させるためのチェック
	CALLFORM CHK_ABLUP_MAIN(ARG,L_I,"ABL")
	; CHK:対象外
	SIF RESULT < 0 || RESULT > 1
		CONTINUE
	LOCAL:9 = RESULT
	;- 番号作成
	L_TMP = L_I + 0
	;NG
;	SIF LOCAL:9
;		SETCOLOR COLOR("伏字")

	;-- 表示
	PRINTFORM  [{L_TMP, 3}] %LOCALS,10,LEFT% :LV{ABL:ARG:L_I, 2, LEFT} 
	BAR LOCAL:1, L_MAX, 3 

	;-  表示:ABLUP(LIST-結果)
	CALL _ABLUP_LIST_PRINT_SIGNAL(LOCAL:9,LOCAL:1,L_MAX,0)
;	RESETCOLOR
	B:L_TMP = 1

	LOCAL:5 += 1
	SIF (LOCAL:5%3) == 0
		PRINTL

NEXT

SIF (LOCAL:5%3) != 0
	PRINTL

PRINTL
;-----------------------------
;-- 表示:MARK
;-----------------------------
VARSET LOCAL

L_MAX = FLAG:최대数_MARKLV

;- ライン表示:見出し
CALL C_HEADLINE("각인(MARK)")

;- 全要素をチェック
FOR L_I,0,VARSIZE("MARK")

	LOCALS	= %MARKNAME:L_I%
	LOCAL:1	= MARK:ARG:L_I

	;- CHK:読み飛ばし
	SIF STRLENS(LOCALS) <= 0
		CONTINUE

	;- ABLUP可能かを表示させるためのチェック
	CALLFORM CHK_ABLUP_MAIN(ARG,L_I,"MARK")
	; CHK:対象外
	SIF !(RESULT == 0 || RESULT == 1)
		CONTINUE
	LOCAL:9 = RESULT
	;- 番号作成
	L_TMP = L_I + VARSIZE("ABL")

	;-- 表示
	PRINTFORM  [{L_TMP, 3}] %LOCALS,10,LEFT% :LV{MARK:ARG:L_I, 2, LEFT} 
	BAR LOCAL:1, L_MAX, 3 

	;-  表示:ABLUP(LIST-結果)
	CALL _ABLUP_LIST_PRINT_SIGNAL(LOCAL:9,LOCAL:1,0,1)

	B:L_TMP = 1

	LOCAL:5 += 1
	SIF (LOCAL:5%3) == 0
		PRINTL

NEXT

SIF (LOCAL:5%3) != 0
	PRINTL

PRINTL

;-----------------------------
;-- 表示:BASE
;-----------------------------

;- ライン表示:見出し
CALL C_HEADLINE("기초(BASE)")
LOCAL:1 = MAXBASE:ARG:체력
PRINTFORM  [{200, 3}] %"체력",8,LEFT% :{LOCAL:1}/{FLAG:최대値_체력} 
BAR LOCAL:1, FLAG:최대値_체력, 10 
PRINTL
PRINTL


;-------------------------------------------------------------------------------
;   ABLUP:詳細画面
;-------------------------------------------------------------------------------

;=================================================
;   sub _ABLUP_DETAIL_MAIN
;=================================================
;   表示:ABLUP詳細 主処理
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ位置
;  ARG:1				対象要素位置
;  ARGS					変更対象(ABL,MARK)
;-------------------------------------------------
@_ABLUP_DETAIL_MAIN(ARG, ARG:1, ARGS="")
VARSET LOCAL

$PRINT_LOOP_1

;-----------------------------
;- 表示:ヘッダ
;-----------------------------

;表示:ページヘッダ
CALL C_LINE

;- 処理対象で分岐
SELECTCASE ARGS
	CASE "ABL"
		PRINTFORML 어느 방법으로 능력을 상승시킵니까?
		PRINTFORML （대상 캐릭터：%NAME:ARG%）
		PRINTFORML （대상 능력　：%ABLNAME:(ARG:1)% LV:%TOSTR(ABL:ARG:(ARG:1))%）
	CASE "MARK"
		PRINTFORML 어느 방법으로 각인을 소거합니까?
		PRINTFORML （대상 캐릭터：%NAME:ARG%）
		PRINTFORML （대상 각인　：%MARKNAME:(ARG:1)% LV:%TOSTR(MARK:ARG:(ARG:1))%）
	CASEELSE
		;- エラーの為、RETURN
		PRINTFORML 【ERROR】
		PRINTFORML _ABLUP_DETAIL_MAIN에 에러가 발생했습니다.
		PRINTFORML 인수가 이상합니다.
		PRINTFORML 인수:%ARGS%
		RETURN
ENDSELECT
PRINTL

;-----------------------------
;- 能力
;-----------------------------

;- ライン表示:見出し
CALL C_HEADLINE("현재 능력 데이터")

;- 簡易表示:STATUS(能力)
CALL INFO_STATUS_ABL(ARG, 1)
PRINTL

;-----------------------------
;- 経験
;-----------------------------

;- ライン表示:見出し
CALL C_HEADLINE("현재 경험 데이터")

;- 簡易表示:STATUS(経験)
CALL INFO_SIMPLE_STATUS_EXP(ARG)
PRINTL

;-----------------------------
;- 宝珠
;-----------------------------

;- ライン表示:見出し
CALL C_HEADLINE("현재 구슬 데이터")

;- 表示:STATUS(珠)
CALL INFO_STATUS_JUEL(ARG)
PRINTL

;-----------------------------
;- 表示:内容部
;-----------------------------

;- 表示:ABLUP詳細
CALL _ABLUP_DETAIL(ARG, ARG:1,ARGS)

;- 表示:選択部
PRINTLC  
PRINTLC [1000]戻る


;-----------------------------
;- 表示:INPUT
;-----------------------------

$INPUT_LOOP_1
INPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1000
		RETURN

	;- パターン数の範囲内か
	CASE 0 to ABLUP_GET_SYRPTN_NUM()

		;- 選択されたパターンをチェック

		;- CHK:ABLUP(詳細)
		CALL CHK_ABLUP_DETAIL(ARG, ARG:1, LOCAL, ARGS)

		;- CHK:結果
		SELECTCASE RESULT
			;- 対象外
			CASE -1
				;念の為詰め替え
				RESULT = LOCAL
				;INPUTエラー対応
				CALL C_INPUT_ERROR
				GOTO INPUT_LOOP_1
			;- OK
			CASE 0
				;- 実行:ABLUP
				CALL ABLUP_EXE(ARG,ARG:1,LOCAL,ARGS)

				SELECTCASE ARGS
					CASE "ABL"
						PRINTFORMW %ABLNAME:(ARG:1)%의 레벨이 {ABL:ARG:(ARG:1)}이 되었습니다.
					CASE "MARK"
						PRINTFORMW %MARKNAME:(ARG:1)%의 레벨이 {MARK:ARG:(ARG:1)}이 되었습니다.
				ENDSELECT
			;- NG
			CASEELSE
				CLEARLINE 1
				REUSELASTLINE 조건을 만족하고 있지 않습니다.
				GOTO INPUT_LOOP_1
		ENDSELECT
	CASEELSE
		;念の為詰め替え
		RESULT = LOCAL
		;INPUTエラー対応
		CALL C_INPUT_ERROR
		GOTO INPUT_LOOP_1
ENDSELECT

GOTO PRINT_LOOP_1

;=================================================
;   sub _ABLUP_DETAIL
;=================================================
;   表示:ABLUP詳細
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ位置
;  ARG:1				対象要素位置
;  ARGS					変更対象(ABL,MARK)
;-------------------------------------------------
@_ABLUP_DETAIL(ARG, ARG:1, ARGS="")
#DIM L_SYRPTN
#DIM L_CHARA_POS
#DIM L_NG

L_SYRPTN=0
L_CHARA_POS	= ARG
L_NG=0

;- 処理対象で分岐
SELECTCASE ARGS
	CASE "ABL"
		LOCALS	= %ABLNAME:(ARG:1)%
	CASE "MARK"
		LOCALS	= %MARKNAME:(ARG:1)%
	CASEELSE
		;- エラー
		PRINTFORML 【ERROR】
		PRINTFORML _ABLUP_DETAIL
		PRINTFORML 引数:%ARGS%
		RETURN
ENDSELECT

;- パターンを全処理
FOR L_SYRPTN,0,ABLUP_GET_SYRPTN_NUM()
	
	VARSET RESULTS
	VARSET L_NG

	;- CHK:ABLUP(詳細)
	CALL CHK_ABLUP_DETAIL(ARG, ARG:1, L_SYRPTN, ARGS)
	L_NG = RESULT

	;-- 必要データの出力

	;- 全件連結
	LOCALS  =
	REPEAT ( VARSIZE("JUEL") + VARSIZE("EXP") + VARSIZE("ABL") + VARSIZE("MARK") )
		SIF !STRLENS(RESULTS:COUNT)
			CONTINUE
		SIF STRLENS(LOCALS)
			LOCALS += "、"
		LOCALS += RESULTS:COUNT
	REND

	;- データ無しの場合、次のパターンへ
	SIF !STRLENS(LOCALS)
		CONTINUE

	;- データありの場合、表示
	PRINTFORM [{L_SYRPTN}] - %LOCALS%・・・

	;- 連結
	LOCALS =
	IF L_NG == 0
		LOCALS +="ＯＫ "
	ELSE
		SIF GETBIT(L_NG,1)
			LOCALS +="宝珠 "
		SIF GETBIT(L_NG,2)
			LOCALS +="経験 "
		SIF GETBIT(L_NG,3)
			LOCALS +="能力 "
		SIF GETBIT(L_NG,4)
			LOCALS +="刻印 "
		LOCALS +="が不足"
	ENDIF

	PRINTSL LOCALS
NEXT

PRINTL

