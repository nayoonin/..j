;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SOURCE_CALC_SRC.ERB
;	Facility	:SOURCE関連処理(ソース計算)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- XXXX
;
;	・ SRC_CALC_SENSE_MAIN				SRC計算:感覚(主処理)
;	・ SRC_CALC_SENSE_DATA				SRC計算:感覚(データ)
;	・ SRC_CALC_MAIN					SRC計算:主処理
;	・ SRC_CALC_DETAIL					SRC計算:詳細
;
; *- SRC補正
;
;	・ SRC_REVISION_TALENT_感度			SRC補正:感度
;	・ SRC_REVISION_ABL_感覚			SRC補正:ABL(感覚)
;	・ SRC_REVISION_ABL_농락			SRC補正:ABL(농락)
;	・ SRC_REVISION_EXP_部位経験		SRC補正:EXP(部位経験)
;	・ SRC_REVISION_PALAM_욕정			SRC補正:PALAM(욕정)
;	・ SRC_REVISION_PALAM_윤활			SRC補正:PALAM(윤활)
;	・ SRC_REVISION_PALAM_치정			SRC補正:PALAM(치정)
;	・ SRC_REVISION_ABL_매료기능		SRC補正:ABL(매료기능)
;	・ SRC_REVISION_ABL_욕망			SRC補正:ABL(욕망)
;	・ SRC_REVISION_ABL_기교			SRC補正:ABL(기교)
;	・ SRC_REVISION_ABL_순종			SRC補正:ABL(순종)
;	・ SRC_REVISION_ABL_친밀			SRC補正:ABL(친밀)
;	・ SRC_REVISION_相殺				SRC補正:相殺
;	・ SRC_REVISION_性行動				SRC補正:性行動
;
; *- SRC共通
;
;	・ SRC_COMMON_肉体接触系			SRC共通:肉体接触系
;	・ SRC_COMMON_征服系				SRC共通:征服系
;	・ SRC_COMMON_部位快感				SRC共通:部位快感
;
; *- SOURCE反映(個別)
;
;	・ SRC_REFLECT_쾌C					SRC反映:쾌C
;	・ SRC_REFLECT_쾌V					SRC反映:쾌V
;	・ SRC_REFLECT_쾌A					SRC反映:쾌A
;	・ SRC_REFLECT_쾌B					SRC反映:쾌B
;	・ SRC_REFLECT_液体					SRC反映:液体
;	・ SRC_REFLECT_接触					SRC反映:接触
;	・ SRC_REFLECT_性行為				SRC反映:性行為
;	・ SRC_REFLECT_性奉仕				SRC反映:性奉仕
;	・ SRC_REFLECT_고통					SRC反映:고통
;	・ SRC_REFLECT_露出					SRC反映:露出
;	・ SRC_REFLECT_征服					SRC反映:征服
;	・ SRC_REFLECT_純愛					SRC反映:純愛
;	・ SRC_REFLECT_습득					SRC反映:습득
;	・ SRC_REFLECT_절정					SRC反映:절정
;	・ SRC_REFLECT_精液					SRC反映:精液
;	・ SRC_REFLECT_子宮精液				SRC反映:子宮精液
;	・ SRC_REFLECT_歓楽					SRC反映:歓楽
;	・ SRC_REFLECT_생활					SRC反映:생활
;
; *- SOURCE反映(その他)
;
;	・ SRC_CUP_EXTRA					SRC反映:CUP(特殊)
;	・ SRC_EXP							SRC反映:경험치
;	・ SRC_DOWNBASE						SRC反映:DOWNBASE
;
;===============================================================================

;-------------------------------------------------------------------------------
;   XXXX
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_CALC_SENSE_MAIN
;=================================================
;   SRC計算:感覚(主処理)
;-------------------------------------------------
;   先に快○○系を集計する(절정判定が先行処理の為)
;-------------------------------------------------
@SRC_CALC_SENSE_MAIN
#DIM L_I
#DIM L_J
#DIM L_T
#DIMS L_STR

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK
	SIF L_T < 0
		BREAK

	CALL SRC_CALC_SENSE_DATA(L_T, "쾌C")
	CALL SRC_CALC_SENSE_DATA(L_T, "쾌V")
	CALL SRC_CALC_SENSE_DATA(L_T, "쾌A")
	CALL SRC_CALC_SENSE_DATA(L_T, "쾌B")

NEXT

;=================================================
;   sub SRC_CALC_SENSE_DATA
;=================================================
;   SRC計算:感覚(データ)
;-------------------------------------------------
; Input:
;  ARG:0				キャラ
;  ARGS					ソース名
;-------------------------------------------------
@SRC_CALC_SENSE_DATA(ARG:0, ARGS)
#DIM L_I
#DIM L_T
#DIMS L_STR
L_I = GETNUM(SOURCE, ARGS)
L_T = ARG
L_STR = %SOURCENAME:L_I%

SIF !SOURCE:L_T:L_STR
	RETURN

SELECTCASE L_T
	;主人は全部2倍
	CASE MASTER
		SOURCE:L_T:L_STR *= 2
	;主人以外は一部調整
	CASEELSE
		IF MARK:함락각인
			SIF L_STR == "쾌V"
				TIMES SOURCE:L_T:L_STR, 1.50
		ENDIF
ENDSELECT

;追加で上昇(150%)
SOURCE:L_T:L_STR = C_TIMES(SOURCE:L_T:L_STR, 150)

;- SRC反映:xxx
CALLFORM SRC_REFLECT_%L_STR%(L_T)

;同一コマンド時、半減
SIF TFLAG:同一コマンド実行フラグ && (L_T == TFLAG:メインターゲット)
	CUP:L_T:L_STR /= 2

;計算:절정加算値
TCVAR:L_T:절정加算値 += CUP:L_T:L_STR


;=================================================
;   sub SRC_CALC_MAIN
;=================================================
;   SRC計算:主処理
;-------------------------------------------------
;   全PLAYERの全SOURCEを計算する
;-------------------------------------------------
@SRC_CALC_MAIN
#DIM L_I
#DIM L_T

;-----------------------------
;-- 全ソースを先に計算
;-----------------------------

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK:END
	SIF L_T < 0
		BREAK

	;- SRC計算:詳細
	CALL SRC_CALC_DETAIL(L_T)

	;同一コマンド時、半減
	IF TFLAG:同一コマンド実行フラグ && (L_T == TARGET)
		CUP:L_T:욕정 /= 2
		CUP:L_T:습득 /= 2
		CUP:L_T:치정 /= 2
		CUP:L_T:호의 /= 2
		CUP:L_T:굴복 /= 2
	ENDIF
NEXT

;- あなたは消費無し
DOWNBASE:MASTER:체력=0

;-----------------------------
;-- 全ソース計算後、特殊計算処理
;-----------------------------

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK:END
	SIF L_T < 0
		BREAK

	;- SRC反映:CUP(特殊)
	CALL SRC_CUP_EXTRA(L_T)

	;- SRC反映:경험치
	CALL SRC_EXP(L_T)

	;- SRC反映:DOWNBASE
	CALL SRC_DOWNBASE(L_T)

	;- 行動フラグを立てる(ソース変動)
	TCVAR:L_T:行動フラグ_ソース変動 = 1

	;- 各種フラグ立て
	TCVAR:L_T:キャラ욕정 = ( (TR_GETPALAMLV("욕정", L_T) >= 5) ? 1 # 0 )
	TCVAR:L_T:ペニス勃起 = ( (TR_GETPALAMLV("욕정", L_T) >= 3) ? 1 # 0 )
	TCVAR:L_T:キャラ윤활 = ( (TR_GETPALAMLV("윤활", L_T) >= 5) ? 1 # 0 )

NEXT

;=================================================
;   sub SRC_CALC_DETAIL
;=================================================
;   SRC計算:詳細
;-------------------------------------------------
;   既に一部ソースが先行計算されている為、
;   当関数では計算されていないソースを処理する。
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_CALC_DETAIL(ARG=-1)
#DIM L_I
#DIMS L_STR

FOR L_I, 0, VARSIZE("SOURCE")

	; CHK:値
	SIF !SOURCE:ARG:L_I
		CONTINUE

	L_STR = %SOURCENAME:L_I%

	;先行計算済みは除外
	SIF GROUPMATCH(L_STR, "쾌C", "쾌V", "쾌A", "쾌B")
		CONTINUE

	;- SRC反映:xxx
	CALLFORM SRC_REFLECT_%L_STR%(ARG)

NEXT

;-------------------------------------------------------------------------------
;   SRC補正
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_REVISION_TALENT_感度
;=================================================
;   SRC補正:感度
;-------------------------------------------------
;   範囲: 50%/100%/150%
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				対象値
;  ARGS					対象感度
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_TALENT_感度(ARG, ARG:1, ARGS)
#FUNCTION
#DIM L_TMP
#DIMS L_STR

L_STR = %SUBSTRING(ARGS,0,2)%

L_TMP = 0
SIF TALENT:ARG:@"%L_STR%민감"
	L_TMP = 1
SIF TALENT:ARG:@"%L_STR%둔감"
	L_TMP = -1

SELECTCASE L_TMP
	CASE IS < 0
		L_TMP = 50
	CASE IS > 0
		L_TMP = 150
	CASEELSE
		L_TMP = 100
ENDSELECT

RETURNF (L_TMP * ARG:1 / 100)

;=================================================
;   sub SRC_REVISION_ABL_感覚
;=================================================
;   SRC補正:ABL(感覚)
;-------------------------------------------------
;   範囲: 10%～250%
;-------------------------------------------------
; Input:
;  ARG					対象感覚
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_ABL_感覚(ARG)
#FUNCTION
#DIM L_TMP

SELECTCASE ARG
;-------------------------------------------------
	CASE 0
		L_TMP = 10
	CASE 1
		L_TMP = 30
	CASE 2
		L_TMP = 50
	CASE 3
		L_TMP = 150
	CASE 4 TO 8
		L_TMP = 150 + (10 * ARG)
	CASEELSE
		L_TMP = 250
;-------------------------------------------------
;	CASE 0
;		L_TMP = 10
;	CASE 1
;		L_TMP = 30
;	CASE 2
;		L_TMP = 50
;	CASE 3
;		L_TMP = 150
;	CASE 4 TO 8
;		L_TMP = 50 + (50 * ARG)
;	CASEELSE
;		L_TMP = 500
;-------------------------------------------------
;	CASE 0
;		L_TMP = 5
;	CASE 1
;		L_TMP = 10
;	CASE 2
;		L_TMP = 30
;	CASE 5
;		L_TMP = 70
;	CASE 4
;		L_TMP = 150
;	CASE 5 TO 8
;		L_TMP = 50 + (50 * ARG)
;	CASEELSE
;		L_TMP = 500
;-------------------------------------------------
ENDSELECT


RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_농락
;=================================================
;   SRC補正:ABL(농락)
;-------------------------------------------------
;   範囲: 20%～200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_ABL_농락(ARG)
#FUNCTION
#DIM L_TMP

SELECTCASE ABL:ARG:농락
	CASE 0 TO 8
		L_TMP = 20 + (20 * ABL:ARG:농락)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_EXP_部位経験
;=================================================
;   SRC補正:EXP(部位経験)
;-------------------------------------------------
;   範囲: 30%～150%
;-------------------------------------------------
; Input:
;  ARG					対象部位経験
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_EXP_部位経験(ARG)
#FUNCTION
#DIM L_TMP

;--経験をみる
SELECTCASE ARG
	CASE IS < EXPLV:1
		L_TMP = 030
	CASE IS < EXPLV:2
		L_TMP = 060
	CASE IS < EXPLV:3
		L_TMP = 090
	CASE IS < EXPLV:4
		L_TMP = 120
	CASE IS < EXPLV:5
		L_TMP = 130
	CASEELSE
		L_TMP = 150
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_PALAM_욕정
;=================================================
;   SRC補正:PALAM(욕정)
;-------------------------------------------------
;   範囲: 20%～100% (通常)
;   範囲: 10%～60% (低)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:9				処理区分(1:通常 2:補正低)
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_PALAM_욕정(ARG,ARG:9 = 1)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = PALAM:ARG:욕정
;--PALAM:욕정をみる

IF ARG:9 == 2
;補正:低
	SELECTCASE L_VAL
		CASE IS < PALAMLV:1
			L_TMP = 010
		CASE IS < PALAMLV:2
			L_TMP = 020
		CASE IS < PALAMLV:4
			L_TMP = 040
		CASE IS < PALAMLV:6
			L_TMP = 060
		CASEELSE
			L_TMP = 080
	ENDSELECT
ELSE
;補正:通常
	SELECTCASE L_VAL
		CASE IS < PALAMLV:1
			L_TMP = 020
		CASE IS < PALAMLV:2
			L_TMP = 030
		CASE IS < PALAMLV:4
			L_TMP = 050
		CASE IS < PALAMLV:6
			L_TMP = 070
		CASEELSE
			L_TMP = 100
	ENDSELECT
ENDIF

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_PALAM_윤활
;=================================================
;   SRC補正:PALAM(윤활)
;-------------------------------------------------
;   範囲: 10%～140%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_PALAM_윤활(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = PALAM:ARG:윤활
;--PALAM:욕정をみる

SELECTCASE L_VAL
	CASE IS < PALAMLV:1
		L_TMP = 010
	CASE IS < PALAMLV:2
		L_TMP = 040
	CASE IS < PALAMLV:4
		L_TMP = 060
	CASE IS < PALAMLV:6
		L_TMP = 100
	CASEELSE
		L_TMP = 140
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_PALAM_치정
;=================================================
;   SRC補正:PALAM(치정)
;-------------------------------------------------
;   範囲:100%～60%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_PALAM_치정(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

;PALAM:치정をみる
L_VAL = TR_GETPALAMLV("치정", ARG)

;恥ずかしさを感じれば感じるほどあまり恥ずかしく無くなる
SELECTCASE L_VAL
	CASE 0 TO 4
		L_TMP = 100 - (010 * L_VAL)
	CASEELSE
		L_TMP = 060
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_ABL_매료기능
;=================================================
;   SRC補正:ABL(매료기능)
;-------------------------------------------------
;   範囲: 10%～150%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_매료기능(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:매료기능

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 10 +(15 * L_VAL)
	CASEELSE
		L_TMP = 150
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_욕망
;=================================================
;   SRC補正:ABL(욕망)
;-------------------------------------------------
;   範囲: 20%～200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_욕망(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:욕망

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_기교
;=================================================
;   SRC補正:ABL(기교)
;-------------------------------------------------
;   範囲: 20%～200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_기교(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:기교

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_ABL_순종
;=================================================
;   SRC補正:ABL(순종)
;-------------------------------------------------
;   範囲: 20%～200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_순종(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:순종

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_친밀
;=================================================
;   SRC補正:ABL(친밀)
;-------------------------------------------------
;   範囲: 20%～200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_친밀(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:친밀

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_相殺
;=================================================
;   SRC補正:相殺
;-------------------------------------------------
;   マイナスパラメータの相殺に使われる
;   範囲:130%～0%
;-------------------------------------------------
; Input:
;  ARG:0				対象値
;  ARG:1				対象キャラ
;  ARG:9				処理타입(0:通常 1:性関連)
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_相殺(ARG,ARG:1,ARG:9 = 0)
#FUNCTION
#DIM L_P
#DIM L_T
#DIM L_VAL
#DIM L_SUM
#DIM L_TMP
#DIM L_倍率

L_P = MASTER
L_T = ARG:1
L_VAL = ARG

;-----------------------------
;- ポイント計算
;-----------------------------
L_SUM = 0

;- ABLで加算(範囲:0～9 * 個数)
L_SUM += ABL:L_T:친밀
L_SUM += ABL:L_T:순종
L_SUM += ABL:L_T:욕망
L_SUM += ABL:L_P:매료기능

;무드レベル
L_SUM += VAR_MOOD_LV(L_T)

;- 陥落状態で更に加算(基礎値5 * 個数)
SIF MARK:L_T:음란각인
	L_SUM += 5
SIF MARK:L_T:함락각인
	L_SUM += 5

;- 절정強度で更に加算(基礎値3 * 範囲:0～3)
L_SUM += 3 * TCVAR:L_T:절정強度

IF !TCVAR:L_T:절정強度
	;- 반발각인で減算(基礎値5 * 範囲:0～3)
	L_SUM -= 5 * MARK:L_T:반발각인
ENDIF

;最低は-20
L_SUM = MAX(L_SUM, -20)

;-----------------------------
;- 倍率計算
;-----------------------------

;- ()内に0になる必要ポイント数をセット(例:20の場合は、上記計算で20ポイントあれば完全に相殺)

L_TMP=0
SELECTCASE ARG:9
	;性関連
	CASE 1
		L_TMP = 100 / (20)
	CASEELSE
		L_TMP = 100 / (10)
ENDSELECT

;- 計算
L_倍率 = MAX(100 - (L_TMP * L_SUM) , 0)


;-----------------------------
;- 相殺
;-----------------------------
L_VAL = (L_VAL * L_倍率) / 100

;- 特殊減算
L_VAL -= 10 * (ABL:L_T:욕망 + ABL:L_T:순종 + ABL:L_T:봉사정신)
L_VAL = MAX(L_VAL, 0)

RETURNF L_VAL

;=================================================
;   sub SRC_REVISION_性行動
;=================================================
;   SRC補正:性行動
;-------------------------------------------------
;   性行動に関係するマイナスパラメータの変動に使われる
;   範囲:?
;-------------------------------------------------
; Input:
;  ARG:0				対象値
;  ARG:1				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_性行動(ARG,ARG:1)
#DIM L_TMP
#DIM L_VAL
#DIM L_BASE
#FUNCTION

L_TMP = 0
L_VAL = ARG

;- 貞操度
L_BASE=20
SIF TALENT:(ARG:1):정조관념
	L_TMP += L_BASE
SIF TALENT:(ARG:1):정조관념없음
	L_TMP -= L_BASE

;- 性的흥미
L_BASE=20
SIF TALENT:(ARG:1):호기심
	L_TMP -= L_BASE
SIF TALENT:(ARG:1):보수적
	L_TMP += L_BASE

;- 計算
L_VAL = C_TIMES(L_VAL, 100 + L_TMP)


RETURNF L_VAL


;-------------------------------------------------------------------------------
;   SRC共通
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_COMMON_肉体接触系
;=================================================
;   SRC共通:肉体接触系
;-------------------------------------------------
;   肉体接触系の共通処理を纏める。
;   下記に対して入力値を基に反映する
;   ◆ 욕정・호의・굴복・불쾌
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:5				入力値
;  ARG:9				オリジナル値(SOURCE)
;  ARG:3				区分(0:通常 1:奉仕)
;-------------------------------------------------
@SRC_COMMON_肉体接触系(ARG,ARG:5 = 0,ARG:9 = 0,ARG:3 = 0)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P

L_P = PLAYER
L_SRC = ARG:5

SIF !L_SRC
	RETURN

;- 退避(オリジナルSOURCE)
L_OLD = ARG:9

;-----------------------------
;-- 共通倍率
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_基本倍率 += SRC_REVISION_ABL_욕망(ARG)
L_基本倍率 += SRC_REVISION_ABL_매료기능(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)


;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_친밀(ARG)
CUP:ARG:욕정 += C_TIMES(L_SRC / 2, L_倍率)


;-----------------------------
;-- CUP取得:호의
;-----------------------------
CUP:ARG:호의 += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:굴복
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_순종(ARG)
SIF ARG:3
	L_倍率 *= 3
CUP:ARG:굴복 += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:불쾌
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)

CUP:ARG:불쾌 += L_OLD


;=================================================
;   sub SRC_COMMON_征服系
;=================================================
;   SRC共通:征服系
;-------------------------------------------------
;   征服系の共通処理を纏める。
;   下記に対して入力値を基に反映する
;   ◆ 対象者:굴복・욕정・호의
;   ◆ 実行者:유혹
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:5				入力値
;  ARG:3				区分(0:通常 1:純愛)
;-------------------------------------------------
@SRC_COMMON_征服系(ARG,ARG:5 = 0,ARG:3 = 0)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = ARG:5

SIF !L_SRC
	RETURN

;-----------------------------
;-- CUP取得:굴복
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_순종(ARG)
CUP:ARG:굴복 += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:욕정 += L_TMP

;-----------------------------
;-- CUP取得:호의
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_친밀(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
SIF ARG:3
	L_TMP *= 2
CUP:ARG:호의 += L_TMP

;-----------------------------
;-- CUP取得:유혹(相手)
;-----------------------------
CUP:L_P:유혹 += L_TMP


;=================================================
;   sub SRC_COMMON_部位快感
;=================================================
;   SRC共通:部位快感
;-------------------------------------------------
;   範囲:???%～???%
;-------------------------------------------------
; Input:
;  ARG:0				対象者
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_COMMON_部位快感(ARG)
#FUNCTION
#DIM L_TMP

L_TMP = 10 * MARK:ARG:음란각인

;陥落前のみ有効
IF !MARK:ARG:함락각인

	;자제심
	SIF TALENT:ARG:자제심
		L_TMP -= 10

ENDIF


RETURNF L_TMP


;-------------------------------------------------------------------------------
;   SRC反映(個別)
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_REFLECT_쾌C
;=================================================
;   SRC反映:쾌C
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_쾌C(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:쾌C

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｃ감각)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｃ경험)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｃ感度")

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_쾌C

;更新
SOURCE:ARG:쾌C = L_SRC

;-----------------------------
;-- CUP取得:쾌C
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
CUP:ARG:쾌C += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:윤활
;-----------------------------
CUP:ARG:윤활 += C_TIMES(L_SRC / 2, L_倍率)

;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_농락(ARG)
CUP:ARG:욕정 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_쾌V
;=================================================
;   SRC反映:쾌V
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_쾌V(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:쾌V

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｖ감각)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｖ경험)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｖ感度")

L_基本倍率 = C_TIMES(L_基本倍率, SRC_REVISION_PALAM_윤활(ARG))

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_쾌V

;更新
SOURCE:ARG:쾌V = L_SRC

;-----------------------------
;-- CUP取得:쾌V
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
CUP:ARG:쾌V += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:윤활
;-----------------------------
CUP:ARG:윤활 += C_TIMES(L_SRC / 2, L_倍率)

;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_농락(ARG) * 2
CUP:ARG:욕정 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_쾌A
;=================================================
;   SRC反映:쾌A
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_쾌A(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:쾌A

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ａ감각)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ａ경험)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ａ感度")

L_基本倍率 = C_TIMES(L_基本倍率, SRC_REVISION_PALAM_윤활(ARG))

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_쾌A

;更新
SOURCE:ARG:쾌A = L_SRC

;-----------------------------
;-- CUP取得:쾌A
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG,2)
CUP:ARG:쾌A += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:윤활
;-----------------------------
CUP:ARG:윤활 += C_TIMES(L_SRC / 4, L_倍率)


;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_농락(ARG) * 2
CUP:ARG:욕정 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_쾌B
;=================================================
;   SRC反映:쾌B
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_쾌B(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:쾌B

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｂ감각)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｂ경험)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｂ感度")

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_쾌B

;更新
SOURCE:ARG:쾌B = L_SRC

;-----------------------------
;-- CUP取得:쾌B
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
CUP:ARG:쾌B += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:윤활
;-----------------------------
CUP:ARG:윤활 += C_TIMES(L_SRC / 2, L_倍率)


;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_농락(ARG)
CUP:ARG:욕정 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_液体
;=================================================
;   SRC反映:液体
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_液体(ARG)
#DIM L_SRC

L_SRC = SOURCE:ARG:液体

;-----------------------------
;-- CUP取得:윤활
;-----------------------------
CUP:ARG:윤활 += L_SRC



;=================================================
;   sub SRC_REFLECT_接触
;=================================================
;   SRC反映:接触
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_接触(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:接触

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_친밀(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:接触 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD)

;-----------------------------
;-- CUP取得:유혹 (実行者)
;-----------------------------

CUP:L_P:유혹 += L_SRC


;=================================================
;   sub SRC_REFLECT_性行為
;=================================================
;   SRC反映:性行為
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_性行為(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
#DIM L_TMP

L_P = PLAYER
L_SRC = SOURCE:ARG:性行為

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_친밀(ARG)
L_基本倍率 += SRC_REVISION_ABL_기교(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:性行為 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD)


;-----------------------------
;-- CUP取得:습득
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_ABL_욕망(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:습득 += L_TMP

;-----------------------------
;-- CUP取得:유혹 (実行者)
;-----------------------------
CUP:L_P:유혹 += (L_SRC + L_TMP) / 2

;-----------------------------
;-- CUP取得:습득 (実行者)
;-----------------------------
CUP:L_P:습득 += L_SRC

;-----------------------------
;-- CUP取得:욕정 (実行者)
;-----------------------------
CUP:L_P:욕정 += L_SRC


;=================================================
;   sub SRC_REFLECT_性奉仕
;=================================================
;   SRC反映:性奉仕
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_性奉仕(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:性奉仕

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_친밀(ARG)
L_基本倍率 += SRC_REVISION_ABL_기교(ARG)

;소악마
SIF TALENT:ARG:소악마
	L_基本倍率 += 20

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:性奉仕 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD,1)


;-----------------------------
;-- CUP取得:유혹
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_매료기능(ARG)
LOCAL:1 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:유혹 += LOCAL:1


;-----------------------------
;-- CUP取得:습득
;-----------------------------
L_倍率 = 100
LOCAL:2 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:습득 += LOCAL:2

;-----------------------------
;-- CUP取得:유혹 (実行者)
;-----------------------------
CUP:L_P:유혹 += (L_SRC + LOCAL:2) / 2

;-----------------------------
;-- CUP取得:습득 (実行者)
;-----------------------------
CUP:L_P:습득 += L_SRC / 2

;-----------------------------
;-- CUP取得:욕정(実行者)
;-----------------------------
CUP:L_P:욕정 += LOCAL:1

;-----------------------------
;-- CUP取得:반감
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG,1)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)
CUP:ARG:반감 += L_OLD


;=================================================
;   sub SRC_REFLECT_고통
;=================================================
;   SRC反映:고통
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_고통(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:고통

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------
;無し

;-----------------------------
;-- CUP取得:고통
;-----------------------------
CUP:ARG:고통 += L_SRC

;-----------------------------
;-- CUP取得:욕정
;-----------------------------

LOCAL:1 = L_SRC

;CHK:마조끼
SELECTCASE ABL:ARG:마조끼
	CASE 0 TO 9
		LOCAL:1 = C_TIMES(LOCAL:1, (020 * ABL:ARG:마조끼))
	CASEELSE
		TIMES LOCAL:1 , 3.00
ENDSELECT

;CHK:調教者の사디
SIF TALENT:L_P:사디
	LOCAL:1 = C_TIMES(LOCAL:1, 100 + (050 * TALENT:L_P:사디))

CUP:ARG:욕정 += LOCAL:1


;-----------------------------
;-- CUP取得:공포
;-----------------------------

LOCAL:2 = L_SRC / 20

;CHK:마조끼
SELECTCASE ABL:ARG:마조끼
	CASE 0 TO 8
		LOCAL:2 = C_TIMES(LOCAL:2, 100 - (012 * ABL:ARG:마조끼))
	CASEELSE
		LOCAL:2 = 0
ENDSELECT

CUP:ARG:공포 += LOCAL:2


;-----------------------------
;-- CUP取得:굴복
;-----------------------------

LOCAL:3 = L_SRC + LOCAL:2

CUP:ARG:굴복 += LOCAL:3

;-----------------------------
;-- CUP取得:반감
;-----------------------------

;- SRC補正:相殺
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)

;CHK:마조끼
SELECTCASE ABL:ARG:마조끼
	CASE 0 TO 2
		L_OLD = C_TIMES(L_OLD, 200 - (050 * ABL:ARG:마조끼))
	CASEELSE
		L_OLD = C_TIMES(L_OLD, 100 - (012 * ABL:ARG:마조끼))
ENDSELECT

CUP:ARG:반감 += L_OLD



;=================================================
;   sub SRC_REFLECT_露出
;=================================================
;   SRC反映:露出
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_露出(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
#DIM L_TMP
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:露出

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;羞恥心
L_TMP = 050
SIF TALENT:ARG:부끄러움없음
	L_SRC = C_TIMES(L_SRC, 100 - L_TMP)
SIF TALENT:ARG:부끄럼쟁이
	L_SRC = C_TIMES(L_SRC, 100 + L_TMP)

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_매료기능(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:露出 = L_SRC

;-----------------------------
;-- CUP取得:욕정
;-----------------------------

LOCAL:1 = L_SRC

;CHK:노출벽
SELECTCASE ABL:ARG:노출벽
	CASE 0 TO 8
		LOCAL:1 = C_TIMES(LOCAL:1, (020 * ABL:ARG:노출벽))
	CASEELSE
		TIMES LOCAL:1 , 2.00
ENDSELECT

CUP:ARG:욕정 += LOCAL:1

;-----------------------------
;-- CUP取得:유혹
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(L_P)
LOCAL:2 = C_TIMES(LOCAL:1, L_倍率)
CUP:ARG:유혹 += LOCAL:2

;-----------------------------
;-- CUP取得:치정
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_치정(ARG)
LOCAL:3 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:치정 += LOCAL:3

;-----------------------------
;-- CUP取得:굴복
;-----------------------------
LOCAL:4 = L_SRC + LOCAL:3
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_친밀(ARG)

LOCAL:4 = C_TIMES(LOCAL:4, L_倍率)
CUP:ARG:굴복 += LOCAL:4


;-----------------------------
;-- CUP取得:반감
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)
CUP:ARG:반감 += L_OLD


;=================================================
;   sub SRC_REFLECT_征服
;=================================================
;   SRC反映:征服
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_征服(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:征服

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_매료기능(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:征服 = L_SRC

;-----------------------------
;-- CUP取得:xxx(征服系)
;-----------------------------

;- SRC共通:征服系
CALL SRC_COMMON_征服系(ARG,L_SRC)

;-----------------------------
;-- CUP取得:억울
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
L_TMP = SRC_REVISION_相殺(L_TMP,ARG)
CUP:ARG:억울 += L_TMP

;-----------------------------
;-- CUP取得:반감
;-----------------------------
L_TMP = SRC_REVISION_相殺(L_OLD,ARG)
L_TMP = SRC_REVISION_性行動(L_TMP,ARG)
CUP:ARG:반감 += L_TMP


;=================================================
;   sub SRC_REFLECT_純愛
;=================================================
;   SRC反映:純愛
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_純愛(ARG)
#DIM L_基本倍率
#DIM L_SRC
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:純愛

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_매료기능(L_P)
L_基本倍率 += SRC_REVISION_ABL_순종(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:純愛 = L_SRC

;-----------------------------
;-- CUP取得:xxx(征服系)
;-----------------------------

;- SRC共通:征服系
CALL SRC_COMMON_征服系(ARG,L_SRC,1)


;=================================================
;   sub SRC_REFLECT_습득
;=================================================
;   SRC反映:절정
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_습득(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:습득

;-----------------------------
;-- SOURCE更新
;-----------------------------
;特に無し

;-----------------------------
;-- CUP取得:굴복
;-----------------------------

CUP:ARG:습득 += L_SRC




;=================================================
;   sub SRC_REFLECT_절정
;=================================================
;   SRC反映:절정
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_절정(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:절정

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_매료기능(L_P) / 6
L_基本倍率 += SRC_REVISION_ABL_욕망(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:절정 = L_SRC

;-----------------------------
;-- CUP取得:굴복
;-----------------------------
CUP:ARG:굴복 += L_SRC


;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:욕정 += L_TMP


;-----------------------------
;-- CUP取得:치정
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_치정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:치정 += L_TMP


;-----------------------------
;-- CUP取得:억울
;-----------------------------
L_TMP = L_SRC / 10
L_TMP = SRC_REVISION_相殺(L_TMP,ARG,1)
CUP:ARG:억울 += L_TMP


;=================================================
;   sub SRC_REFLECT_精液
;=================================================
;   SRC反映:精液
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_精液(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P
#DIM L_ADD
#DIM L_VAL

L_P = PLAYER
L_SRC = SOURCE:ARG:精液
L_ADD=0
L_VAL=1

;-----------------------------
;-- SOURCE更新
;-----------------------------
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_매료기능(L_P)
L_基本倍率 += SRC_REVISION_ABL_농락(ARG)
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:精液 = L_SRC

IF TCVAR:ARG:被射精部位 == [[口上:主人Ｖ射精]]
	CFLAG:ARG:子宮精液貯蔵量       += L_SRC
	CFLAG:ARG:子宮精液貯蔵量合計 += L_SRC
	SOURCE:ARG:子宮精液 = L_SRC
	SOURCE:ARG:精液 = 0
	;陥落後の場合、効果追加
	IF MARK:ARG:함락각인
;		L_ADD=L_SRC
	;陥落前は効果減少
	ELSE
		TIMES L_SRC, 0.50
	ENDIF
ENDIF

IF MARK:ARG:함락각인
	L_VAL = 3
ELSEIF MARK:ARG:예속각인
	L_VAL = 2
ENDIF


;-----------------------------
;-- CUP取得:굴복
;-----------------------------
CUP:ARG:굴복 += (L_SRC + L_ADD) * L_VAL


;-----------------------------
;-- CUP取得:호의
;-----------------------------
SIF MARK:ARG:함락각인
	CUP:ARG:호의 += (L_SRC + L_ADD) * 3

;-----------------------------
;-- CUP取得:욕정
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:욕정 += L_TMP


;-----------------------------
;-- CUP取得:치정
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_치정(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)

CUP:ARG:치정 += L_TMP


;-----------------------------
;-- CUP取得:억울
;-----------------------------
L_TMP = L_SRC / 10
L_TMP = SRC_REVISION_相殺(L_TMP,ARG,1)
CUP:ARG:억울 += L_TMP


;=================================================
;   sub SRC_REFLECT_子宮精液
;=================================================
;   SRC反映:子宮精液
;-------------------------------------------------
;   ダミー関数。
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_子宮精液(ARG)
;ダミー



;=================================================
;   sub SRC_REFLECT_歓楽
;=================================================
;   SRC反映:歓楽
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_歓楽(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:歓楽

;-----------------------------
;-- SOURCE更新
;-----------------------------
L_基本倍率 = 100
;L_基本倍率 += (010 * ABL:L_P:話術)
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:歓楽 = L_SRC

;-----------------------------
;-- CUP取得:호의
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_친밀(ARG)
L_倍率 += SRC_REVISION_ABL_매료기능(L_P)
CUP:ARG:호의 += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:욕정
;-----------------------------

L_TMP = ABL:L_P:매료기능 + ABL:ARG:순종

;- 性別が違う or [実行者の매료기능 + 対象者の순종]の合計が4以上
IF (VAR_HETEROSEX(ARG,L_P) == 0) || (L_TMP >= 4)

	L_倍率 = 0
	L_倍率 += (010 * L_TMP)
	CUP:ARG:욕정 += C_TIMES(L_SRC, L_倍率)

ENDIF

;-----------------------------
;-- CUP取得:유혹(相手)
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_욕정(ARG)
L_倍率 += SRC_REVISION_ABL_매료기능(L_P)
CUP:L_P:유혹 += C_TIMES(L_SRC, L_倍率) / 3


;=================================================
;   sub SRC_REFLECT_생활
;=================================================
;   SRC反映:생활
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_생활(ARG)
#DIM L_SRC
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:생활

;-----------------------------
;-- SOURCE更新
;-----------------------------
;なし

;-----------------------------
;-- CUP取得:생활
;-----------------------------
CUP:ARG:생활 += L_SRC
CUP:L_P:생활 += L_SRC


;-------------------------------------------------------------------------------
;   SRC反映(その他)
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_CUP_EXTRA
;=================================================
;   SRC反映:CUP(特殊)
;-------------------------------------------------
;   CUPを纏めて調整する
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_CUP_EXTRA(ARG)
#DIM L_T
#DIM L_TMP
#DIM L_BASE
#DIMS L_STR

;- 初期化
L_T = ARG
L_STR =

;-------------------------------------------------
;- 反映:個人PALAM
;-------------------------------------------------

;-----------------------------
;- 計算:욕정
;-----------------------------

L_TMP = TCVAR:L_T:절정加算値

CUP:L_T:욕정 += (L_TMP / 2)

SIF TCVAR:L_T:절정強度
	CUP:L_T:욕정 += (TCVAR:L_T:절정強度 * 300)

;-----------------------------
;- 計算:윤활
;-----------------------------

;- 女の場合
SIF VAR_SEX(L_T)
	CUP:L_T:윤활 += (CUP:L_T:욕정 / 2) + (L_TMP / 2)

;-----------------------------
;- 補正:その他
;-----------------------------


;-- 陥落素質
;陥落
L_TMP = MARK:L_T:함락각인
SELECTCASE L_TMP
	CASE IS > 0
		TIMES CUP:L_T:억울 , 0.50
ENDSELECT

;-- MASTERの場合
IF L_T == MASTER
	;- マイナスPALAMは0
	CUP:L_T:억울 = 0
ENDIF


;-------------------------------------------------
;- 反映:対人PALAM
;-------------------------------------------------

;-----------------------------
;- 事前チェック
;-----------------------------

;- 対象者がMASTERの場合
IF (L_T == MASTER)
	;- 対人PALAM無し
	CUP:L_T:호의 = 0
	CUP:L_T:굴복 = 0
	CUP:L_T:공포 = 0
	CUP:L_T:반감 = 0
	CUP:L_T:불쾌 = 0
	RETURN
ENDIF

;-----------------------------
;- 補正:刻印
;-----------------------------

;陥落
L_TMP = MARK:L_T:함락각인
SELECTCASE L_TMP
	CASE IS > 0
		CUP:L_T:반감 = C_TIMES(CUP:L_T:반감, 100 - (020 * L_TMP))
		CUP:L_T:불쾌 = C_TIMES(CUP:L_T:불쾌, 100 - (020 * L_TMP))
	CASE 0
		TIMES CUP:L_T:반감 , 1.20
		TIMES CUP:L_T:불쾌 , 1.20
ENDSELECT

;-----------------------------
;- 補正:素質
;-----------------------------
;합의
IF TALENT:L_T:합의
	TIMES CUP:L_T:반감 , 0.50
	TIMES CUP:L_T:불쾌 , 0.50
ENDIF
;조교완료
IF TALENT:L_T:조교완료
	TIMES CUP:L_T:반감 , 0.50
	TIMES CUP:L_T:불쾌 , 0.50
ENDIF
;비합의
IF TALENT:L_T:비합의 && TALENT:L_T:조교완료 == 0
	TIMES CUP:L_T:반감 , 700
	TIMES CUP:L_T:불쾌 , 700
ENDIF



;プライド
IF TALENT:L_T:프라이드높음
	TIMES CUP:L_T:반감 , 2.00
	TIMES CUP:L_T:불쾌 , 2.00
ELSEIF TALENT:L_T:프라이드낮음
	TIMES CUP:L_T:반감 , 0.50
	TIMES CUP:L_T:불쾌 , 0.50
ENDIF

;유치
IF TALENT:L_T:유치
	TIMES CUP:L_T:반감 , 0.75
	TIMES CUP:L_T:불쾌 , 0.75
	TIMES CUP:L_T:호의 , 1.25
ENDIF

;性的흥미(보수적/호기심)
L_BASE=020
SIF TALENT:L_T:보수적
	CUP:L_T:습득 = C_TIMES(CUP:L_T:습득, 100 - L_BASE)
SIF TALENT:L_T:호기심
	CUP:L_T:습득 = C_TIMES(CUP:L_T:습득, 100 + L_BASE)

;습득速度(보수적/호기심)
SIF TALENT:L_T:습득느림
	TIMES CUP:L_T:습득 , 0.8
SIF TALENT:L_T:습득빠름
	TIMES CUP:L_T:습득 , 1.2

;습득速度(백치녀/천재)
SIF TALENT:L_T:백치녀
	TIMES CUP:L_T:습득 , 0.5
SIF TALENT:L_T:천재
	TIMES CUP:L_T:습득 , 1.5


;態度(정직)
IF TALENT:L_T:정직
	TIMES CUP:L_T:호의 , 1.20
	TIMES CUP:L_T:굴복 , 1.20
ENDIF

;応答(얌전함)
IF TALENT:L_T:얌전함
	TIMES CUP:L_T:반감 , 0.80
	TIMES CUP:L_T:불쾌 , 0.80
ENDIF

;社交性(소극적/눈에띄고싶음)
L_BASE=025
SIF TALENT:L_T:소극적
	CUP:L_T:치정 = C_TIMES(CUP:L_T:치정, 100 - L_BASE)
SIF TALENT:L_T:눈에띄고싶음
	CUP:L_T:치정 = C_TIMES(CUP:L_T:치정, 100 + L_BASE)

;思考(낙관적)
IF TALENT:L_T:낙관적 > 0
	TIMES CUP:L_T:반감 , 0.80
	TIMES CUP:L_T:불쾌 , 0.80
ENDIF

;남미새
IF TALENT:L_T:남미새 && !VAR_SEX(PLAYER)
	TIMES CUP:L_T:호의 , 1.20
	TIMES CUP:L_T:굴복 , 1.20
ENDIF
;동성애
IF TALENT:L_T:동성애 && VAR_SEX(PLAYER)
	TIMES CUP:L_T:호의 , 1.20
	TIMES CUP:L_T:굴복 , 1.20
ENDIF

;금사빠
IF TALENT:L_T:금사빠 
	TIMES CUP:L_T:호의 , 2
	TIMES CUP:L_T:굴복 , 2
ENDIF

;즉각타락
IF TALENT:L_T:즉각타락
	TIMES CUP:L_T:호의 , 3
	TIMES CUP:L_T:굴복 , 3
ENDIF

;애석
IF TALENT:L_T:애석
	TIMES CUP:L_T:호의 , 2
ENDIF

;경계심
IF TALENT:L_T:경계심 
	TIMES CUP:L_T:호의 , 0.80
	TIMES CUP:L_T:굴복 , 0.80
ENDIF

;인간불신
IF TALENT:L_T:인간불신
	TIMES CUP:L_T:호의 , 0.50
	TIMES CUP:L_T:굴복 , 0.50
ENDIF

;강간원망
IF TALENT:L_T:강간원망
	TIMES CUP:L_T:반감 , 0.60
	TIMES CUP:L_T:공포 , 0.60
	TIMES CUP:L_T:굴복 , 1.2
ENDIF


;陥落前のみ有効
IF !MARK:L_T:함락각인

	;무관심
	IF TALENT:L_T:무관심
		;個人PALAM
		TIMES CUP:L_T:욕정 , 0.80
		;対人PALAM
		TIMES CUP:L_T:호의 , 0.80
		TIMES CUP:L_T:굴복 , 0.80
		TIMES CUP:L_T:공포 , 0.80
		TIMES CUP:L_T:반감 , 0.80
		TIMES CUP:L_T:불쾌 , 0.80
	ENDIF

	;態度(반항적)
	IF TALENT:L_T:반항적
		TIMES CUP:L_T:호의 , 0.80
		TIMES CUP:L_T:굴복 , 0.80
	ENDIF

	;応答(건방짐)
	IF TALENT:L_T:건방짐
		TIMES CUP:L_T:호의 , 0.80
		TIMES CUP:L_T:굴복 , 0.80
	ENDIF

	;남혐/여혐
	IF (TALENT:L_T:남혐 && !VAR_SEX(PLAYER)) || (TALENT:L_T:여혐 && VAR_SEX(PLAYER))
		TIMES CUP:L_T:호의 , 0.80
		TIMES CUP:L_T:굴복 , 0.80
	ENDIF


	;바이以外
	IF !TALENT:L_T:바이
		;性別が同じ
		IF VAR_SEX(L_T) == VAR_SEX(PLAYER)
			TIMES CUP:L_T:호의 , 0.90
			TIMES CUP:L_T:굴복 , 0.90
		ENDIF
	ENDIF

	;저항
	IF TALENT:L_T:저항
		TIMES CUP:L_T:욕정 , 0.80
		TIMES CUP:L_T:반감 , 1.20
	ENDIF

	;억압
	IF TALENT:L_T:억압
		TIMES CUP:L_T:욕정 , 0.90
		TIMES CUP:L_T:억울 , 1.10
	ENDIF

	;헌신적
	IF TALENT:L_T:헌신적
		TIMES CUP:L_T:호의 , 1.20
		TIMES CUP:L_T:굴복 , 1.20
	ENDIF

	;자애
	IF TALENT:L_T:자애
		TIMES CUP:L_T:호의 , 1.40
		TIMES CUP:L_T:굴복 , 1.40
	ENDIF

	;思考(비관적)
	IF TALENT:L_T:비관적
		TIMES CUP:L_T:억울 , 1.10
	ENDIF
	;백합공주
	IF TALENT:L_T:백합공주
		TIMES CUP:L_T:호의 , 0.50
		TIMES CUP:L_T:굴복 , 0.50
	ENDIF


ENDIF

;陥落後のみ有効
IF MARK:L_T:함락각인
	;츤데레
	SIF TALENT:L_T:츤데레
		TIMES CUP:L_T:호의 , 1.25
;	;얀데레
;	SIF TALENT:L_T:얀데레
;		TIMES CUP:L_T:호의 , 2.00
ENDIF

;선을넘지않음
IF TALENT:L_T:선을넘지않음
	;陥落してたら上昇/未陥落なら減少
	L_TMP = MARK:L_T:함락각인

	;個人PALAM
	CUP:L_T:욕정 = C_TIMES(CUP:L_T:욕정, 80 + (040 * L_TMP))
	;対人PALAM
	CUP:L_T:호의 = C_TIMES(CUP:L_T:호의, 80 + (040 * L_TMP))
	CUP:L_T:굴복 = C_TIMES(CUP:L_T:굴복, 80 + (040 * L_TMP))
ENDIF

;貞操度(정조관념)
IF TALENT:L_T:정조관념
	;陥落してたら上昇/未陥落なら減少
	L_TMP = MARK:L_T:함락각인

	IF L_TMP
		;個人PALAM
		CUP:L_T:욕정 = C_TIMES(CUP:L_T:욕정, 80 + (040 * L_TMP))
		;対人PALAM
		CUP:L_T:호의 = C_TIMES(CUP:L_T:호의, 80 + (040 * L_TMP))
		CUP:L_T:굴복 = C_TIMES(CUP:L_T:굴복, 80 + (040 * L_TMP))
	ELSE
		TIMES CUP:L_T:억울 , 1.20
		TIMES CUP:L_T:반감 , 1.20
		TIMES CUP:L_T:불쾌 , 1.20
	ENDIF

;貞操度(정조관념없음)
ELSEIF TALENT:L_T:정조관념없음

	TIMES CUP:L_T:억울 , 0.80
	TIMES CUP:L_T:반감 , 0.80
	TIMES CUP:L_T:불쾌 , 0.80

ENDIF

IF TALENT:L_T:ＮＴＬ 
	TIMES CUP:L_T:반감 , 0.5
	TIMES CUP:L_T:불쾌 , 0.5
	TIMES CUP:L_T:호의 , 2.00
	TIMES CUP:L_T:굴복 , 2.00
ENDIF


;-----------------------------
;- 補正:刻印
;-----------------------------



;-------------------------------------------------
;- 反映:バランス調整
;-------------------------------------------------
;  キャラ情報ではなく、システム的な意味でのバランス調整をここで行う
;-------------------------------------------------
TIMES CUP:L_T:욕정 , 0.25
TIMES CUP:L_T:호의 , 0.50
TIMES CUP:L_T:굴복 , 0.50

;- 음란각인無しの場合は욕정・호의・굴복低下 (75%)
IF !MARK:L_T:음란각인
	TIMES CUP:L_T:욕정 , 0.75
	TIMES CUP:L_T:호의 , 0.75
	TIMES CUP:L_T:굴복 , 0.75
ENDIF

;- 예속각인無しの場合は호의・굴복低下 (75%)
IF !MARK:L_T:예속각인
	TIMES CUP:L_T:호의 , 0.75
	TIMES CUP:L_T:굴복 , 0.75
ENDIF

;- 暫定で함락각인無しの場合は호의が300以上を超えにくくする
IF !MARK:L_T:함락각인
	SELECTCASE CUP:L_T:호의
		CASE IS < 300
		CASE IS < 500
			TIMES CUP:L_T:호의 , 0.75
		CASE IS < 1000
			TIMES CUP:L_T:호의 , 0.5
		CASEELSE
			TIMES CUP:L_T:호의 , 0.4
	ENDSELECT
;- 暫定で함락각인有りの場合は호의、굴복が5000以上を超えにくくする
ELSE
	SELECTCASE CUP:L_T:호의
		CASE IS < 3000
		CASE IS < 5000
			TIMES CUP:L_T:호의 , 0.75
		CASE IS < 10000
			TIMES CUP:L_T:호의 , 0.5
		CASE IS < 15000
			TIMES CUP:L_T:호의 , 0.4
		CASEELSE
			TIMES CUP:L_T:호의 , 0.3
	ENDSELECT
	SELECTCASE CUP:L_T:굴복
		CASE IS < 3000
		CASE IS < 5000
			TIMES CUP:L_T:굴복 , 0.75
		CASE IS < 10000
			TIMES CUP:L_T:굴복 , 0.5
		CASE IS < 15000
			TIMES CUP:L_T:굴복 , 0.4
		CASEELSE
			TIMES CUP:L_T:굴복 , 0.3
	ENDSELECT
ENDIF

;主人以外
IF L_T != MASTER
	;性交有りの場合
	IF TEQUIP:L_T:ポイント性交
		;함락도合いで効果減少
		IF MARK:L_T:함락각인
			;スルー
		ELSEIF MARK:L_T:예속각인
			TIMES CUP:L_T:호의 , 0.60
			TIMES CUP:L_T:굴복 , 0.60
		ELSE
			TIMES CUP:L_T:호의 , 0.25
			TIMES CUP:L_T:굴복 , 0.25
		ENDIF
	ENDIF
ENDIF

;TIMES CUP:L_T:호의 , 0.50
;TIMES CUP:L_T:굴복 , 0.50


;=================================================
;   sub SRC_EXP
;=================================================
;   SRC反映:경험치
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_EXP(ARG)
#DIM L_TMP

SIF SOURCE:ARG:쾌C
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｃ경험") += 1

SIF SOURCE:ARG:쾌V
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｖ경험") += 1

SIF SOURCE:ARG:쾌A
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ａ경험") += 1

SIF SOURCE:ARG:쾌B
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｂ경험") += 1

IF CUP:ARG:고통 && (MARK:ARG:예속각인 || ABL:ARG:마조끼 || TALENT:ARG:마조)
	L_TMP=0
	SELECTCASE CUP:ARG:고통
		CASE IS >= 3000
			L_TMP = 4
		CASE IS >= 2000
			L_TMP = 3
		CASE IS >= 1000
			L_TMP = 2
		CASEELSE
			L_TMP = 1
	ENDSELECT
	L_TMP += ABL:ARG:마조끼
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"고통쾌락경험") += L_TMP
ENDIF




;=================================================
;   sub SRC_DOWNBASE
;=================================================
;   SRC反映:DOWNBASE
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_DOWNBASE(ARG)



BASE:ARG:체력 = LIMIT((BASE:ARG:체력 + DOWNBASE:ARG:체력), 0, MAXBASE:ARG:체력)

SIF BASE:ARG:체력 <= 50
	CFLAG:ARG:調教疲労フラグ = 1

