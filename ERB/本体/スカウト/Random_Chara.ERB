;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:Random_Chara.ERB
;	Facility	:ランダムキャラ設定
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;   002     2019/04/14      M.                      傾向安定のため改変
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

@Random_Chara_Set(ARG:0)
#DIM L_I
#DIM RCS_SST
#DIM RCS_body
#DIM RCS_bust
#DIM RCS_hip
#DIM RCS_TALENT1
#DIM RCS_TALENT2
#DIM RCS_RANDOM
#DIMS first1
#DIMS first2
#DIMS first3


L_I = ARG:0

RCS_RANDOM = RAND:16

IF RCS_RANDOM >= 14
	RCS_body = -3
	RCS_bust = -3
	RCS_hip = -3
ELSEIF RCS_RANDOM >= 12
	RCS_body = 3
	RCS_bust = 3
	RCS_hip = 3
ELSEIF RCS_RANDOM >= 9
	RCS_bust = -3
	RCS_hip = -3
ELSEIF RCS_RANDOM >= 6
	RCS_bust = 3 
	RCS_hip = 3
ELSE
ENDIF
	
;身体のサイズを指定
SELECTCASE  RAND:10 + RCS_body
	CASE is <= -3
		TALENT:L_I:작은체형 = 1
		TALENT:L_I:소인체형 = 1
	CASE is <= 0
		TALENT:L_I:작은체형 = 1
	CASE is >= 10
		SIF !TALENT:L_I:작은체형
			TALENT:L_I:장신 = 1
	CASEELSE
ENDSELECT

IF TALENT:L_I:오토코 == 0
	;胸のサイズを設定
	SELECTCASE  RAND:10 + RCS_bust
		CASE is <=0
			TALENT:L_I:절벽 = 1
		CASE 1,2,3
			TALENT:L_I:빈유 = 1
		CASE 7,8,9
			TALENT:L_I:거유 = 1
		CASE is >=10
			TALENT:L_I:폭유 = 1
	ENDSELECT
	;尻のサイズを設定
	SELECTCASE  RAND:10 + RCS_hip
		CASE 8,9
			TALENT:L_I:큰엉덩이 = 1
		CASE 10
			TALENT:L_I:거대엉덩이 = 1
	ENDSELECT
ENDIF

;----------------------------------------------------------
;----------------------------------------------------------
;素質関連（区画）
;----------------------------------------------------------
;----------------------------------------------------------
SIF TALENT:L_I:합의 == 0
	TALENT:L_I:비합의 = 1
IF TALENT:L_I:남성거주자 == 1
	CALL SET_RANDOM_TALENT_MENTAL_O(L_I)
ELSE
	CALL SET_RANDOM_TALENT_MENTAL(L_I)
	CALL SET_RANDOM_TALENT_SENCIBITY(L_I)
ENDIF



;----------------------------------------------------------
;----------------------------------------------------------
;詳細情報
;----------------------------------------------------------
;----------------------------------------------------------
CALL CHARACTER_SCOUT_PERSONAL(L_I)
RETURN 0



;----------------------------------------------------------
;----------------------------------------------------------
;素質関連（性格）
;----------------------------------------------------------
;----------------------------------------------------------
;FUNC_FISHER_YATES_SHAFFLEによりランダム配列を生成することで抽選の重複を排除した
;この時点ではランダムキャラはタレントがまっさらな状態なはずだが
;念のために素質判定を入れて弾いておく
@SET_RANDOM_TALENT_MENTAL(L_I)
#DIM L_I
#DIM TAL_NUM
#DIM TAL_INDEX, 55
;TALENT傾向: 矛盾したTALENTが出にくくなるように、TALENTの性格ごとにTYPE区分
;より安定化させるなら重み付けする
#DIM L_ENDCOUNT
#DIM TYPEネアカ度
#DIM TYPEスケベ度
#DIM TYPE優しさ
#DIM TYPE強さ
#DIM L_SUM_ALLTYPE

CALLF FUNC_FISHER_YATES_SHAFFLE(TAL_INDEX, 55)
L_ENDCOUNT = RAND(3, 13)
LOCAL = 0
LOCAL:1 = 0  ; 実際についたTALENT個数

DO
LOCAL ++
;<!>性格パラメータ値積算
{
	TYPEネアカ度 = (TALENT:L_I:눈에띄고싶음 - TALENT:L_I:얌전함) + TALENT:L_I:매력
				 + (TALENT:L_I:감수성 - TALENT:L_I:감정결여) + (TALENT:L_I:해방 - TALENT:L_I:억압)
				 + (TALENT:L_I:낙관적 + TALENT:L_I:비관적) + (TALENT:L_I:고통에강함 - TALENT:L_I:고통에약함)
				 + (TALENT:L_I:부끄러움없음 - TALENT:L_I:부끄럼쟁이) - TALENT:L_I:얀데레 + TALENT:L_I:매혹
				 + TALENT:L_I:카리스마 + TALENT:L_I:인기 + TALENT:L_I:소악마 + TALENT:L_I:고무
				 - TALENT:L_I:소극적 - TALENT:L_I:경계심 - TALENT:L_I:인간불신 + TALENT:L_I:미모
				 + (TALENT:L_I:백치녀 - TALENT:L_I:천재) - TALENT:L_I:고원의꽃 + TALENT:L_I:극상의암컷
				 + TALENT:L_I:마돈나
}
{
	TYPEスケベ度 = (TALENT:L_I:남미새 - TALENT:L_I:남혐) + (TALENT:L_I:호기심 - TALENT:L_I:무관심)
				 + (TALENT:L_I:해방 - TALENT:L_I:억압) + (TALENT:L_I:젖기쉬움 - TALENT:L_I:젖기힘듦)
				 + (TALENT:L_I:부끄러움없음 - TALENT:L_I:부끄럼쟁이) + (TALENT:L_I:쾌감솔직 - TALENT:L_I:쾌감부정)
				 + (TALENT:L_I:정조관념없음 - TALENT:L_I:정조관념) + (TALENT:L_I:습득빠름 - TALENT:L_I:습득느림)
				 + (TALENT:L_I:도착적 - TALENT:L_I:보수적) + (TALENT:L_I:사디 - TALENT:L_I:마조)
				 + TALENT:L_I:얀데레 - TALENT:L_I:유치 + TALENT:L_I:혀놀림 - TALENT:L_I:선을넘지않음
				 + TALENT:L_I:자위하기쉬움 + TALENT:L_I:중독되기쉬움 + TALENT:L_I:소악마
				 + TALENT:L_I:허리기술 - TALENT:L_I:자제심 + TALENT:L_I:손놀림
				 + (TALENT:L_I:금사빠 + TALENT:L_I:즉각타락 - TALENT:L_I:경계심 - TALENT:L_I:인간불신)
				 + TALENT:L_I:키스마 + TALENT:L_I:색마 + TALENT:L_I:항문광
				 + TALENT:L_I:성녀의자궁 + TALENT:L_I:극상의암컷
				 + TALENT:L_I:강간원망
}
	;- 人当たり
{
	TYPE優しさ = TALENT:L_I:치료 + (TALENT:L_I:정직 - TALENT:L_I:건방짐)
			   - TALENT:L_I:츤데레 - (TALENT:L_I:프라이드높음 - TALENT:L_I:프라이드낮음)
			   + (TALENT:L_I:습득빠름 - TALENT:L_I:습득느림) - (TALENT:L_I:사디 - TALENT:L_I:마조)
			   + TALENT:L_I:유치 - TALENT:L_I:얀데레 + (TALENT:L_I:헌신적 - TALENT:L_I:반항적)
			   + TALENT:L_I:고무 + TALENT:L_I:금사빠 - TALENT:L_I:경계심 - TALENT:L_I:인간불신
}
	;- ストレスや肉体的強さ
{
	TYPE強さ = (TALENT:L_I:다부짐 - TALENT:L_I:겁쟁이) + TALENT:L_I:츤데레
			 + (TALENT:L_I:프라이드높음 - TALENT:L_I:프라이드낮음)
			 + (TALENT:L_I:고통에강함 - TALENT:L_I:고통에약함)
			 + (TALENT:L_I:습득빠름 - TALENT:L_I:습득느림)
			 + TALENT:L_I:도착적 + TALENT:L_I:마조
			 + (TALENT:L_I:회복빠름 - TALENT:L_I:회복느림) + TALENT:L_I:카리스마
			 - TALENT:L_I:유치 + TALENT:L_I:선을넘지않음 - TALENT:L_I:중독되기쉬움
			 + TALENT:L_I:자제심 + (TALENT:L_I:저항 - TALENT:L_I:의존)
			 + (- TALENT:L_I:금사빠 - TALENT:L_I:즉각타락 + TALENT:L_I:경계심 - TALENT:L_I:인간불신)
			 + TALENT:L_I:강간원망
}
	; 正規化
	L_SUM_ALLTYPE = TYPEネアカ度 + TYPEスケベ度 + TYPE優しさ + TYPE強さ
	IF L_SUM_ALLTYPE
		TYPEネアカ度 = (4 * MAX((LOCAL:1)/3,1) * (4 * TYPEネアカ度 - L_SUM_ALLTYPE)) / (4 * L_SUM_ALLTYPE)
		TYPEスケベ度 = (4 * MAX((LOCAL:1)/3,1) * (4 * TYPEスケベ度 - L_SUM_ALLTYPE)) / (4 * L_SUM_ALLTYPE)
		TYPE優しさ   = (4 * MAX((LOCAL:1)/3,1) * (4 * TYPE優しさ   - L_SUM_ALLTYPE)) / (4 * L_SUM_ALLTYPE)
		TYPE強さ     = (4 * MAX((LOCAL:1)/3,1) * (4 * TYPE強さ     - L_SUM_ALLTYPE)) / (4 * L_SUM_ALLTYPE)
	ENDIF
	;DEBUGPRINTFORML SET_RANDOM_TALENT_MENTAL-LOOP:[{LOCAL:1}]: ネアカ度 = {TYPEネアカ度}, スケベ度 = {TYPEスケベ度}, 優しさ= {TYPE優しさ}, 強さ= {TYPE強さ}

	SELECTCASE TAL_INDEX:LOCAL
	CASE 0
		IF TYPE優しさ >= 0 || !RAND:5
			TALENT:L_I:치료 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 1
		SIF TALENT:L_I:눈에띄고싶음 || TALENT:L_I:얌전함
			CONTINUE
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:눈에띄고싶음 = 1
		ELSEIF !RAND:5 || (TYPEネアカ度 < 0 && RAND:(2 - TYPEネアカ度))
			TALENT:L_I:얌전함 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 2
		SIF TALENT:L_I:다부짐 || TALENT:L_I:겁쟁이
			CONTINUE
		IF !RAND:5 || (TYPE強さ >= 0 && RAND:(2 + TYPE強さ))
			TALENT:L_I:다부짐 = 1
		ELSEIF !RAND:5 || (TYPE強さ <= 0 && RAND:(2 - TYPE強さ))
			TALENT:L_I:겁쟁이 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 3
		SIF TALENT:L_I:건방짐 || TALENT:L_I:정직
			CONTINUE
		IF !RAND:5 || (TYPE優しさ <= 0 && RAND:(2 - TYPE優しさ))
			TALENT:L_I:건방짐 = 1
		ELSEIF !RAND:5 || (TYPE優しさ >= 0 && RAND:(2 + TYPE優しさ))
			TALENT:L_I:정직 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 4
		IF (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度)) || !RAND:5
			TALENT:L_I:매력 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 5
		SIF TALENT:L_I:남미새 || TALENT:L_I:남혐
			CONTINUE
		IF !RAND:5 || (TYPEスケベ度 > 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:남미새 = 1
			IF TALENT:L_I:동성애
				IF !TALENT:L_I:백합공주 && RAND:3
					TALENT:L_I:동성애 = 0
					TALENT:L_I:바이 = 1
				ELSE
					TALENT:L_I:남미새 = 0
					CONTINUE
				ENDIF
			ENDIF
		ELSEIF !RAND:5 || (TYPEスケベ度 <= 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:남혐 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 6
		SIF TALENT:L_I:무관심 || TALENT:L_I:호기심
			CONTINUE
		IF !RAND:5 || (TYPEスケベ度 < 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:무관심 = 1
		ELSEIF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:호기심 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 7
		SIF TALENT:L_I:감수성 || TALENT:L_I:감정결여
			CONTINUE
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:감수성 = 1
		ELSEIF !RAND:5 || (TYPEネアカ度 <= 0 && RAND:(2 - TYPEネアカ度))
			TALENT:L_I:감정결여 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 8
		IF !RAND:5 || ((TYPEスケベ度 - TYPE優しさ) >= 0 && RAND:(2 + TYPEスケベ度 - TYPE優しさ))
			TALENT:L_I:츤데레 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 9
		SIF TALENT:L_I:억압 || TALENT:L_I:해방
			CONTINUE
		IF !RAND:5 || (TYPEネアカ度 < 0 && RAND:(2 - TYPEネアカ度)) || (TYPEスケベ度 < 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:억압 = 1
		ELSEIF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度)) || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:해방 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 10
		SIF TALENT:L_I:젖기힘듦 || TALENT:L_I:젖기쉬움
			CONTINUE
		IF !RAND:5 || (TYPEスケベ度 < 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:젖기힘듦 = 1
		ELSEIF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:젖기쉬움 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 11
		SIF TALENT:L_I:프라이드높음 || TALENT:L_I:프라이드낮음
			CONTINUE
		IF !RAND:5 || (TYPE優しさ < 0 && RAND:(2 - TYPE優しさ))
			TALENT:L_I:프라이드높음 = 1
		ELSEIF !RAND:5 || (TYPE優しさ >= 0 && RAND:(2 + TYPE優しさ))
			TALENT:L_I:프라이드낮음 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 12
		SIF TALENT:L_I:낙관적 || TALENT:L_I:비관적
			CONTINUE
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:낙관적 = 1
		ELSEIF !RAND:5 || (TYPEネアカ度 <= 0 && RAND:(2 - TYPEネアカ度))
			TALENT:L_I:비관적 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 13
		SIF TALENT:L_I:고통에강함 || TALENT:L_I:고통에약함
			CONTINUE
		IF !RAND:5 || (TYPE強さ >= 0 && RAND:(2 + TYPE強さ))
			TALENT:L_I:고통에강함 = 1
		ELSEIF !RAND:5 || (TYPE強さ < 0 && RAND:(2 - TYPE強さ))
			TALENT:L_I:고통에약함 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 14
		SIF TALENT:L_I:부끄러움없음 || TALENT:L_I:부끄럼쟁이
			CONTINUE
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度)) || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:부끄러움없음 = 1
		ELSEIF !RAND:5 || (TYPEネアカ度 <= 0 && RAND:(2 - TYPEネアカ度)) && (TYPEスケベ度 <= 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:부끄럼쟁이 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 15
		SIF TALENT:L_I:쾌감부정 || TALENT:L_I:쾌감솔직
			CONTINUE
		IF !RAND:5 || (TYPEスケベ度 <= 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:쾌감부정 = 1
		ELSEIF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:쾌감솔직 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 16
		SIF TALENT:L_I:정조관념 || TALENT:L_I:정조관념없음
			CONTINUE
		IF !RAND:5 || (TYPEスケベ度 <= 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:정조관념 = 1
		ELSEIF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:정조관념없음 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 17
		SIF TALENT:L_I:습득빠름 || TALENT:L_I:습득느림
			CONTINUE
		IF !RAND:5 || (TYPE強さ >= 0 && RAND:(2 + TYPE強さ))
			TALENT:L_I:습득빠름 = 1
		ELSEIF !RAND:5 || (TYPE強さ <= 0 && RAND:(2 - TYPE強さ))
			TALENT:L_I:습득느림 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 18
		SIF TALENT:L_I:도착적 || TALENT:L_I:보수적
			CONTINUE
		IF !RAND:5 || ((TYPEスケベ度-TYPE強さ) >= 0 && RAND:(2 + TYPEスケベ度 - TYPE強さ))
			TALENT:L_I:도착적 = 1
		ELSEIF !RAND:5 || (TYPEスケベ度 < 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:보수적 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 19
		SIF TALENT:L_I:사디 || TALENT:L_I:마조
			CONTINUE
		IF !RAND:5 || ((TYPE強さ - TYPE優しさ) >= 0 && RAND:(2 + TYPE強さ - TYPE優しさ))
			TALENT:L_I:사디 = 1
		ELSEIF !RAND:5 || ((TYPE強さ + TYPEスケベ度) > 0 && RAND:(2 + TYPE強さ + TYPEスケベ度))
			TALENT:L_I:마조 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 20
		SIF TALENT:L_I:회복느림 || TALENT:L_I:회복빠름
			CONTINUE
		IF !RAND:5 || (TYPE強さ <= 0 && RAND:(2 - TYPE強さ))
			TALENT:L_I:회복느림 = 1
		ELSEIF !RAND:5 || (TYPE強さ >= 0 && RAND:(2 + TYPE強さ))
			TALENT:L_I:회복빠름= 1
		ELSE
			CONTINUE
		ENDIF
	CASE 21
		;TALENT:L_I:광기 = 1
		CONTINUE
	CASE 22
		IF !RAND:5 || ((TYPEスケベ度 - TYPE優しさ - TYPEネアカ度) >= 0 && RAND:(2 + TYPEスケベ度 - TYPE優しさ - TYPEネアカ度))
			TALENT:L_I:얀데레 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 23
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:매혹 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 24
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:카리스마 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 25
		TALENT:L_I:유치 = 1
	CASE 26
		SIF TALENT:L_I:고양이혀 || TALENT:L_I:혀놀림 
			CONTINUE
		IF !RAND:2
			TALENT:L_I:고양이혀 = 1
		ELSE
			TALENT:L_I:혀놀림 = 1
		ENDIF
	CASE 27
		IF !RAND:5 || (TYPEスケベ度 <= 0 && RAND:(2 - TYPEスケベ度))
			TALENT:L_I:선을넘지않음 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 28
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:자위하기쉬움 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 29
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:중독되기쉬움 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 30
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:인기 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 31
		SIF TALENT:L_I:헌신적 || TALENT:L_I:반항적
			CONTINUE
		IF !RAND:5 || (TYPE優しさ >= 0 && RAND:(2 + TYPE優しさ))
			TALENT:L_I:헌신적 = 1
		ELSEIF !RAND:5 || (TYPE優しさ <= 0 && RAND:(2 - TYPE優しさ))
			TALENT:L_I:반항적 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 32
		IF !RAND:3 || ((TYPEネアカ度 + TYPEスケベ度) >= 0 && RAND:(2 + TYPEネアカ度 + TYPEスケベ度))
			TALENT:L_I:소악마 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 33
		IF !RAND:5 || (TYPE優しさ >= 0 && RAND:(2 + TYPE優しさ))
			TALENT:L_I:고무 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 34
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:허리기술 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 35
		IF !RAND:3 || ((TYPE強さ - TYPEスケベ度) >= 0 && RAND:(2 + TYPE強さ - TYPEスケベ度))
			TALENT:L_I:자제심 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 36
		IF !RAND:5 || (TYPEネアカ度 <= 0 && RAND:(2 - TYPEネアカ度))
			TALENT:L_I:소극적 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 37
		SIF TALENT:L_I:의존 || TALENT:L_I:저항
			CONTINUE
		IF !RAND:5 || (TYPE強さ <= 0 && RAND:(2 - TYPE強さ))
			TALENT:L_I:의존 = 1
		ELSEIF !RAND:5 || (TYPE強さ >= 0 && RAND:(2 + TYPE強さ))
			TALENT:L_I:저항 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 38
		IF !RAND:2
			TALENT:L_I:동성애 = 1
			IF RAND:10 < 6
				TALENT:L_I:백합공주 = 1
				LOCAL:1 ++
				IF TYPEスケベ度 < 0 && !RAND:5
					IF TALENT:L_I:남미새
						TALENT:L_I:남미새 = 0
						LOCAL:1 --
					ELSEIF !TALENT:L_I:남혐 && !TALENT:L_I:남미새
						TALENT:L_I:남혐 = 1
						LOCAL:1 ++
					ENDIF
				ENDIF					
			ENDIF
			IF TALENT:L_I:남미새
				; 남미새があると 1/3 に分岐
				IF !RAND:3
					TALENT:L_I:남미새 = 0
					LOCAL:1 --
				ELSEIF RAND:2
					TALENT:L_I:바이 = 1
					TALENT:L_I:동성애 = 0
				ELSE
					TALENT:L_I:동성애 = 0
					CONTINUE
				ENDIF
			ENDIF
		ELSE
			TALENT:L_I:바이= 1
			;<!>남혐との干渉
			IF TALENT:L_I:남혐
				TALENT:L_I:남혐 = RAND:2
				TALENT:L_I:바이 = TALENT:L_I:바이 ^^ TALENT:L_I:남혐	; 바이と남혐でXOR
				SIF !TALENT:L_I:바이
					LOCAL:1--
			ENDIF
		ENDIF
	CASE 39
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:손놀림= 1
		ELSE
			CONTINUE
		ENDIF
	CASE 40
		SIF TALENT:L_I:금사빠 || TALENT:L_I:즉각타락 || TALENT:L_I:경계심 || TALENT:L_I:인간불신 
			CONTINUE
		IF !RAND:4 && (TYPEネアカ度 <= 0 && RAND(2 - TYPEネアカ度))
			TALENT:L_I:인간불신 = 1
		ELSEIF !RAND:3 && (TYPE強さ >= 0 && RAND(2 + TYPE強さ))
			TALENT:L_I:경계심 = 1
		ELSEIF !RAND:2 && (TYPEスケベ度 >= 0 && RAND(2 + TYPEスケベ度))
			TALENT:L_I:즉각타락 = 1
		ELSEIF (TYPEスケベ度 >= 0 && RAND(2 + TYPEスケベ度))
			TALENT:L_I:금사빠 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 41
		IF !RAND:5 || (TYPEネアカ度 >= 0 && RAND(2 + TYPEネアカ度))
			TALENT:L_I:미모 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 42
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度)) || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:키스마 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 43
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:색마 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 44
		IF !RAND:5 || (TYPEスケベ度 >= 0 && RAND:(2 + TYPEスケベ度))
			TALENT:L_I:항문광 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 45
		SIF TALENT:L_I:백치녀 || TALENT:L_I:천재
			CONTINUE
		IF !RAND:2 || (TYPEネアカ度 >= 0 && RAND:(2 + TYPEネアカ度))
			TALENT:L_I:백치녀 = 1
		ELSEIF !RAND:2 || (TYPEネアカ度 <= 0 && RAND:(2 - TYPEネアカ度))
			TALENT:L_I:천재 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 46
		IF !RAND:5 || ((TYPEスケベ度 - TYPEネアカ度) >= 0 && RAND:(2 + TYPEスケベ度 - TYPEネアカ度))
			TALENT:L_I:고원의꽃 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 47
		IF  RAND:100 <= 5
			TALENT:L_I:「시스터」= 1
		ELSEIF  RAND:100 <= 5
			TALENT:L_I:「뮤추얼」= 1
		ELSE
			CONTINUE
		ENDIF
	CASE 48
		IF  RAND:100 <= 20
			TALENT:L_I:기억처리완료= 1
		ELSE
			CONTINUE
		ENDIF
	CASE 49
		SIF TALENT:L_I:임신하기쉬움 || TALENT:L_I:임신하기어려움 || TALENT:L_I:성녀의자궁
			CONTINUE
		IF !RAND:2
			TALENT:L_I:임신하기쉬움 = 1
		ELSE
			TALENT:L_I:임신하기어려움 = 1
		ENDIF
		IF  RAND:100 <= 5
			TALENT:L_I:임신하기쉬움 = 1
			TALENT:L_I:임신하기어려움 = 0
			TALENT:L_I:성녀의자궁 = 1
			LOCAL:1 ++
		ENDIF
	CASE 50
		IF  RAND:100 <= 5
			TALENT:L_I:극상의암컷 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 51
		IF  RAND:100 <= 20
			TALENT:L_I:마돈나 = 1
		ELSE
			CONTINUE
		ENDIF
	CASE 52
		TALENT:L_I:강간원망 = 1
	CASE 53
		SIF  RAND:100 <= 10
			TALENT:L_I:AI전속피험자 = 1
	CASE 54
		SIF  RAND:100 <= 20
			TALENT:L_I:모유체질 = 1
	CASEELSE
	ENDSELECT
	
	; <!> 条件判定によるCONTINUE多用のため、For ループ廃止
	LOCAL:1 ++

LOOP LOCAL:1 < L_ENDCOUNT
;DEBUGPRINTFORML SET_RANDOM_TALENT_MENTAL-LOOP:[TALENT Finally COUNT={LOCAL:1}]:
;DEBUGPRINTFORML ネアカ度 = {TYPEネアカ度}, スケベ度 = {TYPEスケベ度}, 優しさ= {TYPE優しさ}, 強さ= {TYPE強さ}
;DEBUGPRINTL

;----------------------------------------------------------
;----------------------------------------------------------
;素質関連（男用性格）
;----------------------------------------------------------
;----------------------------------------------------------
;FUNC_FISHER_YATES_SHAFFLEによりランダム配列を生成することで抽選の重複を排除した
;この時点ではランダムキャラはタレントがまっさらな状態なはずだが
;念のために素質判定を入れて弾いておく
@SET_RANDOM_TALENT_MENTAL_O(L_I)
#DIM L_I
#DIM TAL_NUM
#DIM TAL_INDEX, 16
CALLF FUNC_FISHER_YATES_SHAFFLE(TAL_INDEX, 16)
FOR LOCAL, 0, RAND(3, 5)
	SELECTCASE TAL_INDEX:LOCAL
	CASE 0
		IF !RAND:2
			TALENT:L_I:미남 = 1
		ELSE
			TALENT:L_I:추남 = 1
		ENDIF
	CASE 1
		IF !RAND:2
			TALENT:L_I:육식계 = 1
		ELSE
			TALENT:L_I:초식계 = 1
		ENDIF
	CASE 2
		IF !RAND:2
			TALENT:L_I:허리기술 = 1
		ELSE
			TALENT:L_I:손놀림 = 1
		ENDIF
	CASE 3
		IF !RAND:2
			TALENT:L_I:실좆 = 1
			CFLAG:L_I:ペニスサイズ = 2
		ELSE
			TALENT:L_I:거근 = 1
			CFLAG:L_I:ペニスサイズ = 3
		ENDIF
	CASE 4
		IF !RAND:2
			TALENT:L_I:정조관념없음 = 1
		ELSE
			TALENT:L_I:정조관념 = 1
		ENDIF
	CASE 5
		IF !RAND:2
			TALENT:L_I:쾌감부정 = 1
		ELSE
			TALENT:L_I:쾌감솔직 = 1
		ENDIF
	CASE 6
		IF !RAND:2
			TALENT:L_I:정직 = 1
		ELSE
			TALENT:L_I:반항적 = 1
		ENDIF
	CASE 7
		IF !RAND:2
			TALENT:L_I:얌전함 = 1
		ELSE
			TALENT:L_I:건방짐 = 1
		ENDIF
	CASE 8
		IF !RAND:2
			TALENT:L_I:소극적 = 1
		ELSE
			TALENT:L_I:눈에띄고싶음 = 1
		ENDIF
	CASE 9
		IF !RAND:2
			TALENT:L_I:보수적 = 1
		ELSE
			TALENT:L_I:호기심 = 1
		ENDIF
	CASE 10
		IF !RAND:2
			TALENT:L_I:강간원망 = 1
		ELSE
			TALENT:L_I:억압 = 1
		ENDIF
	CASE 12
		IF !RAND:2
			TALENT:L_I:습득느림 = 1
		ELSE
			TALENT:L_I:습득빠름 = 1
		ENDIF
	CASE 13
		IF !RAND:2
			TALENT:L_I:우등유전자 = 1
		ELSE
			TALENT:L_I:열등유전자 = 1
		ENDIF
	CASE 14
		IF !RAND:2
			TALENT:L_I:농후정액 = 1
		ELSE
			TALENT:L_I:희박정액 = 1
		ENDIF
	CASE 15
		IF !RAND:2
			TALENT:L_I:절륜 = 1
			MAXBASE:L_I:체력 = 400
			BASE:L_I:체력 = MAXBASE:L_I:체력
		ELSE
			TALENT:L_I:담백 = 1
			MAXBASE:L_I:체력 = 100
			BASE:L_I:체력 = MAXBASE:L_I:체력
		ENDIF
	CASEELSE
	ENDSELECT
NEXT



;----------------------------------------------------------
;----------------------------------------------------------
;素質関連（身体）
;----------------------------------------------------------
;----------------------------------------------------------
@SET_RANDOM_TALENT_SENCIBITY(L_I)
#DIM L_I
#DIM TAL_NUM
#DIM TAL_INDEX, 4
CALLF FUNC_FISHER_YATES_SHAFFLE(TAL_INDEX, 4)
FOR LOCAL, 0, RAND(1, 3)
	SELECTCASE TAL_INDEX:LOCAL
	CASE 0
		SIF TALENT:L_I:Ｖ민감 || TALENT:L_I:Ｖ둔감
			CONTINUE
		IF !RAND:2
			TALENT:L_I:Ｖ민감 = 1
		ELSE
			TALENT:L_I:Ｖ둔감 = 1
		ENDIF
		;<!> 색마との相互関係
		IF TALENT:L_I:색마 && TALENT:L_I:Ｖ둔감
			IF !RAND:10
				TALENT:L_I:색마 = 0
			ELSEIF !RAND:5
				TALENT:L_I:Ｖ둔감 = 0
			ENDIF
		ELSEIF !TALENT:L_I:색마 && TALENT:L_I:Ｖ민감
			IF !RAND:5
				TALENT:L_I:색마 = 1
			ELSEIF !RAND:10
				TALENT:L_I:Ｖ민감 = 0
			ENDIF
		ENDIF
	CASE 1
		SIF TALENT:L_I:Ｂ민감 || TALENT:L_I:Ｂ둔감
			CONTINUE
		IF !RAND:2
			TALENT:L_I:Ｂ민감 = 1
		ELSE
			TALENT:L_I:Ｂ둔감 = 1
		ENDIF
	CASE 2
		SIF TALENT:L_I:Ｃ민감 || TALENT:L_I:Ｃ둔감
			CONTINUE
		IF !RAND:2
			TALENT:L_I:Ｃ민감 = 1
		ELSE
			TALENT:L_I:Ｃ둔감 = 1
		ENDIF
	CASE 3
		SIF TALENT:L_I:Ａ민감 || TALENT:L_I:Ａ둔감
			CONTINUE
		IF !RAND:2
			TALENT:L_I:Ａ민감 = 1
		ELSE
			TALENT:L_I:Ａ둔감 = 1
		ENDIF
		;<!> 항문광との干渉
		IF TALENT:L_I:항문광 && TALENT:L_I:Ａ둔감
			IF !RAND:10
				TALENT:L_I:항문광 = 0
			ELSEIF !RAND:5
				TALENT:L_I:Ａ둔감 = 0
			ENDIF
		ELSEIF !TALENT:L_I:항문광 && TALENT:L_I:Ａ민감
			IF !RAND:5
				TALENT:L_I:항문광 = 1
			ELSEIF !RAND:10
				TALENT:L_I:Ａ민감 = 0
			ENDIF
		ENDIF
	CASEELSE
	ENDSELECT
NEXT




