;-------------------------------------------------
;DUNGEON_5：Lv5:完全侵食区域[現代迷宮]
;-------------------------------------------------
@DUNGEON_5_NAME(ISMODIF = 0)
#DIM ISMODIF
#DIMS MODIF
MODIF = （장기 레벨 1）
RESULTS = 레벨 5:완전침식구역[현대미궁]%MODIF * (ISMODIF != 0)%
RETURN


@DUNGEON_5_ABLE
RETURN 1


@DUNGEON_5_FE(STEP)
#DIM FE
#DIM STEP
IF STEP % 50 == 0 && POSSIB(15 + DEFEAT_EID24)
	;50階ごとに判定
	FE = 5
ELSE
	;共通関数
	CALL DUNG_FE_COMMON
	FE = RESULT
ENDIF
RETURN FE

@DUNGEON_5_PERSON
#DIM PER
SELECTCASE CHUSEN(25 * (FLAG:調査深度 >= 30) , 25 * (FLAG:調査深度 >= 70) , 25 * (FLAG:調査深度 >= 50) , 25)
	CASE 0
		;傷ついた조인
		PER = 500
	CASE 1
		;冷凍カプセル
		PER = 400
	CASE 2
		;오니との出会い
		PER = 501
	CASEELSE
		;성노예救出
		PER = 401
ENDSELECT
RETURN PER


@DUNGEON_5_ENEMY(RARE = -1)
#DIM RARE  ;レア敵フラグ。1ならレア敵が確実に出現する(レア敵が設定されているダンジョンのみ)
#DIM ENE
SIF RARE == -1
	RARE = POSSIB(3)

IF RARE == 1
	;レア敵
	SELECTCASE CHUSEN(33 , 33 , 33)
		CASE 0
			;ヴルトゥーム
			ENE = 13
		CASE 1
			;ロードビヤーキー
			ENE = 14
		CASEELSE
			;ニョグダ
			ENE = 15
	ENDSELECT
ELSE
	;通常敵
	;アイホートの雛
	ENE = 24
ENDIF
RETURN ENE


@DUNGEON_5_AFTERBATTLE(EID , WIN)
#DIM EID
#DIM WIN
;アイホートの雛相手の撃破数カウント
SIF WIN == 1 && EID == 24
	DEFEAT_EID24++

;もし敵がアイホートならアイホートの雛の撃破数リセット(_SPBATTLEでやっているはずだが念のため)
SIF EID == 23
	DEFEAT_EID24 = 0

;アイホートを撃破したならアーティファクト進呈
IF EID == 23 && WIN == 1
	DRAWLINE
	PRINTL 
	CALL RPG_GET_ART_UNIQUE(4 , FLAG:調査キャラ > 0 ? FLAG:調査キャラ # -1 , 1)
	PRINTL 
	DRAWLINE
ENDIF
RETURN


@DUNGEON_5_ITEM
#DIM IT
SELECTCASE CHUSEN(20 * (FLAG:調査深度 >= 50) , 20 , 20 , 20 * (FLAG:調査深度 >= 20) , 20)
	CASE 0
		;ゲノム入手。調査深度50以降
		IT = 100
	CASE 1
		;書物閲覧
		IT = 101
	CASE 2
		;古代遺跡発見
		IT = 102
	CASE 3
		;蜘蛛の巣調査
		IT = 150
	CASEELSE
		;キャンプ跡地発見
		IT = 103
ENDSELECT
RETURN IT


;ダンジョンの各種パラメータ設定
;FLAG:調査瘴気くらい？FLAG:調査深度も一応設定できる
@DUNGEON_5_PARAM
FLAG:調査瘴気 = 3
FLAG:索敵難易度 = 700
FLAG:深度上限 = 200
RETURN


@DUNGEON_5_SPBATTLE
;アイホートの雛の撃破数リセット
DEFEAT_EID24 = 0
IF EXPLANATION_ID23 == 0
	DRAWLINE
	PRINTFORMW 창백한 타원형 거구에 달린 무수하게 붉게 빛나는 눈이 자신을 내려다보고 있다.
	PRINTFORMW 손을 내밀어 오는 기색은 없다. 당연하다. 종복을 적으로 보는 주인이 없으니.
	PRINTFORMW ……이상하다. 종복? 주인? 언제부터? 어째서? 태어날 때부터 그렇지 않은가. 약자가 강자에게 따른……아니다.
	PRINTFORMW %조사처리(CALLNAME:(FLAG:調査キャラ),"는")% 모호해진 위화감을 끌어내어 자신의 제정신을 확인한다. 저 빨간 눈이 뭔가 하고 있는 것이다.
	PRINTFORMW 그 자가 물었다. 「종속될 것인가?」라고. 「싫다」라고 답했다.
	PRINTFORMW 그 자가 말했다. 「생명을 내어라.」라고. 「싫다」라고 답했다.
	PRINTFORMW 다음 말은 없었다. 무수한 푸른 눈이 자신을 내려다 보고 있다.
	EXPLANATION_ID23 = 1
ENDIF

;逃走可
CALL RPG_BATTLE(FLAG:調査キャラ , 23 , FLOORTER:NOWFLOOR ,  ,  ,  , FLAG:調査区域)
CALL RPG_BATTLE_RESULT(FLAG:調査キャラ , 23)
RETURN
