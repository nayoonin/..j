;-------------------------------------------------
;DUNGEON_3：Lv3:危険区域
;-------------------------------------------------
@DUNGEON_3_NAME(ISMODIF = 0)
#DIM ISMODIF
#DIMS MODIF
RESULTS = 레벨 3:위험구역%MODIF * (ISMODIF != 0)%
RETURN


@DUNGEON_3_ABLE
RETURN 1


@DUNGEON_3_PERSON
#DIM PER
SELECTCASE CHUSEN(25 * (FLAG:調査深度 >= 30) , 25 * (FLAG:調査深度 >= 70) , 25 * (FLAG:調査深度 >= 50) , 25)
	CASE 0
		;傷ついた조인
		PER = 500
	CASE 1
		;冷凍カプセル
		PER = 400
	CASE 2
		;오니との出会い
		PER = 501
	CASEELSE
		;성노예救出
		PER = 401
ENDSELECT
RETURN PER


@DUNGEON_3_ENEMY(RARE = -1)
#DIM RARE  ;レア敵フラグ。1ならレア敵が確実に出現する(レア敵が設定されているダンジョンのみ)
#DIM ENE
SIF RARE == -1
	RARE = POSSIB(3)

IF RARE == 1
	;レア敵
	SELECTCASE CHUSEN(33 , 33 , 33)
		CASE 0
			;ヴルトゥーム
			ENE = 13
		CASE 1
			;ロードビヤーキー
			ENE = 14
		CASEELSE
			;ニョグダ
			ENE = 15
	ENDSELECT
ELSE
	;通常敵
	IF FLAG:調査深度 > 60
		SELECTCASE CHUSEN(33 , 33 , 33)
			CASE 0
				;落とし子
				ENE = 28
			CASE 1
				;深きもの
				ENE = 29
			CASEELSE
				;ビヤーキー
				ENE = 30
		ENDSELECT
	ELSE
		;ASのデフォルトでは60階以下、30階以下、15階以下と結構細かく設定が分割されているが
		;統一しちゃう
		SELECTCASE CHUSEN(33 , 33 , 33)
			CASE 0
				;ムーンビースト
				ENE = 31
			CASE 1
				;星の精
				ENE = 32
			CASEELSE
				;古きもの
				ENE = 33
		ENDSELECT
		
	ENDIF
ENDIF
RETURN ENE


@DUNGEON_3_ITEM
#DIM IT
SELECTCASE CHUSEN(25 * (FLAG:調査深度 >= 50) , 25 , 25 , 25)
	CASE 0
		;ゲノム入手。調査深度50以降
		IT = 100
	CASE 1
		;書物閲覧
		IT = 101
	CASE 2
		;古代遺跡発見
		IT = 102
	CASE 3
		;キャンプ跡地発見
		IT = 103
ENDSELECT
RETURN IT


;ダンジョンの各種パラメータ設定
;FLAG:調査瘴気くらい？FLAG:調査深度も一応設定できる
@DUNGEON_3_PARAM
FLAG:調査瘴気 = 0
FLAG:索敵難易度 = 180
FLAG:深度上限 = 70
RETURN
