;-------------------------------------------------
;유니크アーティファクト入手
;ARTIDに유니크アーティファクトIDを、DISCOVに発見者として記録したいキャラの番号を受け取る
;MESSAGEに非0を受け取ると入手メッセージを表示
;DISCOV、MESSAGEは未設定でも可
;-------------------------------------------------
@RPG_GET_ART_UNIQUE(ARTID = -1 , DISCOV = -1 , MESSAGE = 0)
#DIM ARTID
#DIM RANK
#DIM ATK
#DIM DEF
#DIM HIT
#DIM SPD
#DIM LUK
#DIM GEN
#DIM ABILIT
#DIM MADE
#DIM TRGT
#DIM DISCOV
#DIM MESSAGE
#DIMS NAMES
#DIMS ABLS
#DIMS MADES
#DIMS RANKS
#DIMS EVALS

CALLFORM ART_UNIQUE_{ARTID}_NAME
NAMES = %RESULTS%


CALLFORM ART_UNIQUE_{ARTID}_ABLE
IF RESULT == 0
	;#;PRINTFORMW 유니크 아티팩트 [%NAMES%]의 입수 조건을 만족하고 있지 않습니다.
	RETURN 0
ENDIF


CALLFORM ART_UNIQUE_{ARTID}_RANK
RANK = RESULT
SIF RANK <= 0
	THROW 유니크アーティファクト%NAMES%のランクが不正です  RANK={RANK}

CALLFORM ART_UNIQUE_{ARTID}_ATK
ATK = RESULT
SIF ATK < 0
	THROW 유니크アーティファクト%NAMES%のATKが不正です  ATK={ATK}

CALLFORM ART_UNIQUE_{ARTID}_DEF
DEF = RESULT
SIF DEF < 0
	THROW 유니크アーティファクト%NAMES%のDEFが不正です  DEF={DEF}

CALLFORM ART_UNIQUE_{ARTID}_HIT
HIT = RESULT
SIF HIT < 0
	THROW 유니크アーティファクト%NAMES%のHITが不正です  HIT={HIT}

CALLFORM ART_UNIQUE_{ARTID}_SPD
SPD = RESULT
SIF SPD < 0
	THROW 유니크アーティファクト%NAMES%のSPDが不正です  SPD={SPD}

CALLFORM ART_UNIQUE_{ARTID}_LUK
LUK = RESULT
SIF LUK < 0
	THROW 유니크アーティファクト%NAMES%のLUKが不正です  LUK={LUK}

CALLFORM ART_UNIQUE_{ARTID}_GENOME
GEN = RESULT
SIF GEN > 0 && CFLAGNAME:GEN == ""
	THROW 유니크アーティファクト%NAMES%のゲノム適性が不正です  GEN={GEN}、CFLAGNAME:GEN=%CFLAGNAME:GEN%

CALLFORM ART_UNIQUE_{ARTID}_ABILITY
ABILIT = RESULT
;ちょっとエラーチェックの方法が思いつかない

CALLFORM ART_UNIQUE_{ARTID}_MADEIN
MADE = RESULT

;以下システムデフォルトの説明文呼び出し
CALL RPG_ARTIFACT_MADEIN_STR(MADE)
MADES = %RESULTS%

CALL RPG_ARTIFACT_ABILITY_STR(ABILIT)
ABLS = %RESULTS%

CALL RPG_ARTIFACT_RANK_STR(RANK)
RANKS = %RESULTS%

CALL RPG_ARTIFACT_EVALUATION_STR(RANK)
EVALS = %RESULTS%

;代入処理
CALL RPG_ARTIFACT_FINDBRANK
TRGT = RESULT
IF TRGT == -1
	;アーティファクト配列に空きがない
	PRINTW 아티팩트 창고가 가득 찼습니다…….
	RETURN 0
ENDIF

FLAG:(18100 + TRGT) = ARTID
FLAG:(15000 + TRGT) = RANK
FLAG:(15100 + TRGT) = MADE
FLAG:(15200 + TRGT) = ABILIT
;装備者は当然まだなし
FLAG:(15300 + TRGT) = 0
FLAG:(15400 + TRGT) = ATK
FLAG:(15500 + TRGT) = DEF
FLAG:(15600 + TRGT) = HIT
FLAG:(15700 + TRGT) = SPD
FLAG:(15800 + TRGT) = LUK
FLAG:(18000 + TRGT) = GEN

SAVESTR:(5000 + TRGT) = %NAMES%
SAVESTR:(5100 + TRGT) = %MADES%
SAVESTR:(5200 + TRGT) = %ABLS%
SAVESTR:(5300 + TRGT) = %RANKS%
SAVESTR:(5400 + TRGT) = %EVALS%
IF DISCOV == -1
	SAVESTR:(5500 + TRGT) = 【발견자:　　　　】기록파손
ELSE
	SAVESTR:(5500 + TRGT) = 【발견자:　　　　】%CALLNAME:DISCOV%
ENDIF

IF MESSAGE
	PRINT 유니크 아티팩트
	CALL PRINT_LABEL(NAMES)
	PRINTFORMW %조사만처리(NAMES,"를")% 입수했다！
ENDIF
RETURN TRGT


;-------------------------------------------------
;유니크アーティファクトの入手数を数える式中関数
;ARTIDに유니크アーティファクトのIDを受け取る
;-------------------------------------------------
@RPG_ARTIFACT_COUNTUNIQUE(ARTID = -1)
#FUNCTION
#DIM ARTID
#DIM CNT
#DIM NUM
#DIM CONST VAR = 18101 , 18200
SIF ARTID == -1
	THROW ARTIDの値が不正です  ARTID={ARTID}

NUM = 0
FOR CNT , VAR:0 , VAR:1
	SIF FLAG:CNT == ARTID
		NUM++
NEXT

RETURNF NUM
